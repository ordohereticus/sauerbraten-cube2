///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  FPS specific stuff:
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
sayteamcommand = [
    inputcommand (if (>= $numargs 1) [result $arg1] [result ""]) [sayteam $commandbuf] "[team]"
]

switchteam = [
    if (strcmp (getteam) "good") [team "evil"] [team "good"]
]

togglespectator = [
    spectator (! (isspectator (getclientnum)))
]

togglezoom = [
    zoom (if (= $zoom 1) -1 1)
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
nodebug [
    defvar  songcurrent -1 -1 8

    defvarp playsong_shownowplaying 0 0 1
]
songlist  = (listshuffle [1 2 3 4 5 6 7 8])
songtitle = ["Demagogue" "Vexation" "Inchoate" "Incindental" "Tribulation" "Mephistopheles" "Antipodes" "Ostentation"]

playsong = [
    songcurrent = (+ $songcurrent 1)
    if (>= $songcurrent (listlen $songlist)) [
        songlist = (listshuffle $songlist)
        songcurrent = 0
    ]
    if $playsong_shownowplaying [
        echo (concatword "^f7NowPlaying: " (at $songtitle (- (at $songlist $songcurrent) 1)) " by Fanatic")
    ]
    music (concatword "fanatic/" (at $songlist $songcurrent) ".ogg") playsong
]
//playsong = [ music (concatword "fanatic/" (rnd 9 1) ".ogg") playsong ]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
nodebug [
    defsvar ModeName ""
]

//#####################################################################################################################################
complete importcube "packages/cube" "cgz"


//#####################################################################################################################################
mapcomplete = [ complete $arg1 "packages/base" "ogz" ]
mapcomplete map

modenames = [
    "ffa"           // mode  0
    "coop"          // mode  1
    "teamplay"      // mode  2
    "insta"         // mode  3
    "instateam"     // mode  4
    "effic"         // mode  5
    "efficteam"     // mode  6
    "tac"           // mode  7
    "tacteam"       // mode  8
    "capture"       // mode  9
    "regencapture"  // mode 10
    "ctf"           // mode 11
    "instactf"      // mode 12
    "protect"       // mode 13
    "instaprotect"  // mode 14
    "hold"          // mode 15
    "instahold"     // mode 16
    "efficctf"      // mode 17
    "efficprotect"  // mode 18
    "effichold"     // mode 19
    "collect"       // mode 20
    "instacollect"  // mode 21
    "efficcollect"  // mode 22
]
loop i (listlen $modenames) [
    ModeName = (at $modenames $i)
    alias $ModeName [ if (mode @i) [if (> $numargs 0) [map $arg1] [map]] ]
    mapcomplete $ModeName
]


//#####################################################################################################################################
spmodenames = [
    "sp"    // 0: mode -3
    "dmsp"  // 1: mode -2
]
loop i (listlen $spmodenames) [
    ModeName = (at $spmodenames $i)
    alias $ModeName [ if (mode (- @i 3)) [if (> $numargs 0) [map $arg1] [map]] ]
    mapcomplete $ModeName
]


//#####################################################################################################################################
demo = [
    stopdemo;
    if (mode -1) [if (> $numargs 0) [map $arg1] [map]]
]
varcomplete demo demodir "dmo"


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
nodebug [
    defvar speditlock 0 1 1
]

allowspedit = [ speditlock = 0; onrelease [speditlock = 1] ]

allowedittoggle = [
    if (&& (m_sp (getmode)) $speditlock) [
        error "you must hold" (prettylist (searchbinds "allowspedit") "or") "to toggle editing in single player modes"
        result 0
    ] [
        result 1
    ]
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
nodebug [
    defvar playermodelcount 1 5 -1
]

playermodeldir0   = "mrfixit"
playermodelicon0  = "mrfixit"
playermodelname0  = "Mr. Fixit"
playermodelstory0 = "Which orc do you choose when the mission calls for an all-purpose wrecking machine? Who do you call when the odds are stacked against you? Mr. Fixit, that's who. Gifted with the ability to perform deconstructive surgery with any weapon, this orc gets the job done fast and with extreme prejudice."

playermodeldir1   = "snoutx10k"
playermodelicon1  = "snoutx10k"
playermodelname1  = "IronSnout X10K"
playermodelstory1 = "IronSnout X10K is a hybrid model of the IronSnout series. Bred in the bowels of a top secret underground facility, this walking fragging machine is for one purpose: to annihilate the opposition. Part pig, part machine, IronSnout X10K knows no fear and fears no fragger."

playermodeldir2   = "ogro2"
playermodelicon2  = "ogro"
playermodelname2  = "Ogro"
playermodelstory2 = "Ogro is smaller than a normal ogre, but just as fat and only slightly less stupid. As opposed to common belief, he is not an evil creature, only killing as is absolutely necessary."

playermodeldir3   = "inky"
playermodelicon3  = "inky"
playermodelname3  = "Inky"
playermodelstory3 = "Inky is the aspiring younger sibling of an infamous transdimensional celebrity. Often teased as being only the "Lesser Evil", Inky searches the battlefields for infamy of his own, claiming it one fragger at a time."

playermodeldir4   = "captaincannon"
playermodelicon4  = "captaincannon"
playermodelname4  = "Captain Cannon"
playermodelstory4 = "Cannon Jones dared to imbibe the old imported beer can that had languished at the bottom of his refrigerator. Unbeknownst to Cannon, it had been bombarded with stray gamma radiation. After some serious indigestion, the mild-mannered Cannon Jones became the mighty Captain Cannon! Captain Cannon now has only one known weakness: his overbearingly righteous sense of justice."

playermodeldir    = [getalias (concatword "playermodeldir"   (if (> $numargs 0) [result $arg1] [result $playermodel]))]
playermodelicon   = [getalias (concatword "playermodelicon"  (if (> $numargs 0) [result $arg1] [result $playermodel]))]
playermodelname   = [getalias (concatword "playermodelname"  (if (> $numargs 0) [result $arg1] [result $playermodel]))]
playermodelstory  = [getalias (concatword "playermodelstory" (if (> $numargs 0) [result $arg1] [result $playermodel]))]

