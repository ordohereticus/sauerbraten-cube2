///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  Implements some editing utilities & commands:
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

_EntityTypes = [
    "barrel"
    "base"
    "box"
    "bullets"
    "carrot"
    "cartridges"
    "elevator"
    "envmap"
    "flag"
    "greenarmour"
    "grenades"
    "health"
    "healthboost"
    "jumppad"
    "light"
    "mapmodel"
    "monster"
    "particles"
    "platform"
    "playerstart"
    "quaddamage"
    "respawnpoint"
    "riflerounds"
    "rockets"
    "shells"
    "sound"
    "spotlight"
    "teledest"
    "teleport"
    "yellowarmour"
]
_EntitiesWithDirection = [
    "barrel"
    "box"
    "elevator"
    "flag"
    "mapmodel"
    "monster"
    "platform"
    "playerstart"
    "teledest"
]

_Labels_BlendPaintMode = [
    "off"
    "replace"           //  "replace"           Black Paints & White Erases
    "paint"             //  "dig"               Black Paints
    "erase inverse"     //  "fill"              White Erases
    "paint inverse"     //  "inverted dig"      White Paints
    "erase"             //  "inverted fill"     Black Erases
]
_Labels_TexRotate = [
    "0 degrees"
    "90 degrees"
    "180 degrees"
    "270 degrees"
    "flip X"
    "flip Y"
    "transpose"
    "flipped transpose"
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  Entities:

//#############################################################################################################################################
//  Utility Stuff:

entcomplete = [ listcomplete $arg1 $_EntityTypes ]
entcomplete newent
entcomplete entfind
entcomplete clearents

=enttype = [ || [strcmp * $arg1] [strcmp (enttype)  $arg1] ]
=entattr = [ || [strcmp * $arg2] [= (entattr $arg1) $arg2] ]

//  Entity Attributes changed with "editpush":
_entaction_base         = [entproperty 0 ( * $arg1  1 )]
_entaction_teleport     = [entproperty 0 ( * $arg1  1 )]
_entaction_teledest     = [entproperty 1 ( * $arg1  1 )]
_entaction_mapmodel     = [entproperty 1 ( * $arg1  1 )]
_entaction_spotlight    = [entproperty 0 ( * $arg1  5 )]
_entaction_light        = [entproperty 0 ( * $arg1  5 )]
_entaction_jumppad      = [entproperty 0 ( * $arg1  5 )]
_entaction_respawnpoint = [entproperty 0 ( * $arg1 15 )]
_entaction_playerstart  = [entproperty 0 ( * $arg1 15 )]
_entaction_envmap       = [entproperty 0 ( * $arg1  5 )]
_entaction_particles    = [entproperty 0 ( * $arg1  1 )]
_entaction_sound        = [entproperty 0 ( * $arg1  1 )]
_entaction_cycle        = [entset ( if ( > $arg1 -1 ) [ result $arg2 ] [ result $arg3 ] )]
_entaction_shells       = [_entaction_cycle $arg1 bullets quaddamage]
_entaction_bullets      = [_entaction_cycle $arg1 rockets shells]
_entaction_rockets      = [_entaction_cycle $arg1 riflerounds bullets]
_entaction_riflerounds  = [_entaction_cycle $arg1 grenades rockets]
_entaction_grenades     = [_entaction_cycle $arg1 cartridges riflerounds]
_entaction_cartridges   = [_entaction_cycle $arg1 quaddamage grenades]
_entaction_quaddamage   = [_entaction_cycle $arg1 shells cartridges]
_entaction_health       = [_entaction_cycle $arg1 healthboost yellowarmour]
_entaction_healthboost  = [_entaction_cycle $arg1 greenarmour health]
_entaction_greenarmour  = [_entaction_cycle $arg1 yellowarmour healthboost]
_entaction_yellowarmour = [_entaction_cycle $arg1 health greenarmour]
_entaction_monster      = [entproperty 1 ( * $arg1  1 )]
_entaction_box          = [entproperty 1 ( * $arg1  1 )]
_entaction_barrel       = [entproperty 1 ( * $arg1  1 )]
_entaction_platform     = [entproperty 1 ( * $arg1  1 )]
_entaction_elevator     = [entproperty 1 ( * $arg1  1 )]

entdirection = [
    if (&& [enthavesel] [ = (havesel) 0 ] ) [
        if (>= (indexof $_EntitiesWithDirection (enttype)) 0) [
            if (> $arg1 0) [
                entproperty 0 $arg2
                if (> (entattr 0) 360) [ entproperty 0 -360 ]
            ][
                entproperty 0 (- 0 $arg2)
                if (< (entattr 0) 0) [ entproperty 0 360 ]
            ]
        ]
        result 1
    ][
        result 0
    ]
]


//#############################################################################################################################################
//  Commands:

//  Clear entities of given type:
clearents = [
    if $editing [
        entcancel
        entselect [ =enttype $arg1 ]
        echo "Deleted" (enthavesel) $arg1 "entities."
        delent
    ]
]

//  Replace all entities that match current selection with the values given:
replaceents = [
    if $editing [
        do [
            entfind @(entget)
            entset @(loopconcat i $numargs [result $[arg@(+ $i 1)]])
        ]
        echo "Replaced" (enthavesel) "entities."
    ]
]

//  Edit selected entity(s) via console:
selentedit = [ saycommand ( concatword "/entset " (entget) ) ]

//  Replace selected entity(s) via console:
selreplaceents = [ saycommand ( concatword "/replaceents " (entget) ) ]

//  Modify given attribute of selected entity(s) by a given amount.
entproperty = [
    // ARGS 1: AttributeIndex
    //      2: Delta
    entattr $arg1 (+ (entattr $arg1) $arg2)
]


//#############################################################################################################################################
//  Selection Commands:

//  Select entities with given properties:  ('*' is wildcard)
entfind = [
    if (= $numargs 0) [
        entselect 1
    ] [
        entselect (concat [ && [=enttype @@arg1] ] (loopconcat i (- $numargs 1) [
            result [ [=entattr @@i @@[arg@(+ $i 2)]] ]
        ]))
    ]
]
entfindinsel = [
    if (= $numargs 0) [
        entselect [insel]
    ][
        entselect (
            concat [&& [insel] [=enttype @@arg1] ] (
                loopconcat i (- $numargs 1) [result [[=entattr @@i @@[arg@(+ $i 2)]]] ]
            )
        )
    ]
]

//  Select all entities matching current selection, type & attributes:
entfindsame = [ if (enthavesel) [do [entfind @(entget)]] ]
selentfindall = $entfindsame // Legacy Alias.

//  Select all entities matching current selection, type only:
entfindsametype = [ if (enthavesel) [do [entfind @(enttype)]] ]

//  Select all pickup-type entities:
entfindpickups = [
    entfind shells
    entfind bullets
    entfind rockets
    entfind riflerounds
    entfind grenades
    entfind cartridges

    entfind greenarmour
    entfind yellowarmour

    entfind health
    entfind healthboost

    entfind quaddamage
]

//  Change selected entity type via index-offset: (index of "_EntityTypes" list)
//  Could be used for scrolling through entity types.
enttypeselect = [
    // ARGS 1: IndexOffset

    _NumOfEntTypes = (listlen $_EntityTypes)

    //  Get index from offset and wrap it:
    _NewEntTypeIndex = (mod (+ (indexof $_EntityTypes (enttype)) $arg1) $_NumOfEntTypes)
    if (< $_NewEntTypeIndex 0) [ _NewEntTypeIndex = (+ $_NewEntTypeIndex $_NumOfEntTypes) ]

    do [entset @(listsplice (entget) (at $_EntityTypes $_NewEntTypeIndex) 0 1)]
]

lse = [
    // ARGS 1: Number of Entities per line.
    //  This won't be formated correctly without a fixed width font.
    echo "^f~** Listing Selected Entities"
    if (> $arg1 0) [_EntsPerLine = $arg1] [_EntsPerLine = 4]
    _Line  = ""
    _Count = 0
    entloop [
        _Line = (
            concatword $_Line (
                entget
            )(
                concatword (loopconcatword n (- 32 (mod (strlen $entget) 32) )[result " "])
            )
        )
        _Count = (+ $_Count 1)
        if (>= $_Count $_EntsPerLine) [ //  Echo string once n entities have been added.
            echo (concatword "^f~* " $_Line)
            _Line  = ""
            _Count = 0
        ]
    ]
    if (> $_Count 0) [ echo (concatword "^f~* " $_Line) ] //  Echo last line if it wasn't echoed in loop.
    echo (concatword "^f~** " (enthavesel) " Entities Selected")
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  Editing:

//#############################################################################################################################################
//  Utility Stuff:

drag       = [ dragging 1; onrelease [ dragging 0 ] ]
corners    = [ selectcorners 1; dragging 1; onrelease [ selectcorners 0; dragging 0 ] ]

editmove   = [ moving 1; onrelease [ moving 0 ]; result $moving ]
entdrag    = [ entmoving 1; onrelease [entmoving 0]; result $entmoving ]
editdrag   = [ cancelsel; || [entdrag] [ drag ] ]

selcorners = [ if $hmapedit [ hmapselect ] [ cancelsel; || [entdrag] [corners] ] ]

editextend = [ || [entdrag] [ selextend; reorient; editmove ] ]

editmovewith = [
    if (havesel) [
        || [editmove] [ @arg1 ]
        onrelease [ moving 0; dragging 0 ]
    ] [
        @arg1
    ]
]


//#############################################################################################################################################
//  Commands:

editdel = [ if (! (enthavesel)) [delcube]; delent ]

editflip = [ flip; entflip ]

editmovecorner = [ editmovewith selcorners ]
editmovedrag   = [ editmovewith editdrag   ]

editpush_ents = 1
editpush = [
    // ARGS 1: Delta
    //      2: Mode:  0 = [MouseWheel+F]  Push|Pull Cube-Face   (All 4 corners)
    //                1 = [MouseWheel  ] Empty|Fill Cubes       (Unless corners are explicitly selected, then Push|Pull selected corners.)
    //                2 = [MouseWheel+Q]  Push|Pull Cube-Corner (Corner pointed at. This is recursive if multiple cubes are selected.)

    if (|| [havesel] [! (enthavesel)] ) [
        // Cubes:
        if $moving [        // "Moving" selection. [MouseRight by default]
            pushsel $arg1
        ][
            entcancel
            editface $arg1 $arg2
        ]
    ][
        // Entities:
        if (editpush_ents) [
            if $entmoving [ entpush $arg1 ] [ _entaction_@(enttype) $arg1 ]
        ]
    ]
]
editfacewentpush = $editpush // Legacy Alias.

editrotate = [
    if $blendpaintmode [
        rotateblendbrush $arg1
    ][
        || [ entdirection $arg1 15 ] [ rotate $arg1 entrotate $arg1 ] //<-- OR short-circuit exploit.
    ]
]

//#############################################################################################################################################
//  Copy & Paste:
//      Three types of copying and pasting:
//          Select+Copy Cubes         -->  Paste Cubes
//          Select+Copy Ents          -->  Paste Ents               If Ent(s) are selected, paste will replace EntAttrs of selection.
//          Select+Copy Cubes & Ents  -->  Paste Cubes & Ents       With same relative positions.

opaquepaste = 1
_entcopybuf = ""

editcopy = [
    if (|| [havesel] [! (enthavesel)]) [
        _entcopybuf = ""
        entcopy
        copy
    ][
        _entcopybuf = (entget)
    ]
]
editcut = [
    if (moving 1) [
        if (= $moving 1) [selsave]
        onrelease [
            moving 0
            if (selmoved) [
                selswap
                copy; entcopy
                delcube; delent
                selrestore
                paste; entpaste
            ]
        ]
    ]
]

entreplace = [
    do [
        if (enthavesel) [] [ newent @_entcopybuf ]
        entset @_entcopybuf
    ]
]
editpaste = [
    _cancelpaste = (! (|| [enthavesel] [havesel]));
    if (strcmp "" $_entcopybuf) [
        pastehilite
        reorient // temp; real fix will be in octaedit      temp you say?
        onrelease [
            if $opaquepaste delcube
            paste
            entpaste
            if $_cancelpaste [ cancelsel ]
        ]
    ] [
        entreplace
        if $_cancelpaste [ cancelsel ]
    ]
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  Material Volumes:

listcomplete editmat "air alpha clip death gameclip glass lava noclip water"

//  Set material shortcuts:
air      = [ editmat air      $arg1 ]
alpha    = [ editmat alpha    $arg1 ]
clip     = [ editmat clip     $arg1 ]
death    = [ editmat death    $arg1 ]
gameclip = [ editmat gameclip $arg1 ]
noclip   = [ editmat noclip   $arg1 ]
loop i 4 [
    [water@(? $i (+ $i 1))] = [ editmat water@(? $i (+ $i 1)) $arg1 ]
    [lava@(? $i (+ $i 1))]  = [ editmat lava@(? $i (+ $i 1)) $arg1 ]
    [glass@(? $i (+ $i 1))] = [ editmat glass@(? $i (+ $i 1)) $arg1 ]
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  Convenience Aliases:

//  These will also show texture params set in map.cfg via: [ texalpha, texcolor, texlayer, texoffset, texrotate, texscale, texscroll ].
echovalpha = [echo (format "^f~** valpha: ^f5%1"                 (getvalpha  $getseltex) )]
echovcolor = [echo (format "^f~** vcolor: ^f3%1 ^f0%2 ^f1%3" (at (getvcolor  $getseltex) 0) (at (getvcolor $getseltex) 1) (at (getvcolor $getseltex) 2) )]
echovlayer = [
    if (!= (getvlayer $getseltex) 0) [
        echo (format "^f~** vlayer: ^f~[^f2%1^f~] ^f2^"%2^"" (getvlayer $getseltex) (gettexname (getvlayer $getseltex)) )
    ][
        echo "^f~** vlayer: ^f~[^f2~^f~] ^f2^"none^""
    ]
]
echovoffset = [echo (format "^f~** voffset: ^f3%1 ^f0%2"      (at (getvoffset $getseltex) 0) (at (getvoffset $getseltex) 1) )]
echovrotate = [echo (format "^f~** vrotate: ^f2%1 ^f7^"%2^""      (getvrotate $getseltex) (at $_Labels_TexRotate (getvrotate $getseltex)) )]
echovscale  = [echo (format "^f~** vscale: ^f2%1"                 (getvscale  $getseltex) )]
echovscroll = [echo (format "^f~** vscroll: ^f3%1 ^f0%2"      (at (getvscroll $getseltex) 0) (at (getvscroll $getseltex) 1) )]
echovshaderparam = [ //  This only works with vshaderparam, not with shaderparams set in map.cfg.
    echo (format "^f~***vshaderparam: ^f2^"%1^" %2" (getvshaderparamnames $getseltex) (getvshaderparam $getseltex (getvshaderparamnames $getseltex)) )
]

getsundir = [
    sunlightpitch $getcampitch
    sunlightyaw   $getcamyaw
]

gettexplease = [
    gettex
    // Since you asked so nicely:
    if $getseltex [
        echo ( concatword "^f0^t** GotTex: [" $getseltex "] ^"" (gettexname $getseltex) "^"")
    ] [
        echo ( concatword "^f3^t** GotTex: [~~] ^"~~~~~^"")
    ]
]

//  Set default skybox with lighting params:
lettherebelight = [
    skybox "skyboxes/remus/sky01"

    ambient 25 21 23

    skylight 148 153 163
    blurskylight 2

    sunlight 255 252 250
    sunlightscale 1.3
    sunlightpitch 43
    sunlightyaw 53
]

minimaphere = [minimapheight (at (getcampos) 2)]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
exec "data/edithud.cfg"

