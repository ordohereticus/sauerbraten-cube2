///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
nodebug [
    defsvar SM_ServDesc   ""
    defsvar SM_ServAddr   ""

    defvar  SM_NumOfClnt  0 0 256
    defvar  SM_NumOfSpec  0 0 256
    defvar  SM_NumOfPlyr  0 0 256
    defvar  SM_NumOfBots  0 0 256
    defvar  SM_MasterMode 0 0   3

    defvar  SM_ClntIsMstr 0 0 1
    defvar  SM_ClntIsSpec 0 0 1

    defvarp SM__ShowSelf    0 1 1
    defvarp SM__ShowBots    0 0 1
    defvarp SM__ConfirmKick 0 1 1

    defvar  SM_KickMe -1 -1 256
]

TODO 0 "menus.cfg -- Master Menu..."

newgui "master" [
    guikeeptab 1
    guistayopen [
    guinoautotab [
        guilist [
            guistrut (max $SM_NumOfSpec (max $SM_NumOfPlyr $SM_NumOfBots)) 1
            guilist [
                //if (!=s $SM_ListClnt (listclients 0 0)) [SM_Update]    Also need to detect Spec/Master/Team changes.
                SM_Update
                guialign 0 [guititle $SM_ServDesc  ]
                guialign 0 [guitext  $SM_ServAddr 0]
                guibar //-------------------------------------------------------------------------
                guialign 0 [//H
                    SM_MasterMode $getmastermode
                    guitext (concatword "^f7MasterMode: ^f2" $getmastermode) 0
                    guistrut 3
                    guiradio "Open  "   SM_MasterMode 0 [mastermode 0]
                    guiradio "Veto  "   SM_MasterMode 1 [mastermode 1]
                    guiradio "Locked  " SM_MasterMode 2 [mastermode 2]
                    guiradio "Private"  SM_MasterMode 3 [mastermode 3]
                ]
                guistrut 1
                guialign 0 [//H
                    guilist [//V
                        guistrut 18 1
                        guialign 0 [guicheckbox "Show Self" SM__ShowSelf]
                        guistrut 1
                        if (ismaster $getclientnum) [
                            guialign 0 [guibutton "^f0Relinquish Master" [setmaster 0]]
                        ][
                            guialign 0 [guibutton "^f0Claim Master" [setmaster 1]]
                        ]
                        guibar //-------------------------------------------------------------------------
                        guititle "Master"
                        looplist iClnt $SM_ListMstr [
                            guialign 0 [guitext (concatword "^f0" (getclientname $iClnt) ) 0]
                        ]
                    ]
                    guilist [//V
                        guistrut 18 1
                        guialign 0 [guicheckbox "Show Bots" SM__ShowBots]
                        guistrut 2
                        guibar //-------------------------------------------------------------------------
                        guititle "Auth"
                        looplist iClnt $SM_ListAuth [
                            guialign 0 [guitext (concatword "^f5" (getclientname $iClnt) ) 0]
                        ]
                    ]
                    guilist [//V
                        guistrut 18 1
                        guialign 0 [guicheckbox "Confirm Kick" SM__ConfirmKick]
                        guistrut 1
                        guialign 0 [guibutton "Clear Bans" clearbans]
                        guibar //-------------------------------------------------------------------------
                        guititle "Admin"
                        looplist iClnt $SM_ListAdmn [
                            guialign 0 [guitext (concatword "^f6" (getclientname $iClnt) ) 0]
                        ]
                    ]
                ]
            ]
        ]


    guitab (concatword "Players [" $SM_NumOfPlyr "]")
        if (= $guitabnum 2) [SM_GenList $SM_ListPlyr]

    guitab (concatword "Spectators [" $SM_NumOfSpec "]")
        if (= $guitabnum 3) [SM_GenList $SM_ListSpec]

    if $SM__ShowBots [guitab (concatword "Bots [" $SM_NumOfBots "]")
        if (= $guitabnum 4) [SM_GenList $SM_ListBots]
    ]

    ]]
] "Server Master" [SM_Update]


//#############################################################################################################################################
SM_Update = [
    //echo (concatword "^f" (mod $getmillis 9) "LISTS UPDATED")

    if $getservdesc [
        SM_ServDesc = (concatword "^f7" $getservdesc)
        SM_ServAddr = (concatword $connectedip ":" $connectedport)
    ][
        if $connectedip [
        SM_ServDesc = "^f4~"
        SM_ServAddr = (concatword $connectedip ":" $connectedport)
        ][
            SM_ServDesc = "^f4Sauerbraten"
            SM_ServAddr = "^f4~"
        ]
    ]

    SM_ListClnt  = (listclients 1 0) // listclients(local, bots)
    SM_NumOfClnt = (listlen $SM_ListClnt)

    SM_ListAdmn = (listfilter i $SM_ListClnt [isadmin $i])
    SM_ListAuth = (listfilter i $SM_ListClnt [&& (isauth $i)  (! (isadmin $i))])
    SM_ListMstr = (listfilter i $SM_ListClnt [&& (ismaster $i) (! (isauth $i)) (! (isadmin $i))])

    SM_ListClnt = (listclients $SM__ShowSelf $SM__ShowBots)

    SM_ListSpec = (listfilter i $SM_ListClnt [isspectator $i])
    SM_ListPlyr = (listfilter i $SM_ListClnt [&& (! (isspectator $i)) (! (isai $i))])
    if $SM__ShowBots [
        SM_ListBots = (listfilter i $SM_ListClnt [isai $i])
    ][
        SM_ListBots = ""
    ]

    SM_NumOfSpec = (listlen $SM_ListSpec)
    SM_NumOfPlyr = (listlen $SM_ListPlyr)
    SM_NumOfBots = (listlen $SM_ListBots)
]


//#############################################################################################################################################
SM_GenList = [
    // ARGS  1: [LIST]  List of client numbers.

    guilist [// H
        //  CLIENT NUMBER:
        guilist [// V
            looplist iClnt $arg1 [
                guialign 1 [ guitext (concatword "[" $iClnt "]") 0 ]
            ]
        ]
        //  NAME:
        guistrut 1
        guilist [// V
            looplist iClnt $arg1 [
                guitext (concatword (? (isadmin $iClnt) "^f6" (? (isauth $iClnt) "^f5" (? (ismaster $iClnt) "^f0"))) (getclientname $iClnt)) (getclienticon $iClnt)
            ]
        ]
        if (ismaster $getclientnum) [
            guispring
            //  BTN -- SWITCH TEAM:
            if (m_teammode $getmode) [
                guistrut 2
                guilist [// V
                    guistrut 9.09 1
                    looplist iClnt $arg1 [
                        if (isspectator $iClnt) [
                            guialign 0 [guitext "^f4~" 0]
                        ][
                            guibutton (concatword (? (=s (getclientteam $getclientnum)(getclientteam $iClnt)) "^f1" "^f3") "SwitchTeam") [setteam @iClnt @(? (=s (getclientteam $iClnt) "good") "evil" "good")] 0
                        ]
                    ]
                ]
            ]
            guistrut 2
            //  BTN -- SPECTATOR:
            guilist [// V
                guistrut 5.9 1
                if (isai (at $arg1 0)) [
                    loop i $SM_NumOfBots [ guialign 0 [guitext "^f4~" 0] ]
                ][
                    looplist iClnt $arg1 [
                        SM_ClntIsSpec (isspectator $iClnt)
                        guicheckbox (concatword (? $SM_ClntIsSpec "^f8" "^f4") "Spec") SM_ClntIsSpec 1 0 [spectator @(= (isspectator $iClnt) 0) @iClnt]
                    ]
                ]
            ]
            guistrut 2
            //  BTN -- MASTER/AUTH/ADMIN:
            guilist [// V
                guistrut 7.29 1
                if (isai (at $arg1 0)) [
                    loop i $SM_NumOfBots [ guialign 0 [guitext "^f4~" 0] ]
                ][
                    looplist iClnt $arg1 [
                        SM_ClntIsMstr (ismaster $iClnt)
                        guicheckbox (? (isadmin $iClnt) "^f6Admin" (? (isauth $iClnt) "^f5Auth" (? (ismaster $iClnt) "^f0Master" "^f4Master"))) SM_ClntIsMstr 1 0 [setmaster @(= (ismaster $iClnt) 0) @iClnt]
                    ]
                ]
            ]
            guistrut 2
            //  BTN -- KICK:
            guilist [// V
                guistrut 3.1 1
                if (isai (at $arg1 0)) [
                    loop i $SM_NumOfBots [ guialign 0 [guitext "^f4~" 0] ]
                ][
                    if $SM__ConfirmKick [
                        looplist iClnt $arg1 [ guibutton "^f2Kick" [SM_KickMe @iClnt; showgui "master_confirmkick"] 0 ]
                    ][
                        looplist iClnt $arg1 [ guibutton "^f2Kick" [kick @iClnt] 0 ]
                    ]
                ]
            ]
        ]
    ]
]


//#############################################################################################################################################
newgui "master_confirmkick" [
    guistrut 28 1
    guialign 0 [guitext "^f7Kick, are you sure?" 0 ]
    guialign 0 [guitext (concatword "^f6[" $SM_KickMe "] " (getclientname $SM_KickMe)) 0 ]
    guistrut 1
    guialign 0 [
        guibutton "^f2Kick" [kick $SM_KickMe; SM_KickMe -1; cleargui 1]
        guistrut 5
        guibutton "^f7Cancel" [cleargui 1]
    ]
] 0

