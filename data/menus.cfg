///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  Standard menu definitions:
//  (Don't modify, add personal menus to 'autoexec.cfg' instead.)
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
reui = [
    clearconsole
    exec "data/menus.cfg"
    echo "^f2*** Menus Reinitialized ***  ^f6This is for testing; it will break stuff; ^f3delete ^"config.cfg^" after use."
]

togglemainmenu = [ || (cleartexgui) [cleargui 1] [showgui "main"] ]
toggleentgui   = [if (|| (cleargui) (cleartexgui)) [] [showentgui]]
toggletexgui   = [if (|| (cleargui) (cleartexgui)) [] [showtexgui]]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
guilistsplit = [
    // ARGS 1: ...
    //      2: ...
    //      3: ...
    //      4: ...
    //      5: ...
    //      6: ...
    guilist [
        _iZ = (div (+ (listlen $arg3) (- $arg2 1)) $arg2)
        loop _iA $arg2 [
            guilist [
                if (>= $numargs 6) [arg6]
                loopsublist $arg1 $arg3 (* $_iA $_iZ) $_iZ [arg4]
            ]
            if (&& (>= $numargs 5) (< (+ $_iA 1) $arg2)) [arg5]
        ]
    ]
]

//#####################################################################################################################################
guifield2 = [
    // ARGS 1: "Label"
    //      2: "Label" Width
    //      3: Variable
    //      4: Variable-Field Width
    //      5: Variable-Decrement Step
    //      6: Variable-Increment Step
    guilist [ // H
        guilist [ // V
            guistrut @arg2 1
            guilist [guitext @arg1]
        ]
        guifield @arg3 @arg4
        guibutton "" [@arg3 (+fround $@arg3 @arg5)] "val_dec"
        guibutton "" [@arg3 (+fround $@arg3 @arg6)] "val_inc"
    ]
]

//#####################################################################################################################################
guifreerow = [ // "Free" in the sense that it is not a part of a table.
    // ARGS 1: ColumnWidth
    //      2: ColumnAlign          -2: NoAlign  -1: Left  0: Center  1: Right
    //      3: [ColumnContents]
    //      ** Repeat, up to 9 columns. **
    guilist [ // H              V                                                     H
        if (>= $numargs  3) [guilist [guistrut $arg1  1; if (= $arg2  -2) [@arg3 ][guialign $arg2  [@arg3 ]] ]] //  Column 1
        if (>= $numargs  6) [guilist [guistrut $arg4  1; if (= $arg5  -2) [@arg6 ][guialign $arg5  [@arg6 ]] ]] //  Column 2
        if (>= $numargs  9) [guilist [guistrut $arg7  1; if (= $arg8  -2) [@arg9 ][guialign $arg8  [@arg9 ]] ]] //  Column 3
        if (>= $numargs 12) [guilist [guistrut $arg10 1; if (= $arg11 -2) [@arg12][guialign $arg11 [@arg12]] ]] //  Column 4
        if (>= $numargs 15) [guilist [guistrut $arg13 1; if (= $arg14 -2) [@arg15][guialign $arg14 [@arg15]] ]] //  Column 5
        if (>= $numargs 18) [guilist [guistrut $arg16 1; if (= $arg17 -2) [@arg18][guialign $arg17 [@arg18]] ]] //  Column 6
        if (>= $numargs 21) [guilist [guistrut $arg19 1; if (= $arg20 -2) [@arg21][guialign $arg20 [@arg21]] ]] //  Column 7
        if (>= $numargs 24) [guilist [guistrut $arg22 1; if (= $arg23 -2) [@arg24][guialign $arg23 [@arg24]] ]] //  Column 8
        if (>= $numargs 27) [guilist [guistrut $arg25 1; if (= $arg26 -2) [@arg27][guialign $arg26 [@arg27]] ]] //  Column 9
    ]
]

//#####################################################################################################################################
guirollovername          = ""
guirolloveraction        = ""
//guirolloverimgpath       = ""   @@  Are these ever used?
//guirolloverimgaction     = ""   @@  Are these ever used?
guirolloverpreviewname   = ""
//guirolloverpreviewaction = ""   @@  Are these ever used?
checkrolloveraction = [
    if (>= (strstr $guirolloveraction $arg1) 0) [
        if (>= $numargs 2) [arg2] [result $guirolloveraction]
    ]
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
newgui "main" [
    guilist [
        guiimage (concatword "packages/icons/" (playermodelicon) ".jpg") [_chooseplayermodel] 1.15
        guistrut 0.25
        guilist [
            _MyName = (getname)
            guifield _MyName 15 [name $_MyName]
            guispring
            guilist [
                guibutton (playermodelname) [_chooseplayermodel] 0
                guistrut 1
                guiimage (getcrosshair) [showgui "crosshair"] 0.5
            ]
        ]
    ]
    guibar
    guibutton "server browser.."            [showgui "serverbrowser"]
    if (isconnected) [
        guibar
        if (|| $editing (m_edit (getmode))) [
            guibutton "editing.."           [showgui "editing"]
        ]
        guibutton "vote game mode / map.."  [showgui "gamemode"]
        // guibutton "lanconnect"
        // guibutton "connect localhost"
        guibutton "switch team"             [switchteam]
        guibutton "toggle spectator"        [togglespectator]           "spectator"
        guibutton "master.."                [showgui "master"]
        guibar
        guibutton "options.."               [showgui "options"]
        guibutton "disconnect"              [disconnect]                "exit"
    ] [
        guibutton "bot match.."             [showgui "botmatch"]
        guibutton "campaign.."              [showgui "campaign"]
        guibar
        guibutton "options.."               [showgui "options"]
        guibutton "about.."                 [showgui "about"]
        guibutton "quit"                    [quit]                      "exit"
    ]
] 0


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
_chooseplayermodel = [guirolloveraction = (_playermodelbutton $playermodel); showgui "playermodel"]

macro _playermodelbutton [cleargui 1; playermodel %1]

newgui "playermodel" [
    guilist [
        guilist [
            loop _iMdl $playermodelcount [
                guibutton (playermodelname $_iMdl) (_playermodelbutton $_iMdl) (playermodelicon $_iMdl)
            ]
        ]
        guibar
        _pmidx = (substr $guirolloveraction (strlen (_playermodelbutton "")))
        _pmidx = (max 0 (min (- $playermodelcount 1) $_pmidx))
        guilist [
            guilist [
                guispring
                guiplayerpreview $_pmidx 0 6 (checkrolloveraction "playermodel ") 4 1 (playermodelname $_pmidx)
                guispring
            ]
            guitextbox (playermodelstory $_pmidx) 40 9
        ]
    ]
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
newgui "serverbrowser" [
    guistayopen [
        guiservers [
            guilist [
                guibutton     "update from master server" [updatefrommaster]
                guibar
                guicheckbox   "search LAN"                [searchlan]
                guibar
                guicheckbox   "auto-update"               [autoupdateservers]
                guibar
                guicheckbox   "auto-sort"                 [autosortservers]
                if (= $autosortservers 0) [
                    guibar
                    guibutton "sort"                      [sortservers]
                ]
            ]
            guibar
        ] 17
    ]
] "servers" [initservers]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
newgui "master" [
    guistayopen [
        if (ismaster (getclientnum)) [
            guibutton "relinquish master" "setmaster 0"
        ][
            guibutton "claim master" "setmaster 1"
        ]
        guilist [
            guitext "mastermode: "
            _mm = (getmastermode)
            guiradio "open (0) "    _mm 0 "mastermode 0"
            guiradio "veto (1) "    _mm 1 "mastermode 1"
            guiradio "locked (2) "  _mm 2 "mastermode 2"
            guiradio "private (3) " _mm 3 "mastermode 3"
        ]
        guibar
        looplist _iClient (listclients 0 0) [
            guilist [
                guitext (concatword (getclientname $_iClient) ": ") (getclienticon $_iClient)
                guibutton "kick " [kick @_iClient]
                guibutton "toggle spectator " [spectator @(= (isspectator $_iClient) 0) @_iClient]
                if (m_teammode (getmode)) [
                    guibutton "switch team " [setteam @_iClient @(? (=s (getclientteam $_iClient) "good") "evil" "good")]
                ]
                if (ismaster $_iClient) [
                    guibutton "take master" [setmaster 0 @_iClient]
                ][
                    guibutton "give master" [setmaster 1 @_iClient]
                ]
            ]
        ]
    ]
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
newgui "about" [
    guititle "Cube 2: Sauerbraten"
    guialign 0 [guitext "^f4by" 0]
    guialign 0 [guitext "^f7Wouter ^"^f2Aardappel^f7^" van Oortmerssen" 0]
    guialign 0 [guitext "^f7Lee ^"^f2eihrul^f7^" Salzman" 0]
    guialign 0 [guitext "^f7Mike ^"^f2Gilt^f7^" Dysart" 0]
    guialign 0 [guitext "^f7Robert ^"^f2baby-rabbit^f7^" Pointon" 0]
    guialign 0 [guitext "^f7John ^"^f2geartrooper^f7^" Siar" 0]
    guialign 0 [guitext "^f7Quinton ^"^f2Quin^f7^" Reeves" 0]
    guialign 0 [guitext "^f7& others" 0]
    guistrut 0.5
    guialign 0 [guitext "^f7For a full list of contributors see the ^f2readme^f7." 0]
    guistrut 0.5
    guialign 0 [guitext "^f2http://sauerbraten.org/" 0]
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
_botmatchcount    =   5
_botmatchminskill =  50
_botmatchmaxskill = 100

_startbotmatch = [
    if (isconnected 0 0) [
        echo "You must disconnect from the current multiplayer game before starting a bot match."
    ] [
        loop i $_botmatchcount [addbot (rnd (+ $_botmatchmaxskill 1) $_botmatchminskill)]
    ]
]
newgui "botmatch" [
    guitext "bot amount"
    guislider _botmatchcount 0 32
    guitext "bot minimum skill"
    guislider _botmatchminskill 50 101 [if (< $_botmatchmaxskill $_botmatchminskill) [_botmatchmaxskill = $_botmatchminskill]]
    guitext "bot maximum skill"
    guislider _botmatchmaxskill 50 101 [if (> $_botmatchminskill $_botmatchmaxskill) [_botmatchminskill = $_botmatchmaxskill]]
    guibutton "start match.." [guionclear [_startbotmatch]; showgui "gamemode"]
] "bot match"


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
newgui "campaign" [
    guibutton   "start Private Stan Sauer"  [showgui "privatestansauer"]
    guibutton   "start An Army Of One"      [showgui "armyofone"]
    guibutton   "start Lost"                [sp lost]                       "cube"
    guibutton   "start Meltdown"            [sp skrsp1]                     "cube"
    guibutton   "start Missile Pass"        [sp crnsp1]                     "cube"
    guibutton   "start Level 9"             [sp level9]                     "cube"
    guibar
    guibutton   "start DMSP map.."          [mode -2; showgui "maps"]
    guicheckbox "slow motion"               [slowmosp]
    guitext     "skill (default: 3)"
    guislider skill
]

newgui "armyofone" [
    guilist [
        guilist [
            guibutton "Part I"   [sp mpsp6a] "cube"
            guibutton "Part II"  [sp mpsp6b] "cube"
            guibutton "Part III" [sp mpsp6c] "cube"
        ]
        _showmapshot (substr $guirolloveraction 3)
    ]
] "An Army Of One"

newgui "privatestansauer" [
    guilist [
        guilist [
            guibutton "Run N' Gun Part I"        [sp mpsp9a] "cube"
            guibutton "Run N' Gun Part II"       [sp mpsp9b] "cube"
            guibutton "Run N' Gun Part III"      [sp mpsp9c] "cube"
            guibutton "THE SERIOUSLY BIG VALLEY" [sp mpsp10] "cube"
        ]
        _showmapshot (substr $guirolloveraction 3)
    ]
] "Private Stan Sauer"

_showmapshot = [
    guibar
    _MapName = (checkrolloveraction "map " [if (> $numargs 0) [result $arg1] [at $guirollovername 0]])
    guilist [
        guiimage (concatword "packages/base/" $_MapName ".jpg") (checkrolloveraction "map ") 4 1 "data/cube.png" $_MapName
    ]
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
newgui "gamemode" [
    guilist [ // H
        guilist [ // V
            guialign 0 [ // H
                guibutton "coop-edit" [mode  1; showgui "maps"]
            ]
            guibar
            guilist [ // H
                guilist [ // V
                    guibutton "ffa"                [mode  0; showgui "maps"]
                    guibutton "ffa team"           [mode  2; showgui "maps"]
                    guistrut 1
                    guibutton "instagib"           [mode  3; showgui "maps"]
                    guibutton "instagib team"      [mode  4; showgui "maps"]
                    guistrut 1
                    guibutton "efficiency"         [mode  5; showgui "maps"]
                    guibutton "efficiency team"    [mode  6; showgui "maps"]
                    guistrut 1
                    guibutton "tactics"            [mode  7; showgui "maps"]
                    guibutton "tactics team"       [mode  8; showgui "maps"]
                ]
                guibar
                guilist [ // V
                    guibutton "capture"            [mode  9; showgui "capturemaps"]
                    guibutton "regen capture"      [mode 10; showgui "capturemaps"]
                    guistrut 1
                    guibutton "hold"               [mode 15; showgui "capturemaps"]
                    guibutton "insta hold"         [mode 16; showgui "capturemaps"]
                    guibutton "efficiency hold"    [mode 19; showgui "capturemaps"]
                ]
                guibar
                guilist [ // V
                    guibutton "ctf"                [mode 11; showgui "ctfmaps"]
                    guibutton "insta ctf"          [mode 12; showgui "ctfmaps"]
                    guibutton "efficiency ctf"     [mode 17; showgui "ctfmaps"]
                    guistrut 1
                    guibutton "protect"            [mode 13; showgui "ctfmaps"]
                    guibutton "insta protect"      [mode 14; showgui "ctfmaps"]
                    guibutton "efficiency protect" [mode 18; showgui "ctfmaps"]
                    guistrut 1
                    guibutton "collect"            [mode 20; showgui "ctfmaps"]
                    guibutton "insta collect"      [mode 21; showgui "ctfmaps"]
                    guibutton "efficiency collect" [mode 22; showgui "ctfmaps"]
                ]
            ]
        ]
    ]
] "select mode"


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
guimaplist = [
    //  This generates full-tabs with map-list and map-screenshot.
    // args 1: ListLabel     = "STRING"
    //      2: MapList       = [LIST]
    //      3: ColumnWidth   = INT
    //      4: MapsPerColumn = INT            //@@  Make all of these list-type GUIs use guiautotab???    Pretty much only the ServerBrowser uses it now.
    //      5: ColumnsPerTab = INT
    //      6: IsFirstTab    = 0|1

    _ML_NumOfMaps  = (- (listlen $arg2) 1) // List index starts at 0.
    _ML_NumOfCols  = (+ (ceil (divf (+ $_ML_NumOfMaps 1) $arg4)) 0)
    _ML_MapsPerTab = (* $arg4 $arg5)
    _ML_NumOfTabs  = (+ (ceil (divf $_ML_NumOfCols $arg5)) 0)

    loop _iTab $_ML_NumOfTabs [
        if (|| (!= $arg6 1) (> $_iTab 0)) [
            if (= $_iTab 0) [
                guitab $arg1
                _ML_CurrentTab = (+ $_ML_CurrentTab 1) //<-- This is yucky.
            ][
                guitab (+ $_iTab 1)
                _ML_CurrentTab = (+ $_ML_CurrentTab 1) //<-- This is yucky.
            ]
        ]
        _iMap = (* $_iTab $_ML_MapsPerTab)
        //=====================================================================================================
        // Is this the active Tab? If so, populate.
        if (= $_ML_CurrentTab (- $guitabnum 1)) [
            guilist [ // H
                guistrut $arg4 1
                _iCol = 1
                while [<= $_iCol $arg5] [
                    guilist [ // V
                        guistrut $arg3 1
                        _iEND = (+ (* $_iTab $_ML_MapsPerTab) (- (* $_iCol $arg4) 1))
                        while [<= $_iMap $_iEND] [
                            if (> $_iMap $_ML_NumOfMaps) [
                                _iMap = (+ $_iEND 1) // exitloop/break
                            ][
                                guibutton (at $arg2 $_iMap) (concat map (at $arg2 $_iMap)) "cube"
                                _iMap = (+ $_iMap 1)
                            ]
                        ]
                    ]
                    _iCol = (+ $_iCol 1)
                ]
                guibar
                guilist [ // V
                    _MapName = (checkrolloveraction "map " [if (> $numargs 0) [result $arg2] [at $guirollovername 0]])
                    guiimage (concatword "packages/base/" $_MapName ".jpg") (checkrolloveraction "map ") 4 0 "data/cube.png"
                    guistrut 0.25
                    guititle $_MapName
                    //guibar
                    //guitext (concatword "LABEL:         ^t"  $arg1            ) 0
                    //guitext (concatword "ColumnWidth:   ^t"  $arg3 "^t^f4arg3") 0
                    //guitext (concatword "MapsPerColumn: ^t"  $arg4 "^t^f4arg4") 0
                    //guitext (concatword "ColumnsPerTab: ^t"  $arg5 "^t^f4arg5") 0
                    //guitext (concatword "IsFirstTab:     ^t" $arg6 "^t^f4arg6") 0
                    //guitext (concatword "NumOfMaps:     ^t"  $_ML_NumOfMaps   ) 0
                    //guitext (concatword "NumOfCols:     ^t"  $_ML_NumOfCols   ) 0
                    //guitext (concatword "MapsPerTab:    ^t"  $_ML_MapsPerTab  ) 0
                    //guitext (concatword "NumOfTabs:     ^t"  $_ML_NumOfTabs   ) 0
                ]
            ]
        ]
        //=====================================================================================================
    ]
]

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
exec "data/maplists.cfg"

newgui "maps" [
    _ML_CurrentTab = 0 //<-- This is yucky.
    guimaplist "ffa"     $_MapsFFA     15 17 3 1
    guimaplist "capture" $_MapsCapture 15 17 3 0
    guimaplist "ctf"     $_MapsCTF     15 17 3 0
    guimaplist "concept" $_MapsConcept 15 17 3 0
] "maps" [_MapName = ""]

newgui "capturemaps" [
    _ML_CurrentTab = 0 //<-- This is yucky.
    guimaplist "capture" $_MapsCapture 15 17 3 1
] "capture maps" [_MapName = ""]

newgui "ctfmaps"     [
    _ML_CurrentTab = 0 //<-- This is yucky.
    guimaplist "ctf"     $_MapsCTF     15 17 3 1
] "ctf maps" [_MapName = ""]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//showcustommaps = [ if (&& (isconnected) (|| (cleargui) (cleartexgui))) [] [showgui "custommaps"] ]
showcustommaps = [showgui "custommaps"]
_MapsCustom = ""
_GenCustomMapList = [
    _MapsCustom = ""
    loopfiles _iFile "packages/base" "ogz" [
        if (< (indexof $_MapsALL $_iFile) 0) [
            _MapsCustom = (concat $_MapsCustom (escape $_iFile))
        ]
    ]
    _MapsCustom = (sortlist $_MapsCustom _iA _iB [naturalsort $_iA $_iB])
]

newgui "custommaps" [
    _ML_CurrentTab = 0 //<-- This is yucky.
    guimaplist "custom" $_MapsCustom 15 17 (min 3 (ceil (divf (listlen $_MapsCustom) 3))) 1
] "custom maps" [_MapName = ""; _GenCustomMapList]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Options:
_OPT_Width  = 66 // in units of (fontscale Width )
_OPT_Height = 16 // in units of (fontscale Height)
newgui "options" [
    guikeeptab 1
    // Save the frames guitabnum hack:
    guistrut $_OPT_Width 1                      //   H          strutV           V
                      if (= $guitabnum  1) [ guilist [guistrut $_OPT_Height 1; guilist [_OPT_TAB_Game   ]] ]
    guitab "console"; if (= $guitabnum  2) [ guilist [guistrut $_OPT_Height 1; guilist [_OPT_TAB_Console]] ]
    guitab "display"; if (= $guitabnum  3) [ guilist [guistrut $_OPT_Height 1; guilist [_OPT_TAB_Display]] ]
    guitab "gfx"    ; if (= $guitabnum  4) [ guilist [guistrut $_OPT_Height 1; guilist [_OPT_TAB_Gfx    ]] ]
    guitab "hud"    ; if (= $guitabnum  5) [ guilist [guistrut $_OPT_Height 1; guilist [_OPT_TAB_Hud    ]] ]
    guitab "input"  ; if (= $guitabnum  6) [ guilist [guistrut $_OPT_Height 1; guilist [_OPT_TAB_Input  ]] ]
    guitab "menus"  ; if (= $guitabnum  7) [ guilist [guistrut $_OPT_Height 1; guilist [_OPT_TAB_Menus  ]] ]
    guitab "res"    ; if (= $guitabnum  8) [ guilist [guistrut $_OPT_Height 1; guilist [_OPT_TAB_Res    ]] ]
    guitab "sound"  ; if (= $guitabnum  9) [ guilist [guistrut $_OPT_Height 1; guilist [_OPT_TAB_Sound  ]] ]
    guitab "cfg"    ; if (= $guitabnum 10) [ guilist [guistrut $_OPT_Height 1; guilist [_OPT_TAB_Cfg    ]] ]
] "game"

//#####################################################################################################################################
//#####################################################################################################################################
_OPT_TAB_Game = [
    guifreerow 18  1 [ guitext "hit indication: " 0
             ] 48 -2 [ guicheckbox "hit crosshair"  hitcrosshair 425 0
    ]
    guifreerow 18 -2 [
             ] 48 -2 [ guicheckbox "hit sound"  hitsound
    ]
    guibar

//@@       ScoreBoard hidefrags
//@@       ScoreBoard showdeaths
//@@       ScoreBoard showping       added "showping" mode 2 for showing pj compactly



    guifreerow 18 -2 [
             ] 48 -2 [ guicheckbox "show scoreboard upon death"  deathscore
    ]

//@@        jumpspawn
//@@        attackspawn



    guibar
    guifreerow 18  1 [ guitext "player models: " 0
             ] 12 -1 [ guicheckbox "fullbright" fullbrightmodels 60 0
             ]  9 -1 [ if (!= $fullbrightmodels 0) [ guiradio "subtle"   fullbrightmodels  60 ][ guiradio "^f4subtle"   fullbrightmodels  60 ]
             ]  9 -1 [ if (!= $fullbrightmodels 0) [ guiradio "bright"   fullbrightmodels 100 ][ guiradio "^f4bright"   fullbrightmodels 100 ]
             ]  9 -1 [ if (!= $fullbrightmodels 0) [ guiradio "intense"  fullbrightmodels 150 ][ guiradio "^f4intense"  fullbrightmodels 150 ]
             ]  9 -1 [ if (!= $fullbrightmodels 0) [ guiradio "max"      fullbrightmodels 200 ][ guiradio "^f4max"      fullbrightmodels 200 ]
    ]
    guifreerow 18 -2 [
             ] 48 -2 [ guicheckbox "always use team skins" teamskins
    ]
    guifreerow 18 -2 [
             ] 48 -2 [ guicheckbox "force matching player models" forceplayermodels
    ]
    guibar
    guifreerow 18  1 [ guitext "violence: " 0
             ] 48 -2 [ guicheckbox "blood"  blood
    ]
    guistrut 0.5
    guifreerow 18 -2 [
             ] 18 -2 [ guicheckbox "hide dead players"  hidedead
    ]

//@@  added "hidedead" mode 2 for hiding enemy deaths but not player's

    guistrut 0.5
    guifreerow 18 -2 [
             ] 18 -2 [ if (= $hidedead 1) [ guicheckbox "^f4ragdoll deaths" ragdoll ][ guicheckbox "ragdoll deaths" ragdoll ]
             ] 30 -2 [ if (|| (= $hidedead 1) (= $ragdoll 0)) [ guicheckbox "^f4keep after respawn" ragdollmillis 10000 0 ][ guicheckbox "keep after respawn" ragdollmillis 10000 0 ]
    ]
    guifreerow 18 -2 [
             ] 18  1 [ if (|| (= $hidedead 1) (= $ragdoll 0)) [ guitext "^f4velocity multiplier: " 0 ][ guitext "velocity multiplier: " 0 ]
             ] 30 -2 [ guislider deadpush
    ]
    guibar
    guifreerow 18 -2 [
             ] 48 -2 [ guibutton "^f6restore defaults (clears ALL settings)" [showgui "Restore_AreYouSure"]
    ]
]
newgui "Restore_AreYouSure" [
    guistrut 28 1
    guialign 0 [guitext "^f7Restore defaults, are you sure?" 0 ]
    guialign 0 [guitext "^f2(clears ^f6ALL ^f2settings)" 0 ]
    guistrut 1
    guialign 0 [
        guibutton "^f2Restore" [exec "restore.cfg"; cleargui] 0
        guistrut 5
        guibutton "^f7Cancel" [cleargui 1] 0
    ]
] 0

//#####################################################################################################################################
_OPT_TAB_Console = [
    guifreerow 18  1 [ guitext "text size: " 0
             ]  1 -2 [ guifield conscale 5
             ]  1 -2 [ guistayopen [guibutton "" [conscale (+fround $conscale -0.05)]  "val_dec"]
             ]  1 -2 [ guistayopen [guibutton "" [conscale (+fround $conscale  0.05)]  "val_inc"]
    ]
    guistrut 0.5
    guifreerow 18  1 [
             ] 24 -1 [ guicheckbox "chat console" miniconfilter 0x300 0
             ]  1 -1 [ guicheckbox "toggle full console ^f2[F11]" fullconsole
    ]
    guistrut 0.25
    guifreerow 18  1 [ guitext "console size: " 0
             ] 48 -2 [ guislider consize
    ]
    guifreerow 18  1 [ guitext "^f4(# of lines) " 0 ] 0.5
    guifreerow 18  1 [ guitext "console filter: " 0
             ] 16 -1 [ guibitfield "important info"   confilter 0x11
             ] 16 -1 [ guibitfield "chat"             confilter 0x100
             ] 16 -1 [ guibitfield "self frags"       confilter 0x800
    ]
    guifreerow 18 -1 [
             ] 16 -1 [ guibitfield "warnings"         confilter 0x02
             ] 16 -1 [ guibitfield "team chat"        confilter 0x200
             ] 16 -1 [ guibitfield "other frags"      confilter 0x1000
    ]
    guifreerow 18 -1 [
             ] 16 -1 [ guibitfield "errors"           confilter 0x04
             ] 16 -1 [ guibitfield "game events"      confilter 0x400
             ] 16 -1 [ guibitfield "team kills"       confilter 0x2000
    ]
    guifreerow 18 -1 [
             ] 16 -1 [ guibitfield "script messages"  confilter 0x20
    ]
    guistrut 0.25
    guifreerow 18  1 [ guitext "full console size: " 0
             ] 48 -2 [ guislider fullconsize
    ]
    guifreerow 18  1 [ guitext "^f4(% of screen) " 0 ] 0.5
    guifreerow 18  1 [ guitext "full console filter: " 0
             ] 16 -1 [ guibitfield "important info"   fullconfilter 0x11
             ] 16 -1 [ guibitfield "chat"             fullconfilter 0x100
             ] 16 -1 [ guibitfield "self frags"       fullconfilter 0x800
    ]
    guifreerow 18 -1 [
             ] 16 -1 [ guibitfield "warnings"         fullconfilter 0x02
             ] 16 -1 [ guibitfield "team chat"        fullconfilter 0x200
             ] 16 -1 [ guibitfield "other frags"      fullconfilter 0x1000
    ]
    guifreerow 18 -1 [
             ] 16 -1 [ guibitfield "errors"           fullconfilter 0x04
             ] 16 -1 [ guibitfield "game events"      fullconfilter 0x400
             ] 16 -1 [ guibitfield "team kills"       fullconfilter 0x2000
    ]
    guifreerow 18 -1 [
             ] 16 -1 [ guibitfield "script messages"  fullconfilter 0x20
    ]
]

//#####################################################################################################################################
//  Multiples of common monitor refresh rates:
//_TheHertz_60 = [  60 120 240 360 480 600 720 840 960 ]
//_TheHertz_75 = [  75 150 225 300 450 600 900 ]
//_TheHertz_90 = [  90 180 270 360 540 720 900 ]
//_TheHertz144 = [     144 288 432 576 720 864 ]
//_TheHertz165 = [     165 330 495 660 825 990 ]
_OPT_TAB_Display = [
    guifreerow 18  1 [ guitext "field of view: " 0
             ] 36 -2 [ guislider fov;
             ] 12  0 [ guitext "(default 100)" 0
    ]
    guibar
    guifreerow 18  1 [ guitext "maxfps: " 0
             ] 36 -2 [ guilistslider maxfps [60 75 90 120 144 165 240 360 480 600 720 840 960]
             ] 12  1 [ guifield maxfps 10
    ]
    guifreerow 18 -1 [
             ] 36 -1 [ guicheckbox "fullscreen"  fullscreen
    ]
    guifreerow 18 -1 [
             ] 36 -1 [ guicheckbox "v-sync"      vsync 1 0
    ]
    guistrut 0.25
    guifreerow 18  1 [ guitext "gamma: " 0
             ] 36 -2 [ guislider gamma
             ] 12  0 [ guitext "(default 100)" 0
    ]
    guifreerow 18  1 [ guitext "z-buffer depth: " 0
             ] 36 -2 [ guilistslider depthbits [0 16 24 32]
             ] 12  0 [ guitext "(default   0)" 0
    ]
    guibar
    guifreerow 18 -1 [
             ] 36 -1 [ guicheckbox "bilinear^t(texture smoothing)"  bilinear
    ]
    guifreerow 18 -1 [
             ] 36 -1 [ guicheckbox "bilinear^t(texture smoothing, map geo only)"  reducefilter
    ]
    guifreerow 18 -1 [
             ] 36 -1 [ guicheckbox "trilinear^t(mipmap-level blending)"  trilinear
    ]
    guistrut 0.25
    guifreerow 18  1 [ guitext "mipmap anisotropy: " 0
             ] 36 -2 [ guilistslider aniso [0 2 4 8 16]     //@@  guinameslider(  char *var,   char *names,   char *list,   char *onchange  )
             ] 12  0 [ guitext "(default   0)" 0
    ]
    guifreerow 18  1 [ guitext "anti-aliasing: " 0
             ] 36 -2 [ guilistslider fsaa [-1 0 2 4 8 16]   //@@  guinameslider(  char *var,   char *names,   char *list,   char *onchange  )
             ] 12  0 [ guitext "(default  -1)" 0
    ]
]

//#####################################################################################################################################
_GfxShowTexReduce = 0
_OPT_TAB_Gfx = [
    guifreerow 17.75  1 [ guitext "^f7*performance key*: " 0
             ] 48     0 [ guitext "^f0fast^f~, ^f2moderate^f~, ^f6moderate-ish^f~, ^f3slow & pretty^f~" 0
    ]
    guistrut 0.25
    guifreerow 17.75  1 [ guitext "shader detail: " 0
             ] 10    -2 [ guiradio "^f0low"    shaderdetail 0
             ] 10    -2 [ guiradio "^f2medium" shaderdetail 1
             ] 10    -2 [ guiradio "^f6high"   shaderdetail 2
             ]  1    -2 [ guiradio "^f3ultra"  shaderdetail 3
    ]
    guistrut 0.25
    _GfxPreset_Glare = 0
    if (= $glare 1) [
        if (= $glarescale 1) [
            if (= $blurglare 4) [_GfxPreset_Glare = 1]
            if (= $blurglare 7) [_GfxPreset_Glare = 3]
        ]
        if (= $glarescale 2) [
            if (= $blurglare 3) [_GfxPreset_Glare = 2]
            if (= $blurglare 7) [_GfxPreset_Glare = 4]
        ]
    ]
    guifreerow  17.75  1 [ guitext "glare: " 0
             ]  10    -2 [ guiradio "^f4off"     _GfxPreset_Glare 0 [glare 0]
             ]  10    -2 [ guiradio "^f2subtle"  _GfxPreset_Glare 1 [glare 1; blurglare 4; glarescale 1]
             ]  10    -2 [ guiradio "^f2glowy"   _GfxPreset_Glare 2 [glare 1; blurglare 3; glarescale 2]
             ]   8.6  -2 [ guiradio "^f3soft"    _GfxPreset_Glare 3 [glare 1; blurglare 7; glarescale 1]
             ]   1    -2 [ guiradio "^f3intense" _GfxPreset_Glare 4 [glare 1; blurglare 7; glarescale 2]
    ]

    _GfxPreset_MotionBlur = 0
    if     (=  $motionblur 0)         [_GfxPreset_MotionBlur = 0] [
        if (=f $motionblurscale 0.5 ) [_GfxPreset_MotionBlur = 1]
        if (=f $motionblurscale 0.65) [_GfxPreset_MotionBlur = 2]
        if (=f $motionblurscale 0.8 ) [_GfxPreset_MotionBlur = 3]
    ]
    guifreerow 17.75  1 [ guitext "motion blur: " 0
             ] 10    -2 [ guiradio "^f4off"      _GfxPreset_MotionBlur 0 [motionblur 0]
             ] 10    -2 [ guiradio "^f3subtle"   _GfxPreset_MotionBlur 1 [motionblur 1; motionblurscale 0.5 ]
             ] 10    -2 [ guiradio "^f3moderate" _GfxPreset_MotionBlur 2 [motionblur 1; motionblurscale 0.65]
             ]  1    -2 [ guiradio "^f3intense"  _GfxPreset_MotionBlur 3 [motionblur 1; motionblurscale 0.8 ]
    ]

    guifreerow 17.75  1 [ guitext "dynamic lights: " 0
             ] 10    -2 [ guiradio "^f4off"    maxdynlights 0
             ] 10    -2 [ guiradio "^f0max 3"  maxdynlights 3
             ] 10    -2 [ guiradio "^f2max 4"  maxdynlights 4
             ]  1    -2 [ guiradio "^f6max 5"  maxdynlights 5
    ]

    _GfxPreset_DynamicShadows = 0
    if (&& (= $blobs 1) (= $shadowmap 0)) [_GfxPreset_DynamicShadows = 1]
    if                  (= $shadowmap 1)  [_GfxPreset_DynamicShadows = 2]
    guifreerow 17.75  1 [ guitext "dynamic shadows: " 0
             ] 10    -2 [ guiradio "^f4off"         _GfxPreset_DynamicShadows 0 [blobs 0; shadowmap 0]
             ] 10    -2 [ guiradio "^f2blobs"       _GfxPreset_DynamicShadows 1 [blobs 1; shadowmap 0]
             ]  1    -2 [ guiradio "^f3shadowmaps"  _GfxPreset_DynamicShadows 2 [blobs 0; shadowmap 1]
    ]
    if (= $shadowmap 1) [
        _GfxPreset_ShadowmapQuality = 0
        if (= $blurshadowmap 0) [
            if (= $shadowmapsize  9) [_GfxPreset_ShadowmapQuality = 1]
        ][
            if (= $shadowmapsize  9) [_GfxPreset_ShadowmapQuality = 2]
            if (= $shadowmapsize 10) [_GfxPreset_ShadowmapQuality = 3]
            if (= $shadowmapsize 11) [_GfxPreset_ShadowmapQuality = 4]
        ]
        guifreerow 17.75  1 [ guitext "shadowmap quality: " 0
                 ] 10    -2 [ guiradio "^f2low"    _GfxPreset_ShadowmapQuality 1 [shadowmapsize  9; blurshadowmap 0]
                 ] 10    -2 [ guiradio "^f2medium" _GfxPreset_ShadowmapQuality 2 [shadowmapsize  9; blurshadowmap 1]
                 ] 10    -2 [ guiradio "^f6high"   _GfxPreset_ShadowmapQuality 3 [shadowmapsize 10; blurshadowmap 1]
                 ]  1    -2 [ guiradio "^f3ultra"  _GfxPreset_ShadowmapQuality 4 [shadowmapsize 11; blurshadowmap 1]
        ]
        guistrut 0.25
    ][
        guistrut 1.25
    ]
    _GfxPreset_SoftParticles = 0
    if         (= $depthfx 0)                          [_GfxPreset_SoftParticles = 0] [
        if (&& (= $depthfxsize  7) (= $blurdepthfx 1)) [_GfxPreset_SoftParticles = 1]
        if (&& (= $depthfxsize 10) (= $blurdepthfx 0)) [_GfxPreset_SoftParticles = 2]
        if (&& (= $depthfxsize 12) (= $blurdepthfx 0)) [_GfxPreset_SoftParticles = 3]
    ]
    guifreerow 17.75  1 [ guitext "soft particles: " 0
             ] 10    -2 [ guiradio "^f4off"    _GfxPreset_SoftParticles 0 [depthfx 0]
             ] 10    -2 [ guiradio "^f0low"    _GfxPreset_SoftParticles 1 [depthfx 1; depthfxsize  7; blurdepthfx 1]
             ] 10    -2 [ guiradio "^f2medium" _GfxPreset_SoftParticles 2 [depthfx 1; depthfxsize 10; blurdepthfx 0]
             ]  1    -2 [ guiradio "^f3high"   _GfxPreset_SoftParticles 3 [depthfx 1; depthfxsize 12; blurdepthfx 0]
    ]

    guifreerow 17.75  1 [ guitext "models: " 0
             ] 10    -2 [ guicheckbox "^f0reflectenv"  envmapmodels
             ]  1    -2 [ guicheckbox "^f2bumpmap"     bumpmodels
    ]
    guistrut 0.25
    if (= $_GfxShowTexReduce 0) [
        guifreerow  17.75  1 [ guistayopen [guibutton "max texture size: " [_GfxShowTexReduce = 1] 0 ]
                 ]  10    -2 [ guiradio "^f0256"        maxtexsize  256
                 ]  10    -2 [ guiradio "^f0512"        maxtexsize  512
                 ]  10    -2 [ guiradio "^f21024"       maxtexsize 1024
                 ]   8.6  -2 [ guiradio "^f62048"       maxtexsize 2048
                 ]   1    -2 [ guiradio "^f6unlimited"  maxtexsize    0
        ]
    ][
        guifreerow 17.75  1 [ guistayopen [guibutton "reduce texture size: " [_GfxShowTexReduce = 0]  0 ] // Secret texture option. :o
                 ] 48    -2 [ guinameslider texreduce ["0" "1/2" "1/4" "1/8" "1/16" "1/32" "1/64" "1/128" "1/256" "1/512" "1/1024" "1/2048" "1/4096"] [0 1 2 3 4 5 6 7 8 9 10 11 12] []
        ]
    ]

    _GfxPreset_Grass = 0
    if     (= $grass 0)       [_GfxPreset_Grass = 0] [
        if (= $grassdist 128) [_GfxPreset_Grass = 1]
        if (= $grassdist 256) [_GfxPreset_Grass = 2]
        if (= $grassdist 512) [_GfxPreset_Grass = 3]
    ]
    guifreerow 17.75  1 [ guitext "grass: " 0
             ] 10    -2 [ guiradio "^f4off"    _GfxPreset_Grass 0 [grass 0]
             ] 10    -2 [ guiradio "^f2low"    _GfxPreset_Grass 1 [grass 1; grassdist 128]
             ] 10    -2 [ guiradio "^f6medium" _GfxPreset_Grass 2 [grass 1; grassdist 256]
             ]  1    -2 [ guiradio "^f3high"   _GfxPreset_Grass 3 [grass 1; grassdist 512]
    ]
    _GfxPreset_Decals = 0
    if         (= $decals 0)                                [_GfxPreset_Decals = 0] [
        if (&& (= $decalfade 10000) (= $maxdecaltris 1024)) [_GfxPreset_Decals = 1]
        if (&& (= $decalfade 30000) (= $maxdecaltris 2048)) [_GfxPreset_Decals = 2]
        if (&& (= $decalfade 60000) (= $maxdecaltris 4096)) [_GfxPreset_Decals = 3]
    ]
    guifreerow 17.75  1 [ guitext "decals: " 0
             ] 10    -2 [ guiradio "^f4off"    _GfxPreset_Decals 0 [decals 0]
             ] 10    -2 [ guiradio "^f0low"    _GfxPreset_Decals 1 [decals 1; decalfade 10000; maxdecaltris 1024]
             ] 10    -2 [ guiradio "^f2medium" _GfxPreset_Decals 2 [decals 1; decalfade 30000; maxdecaltris 2048]
             ]  1    -2 [ guiradio "^f6high"   _GfxPreset_Decals 3 [decals 1; decalfade 60000; maxdecaltris 4096]
    ]
    guifreerow 17.75  1 [ guitext "glass: " 0
             ] 10    -1 [ guicheckbox "^f0reflectenv"  glassenv
    ]
    guifreerow  17.75  1 [ guitext "water: " 0
             ]  10    -2 [ guicheckbox "^f0reflectenv"  waterenvmap
             ]  10    -2 [ guicheckbox "^f3reflection"  waterreflect
             ]  10    -2 [ guicheckbox "^f2refraction"  waterrefract
             ]   8.6  -2 [ guicheckbox "^f0caustics"    caustics
             ]   1    -2 [ guicheckbox "^f2animation"   vertwater
    ]
    guifreerow 17.75  1 [ guitext "waterfalls: " 0
             ] 10    -2 [ guicheckbox "^f0reflectenv"  waterfallenv
             ]  1    -2 [ guicheckbox "^f2refraction"  waterfallrefract
    ]
]

//#####################################################################################################################################
_OPT_TAB_Hud = [

//@@    healthcolors

//@@    show status icons over players ("statusicons")
//@@    show health status bars for teammates ("statusbars")


    guifreerow 18  1 [ guitext "show/hide: " 0
             ] 12 -2 [ guicheckbox "ammobar"  ammobar
             ] 12 -2 [ if (= $ammobar 1) [guibutton "adjust ammobar^t^f2/adjustammobar"      adjustammobar    "menu"]
    ]
    guifreerow 18 -2 [
             ] 12 -2 [ guicheckbox "gameclock"  gameclock
             ] 12 -2 [ if (= $gameclock 1) [guibutton "adjust gameclock^t^f2/adjustgameclock"  adjustgameclock  "menu"]
    ]
    guifreerow 18 -2 [
             ] 12 -2 [ guicheckbox "hudscore"  hudscore
             ] 12 -2 [ if (= $hudscore 1) [guibutton "adjust hudscore^t^f2/adjusthudscore"    adjusthudscore   "menu"]
    ]
    guifreerow 18    -2 [
             ] 12    -2 [ guicheckbox "wallclock"  wallclock
             ] 11.25 -2 [ if $wallclock [ guicheckbox "24 hour"  wallclock24   ]
             ] 12    -2 [ if $wallclock [ guicheckbox "seconds"  wallclocksecs ]
    ]
    guifreerow 18 -2 [
             ] 12 -2 [ guicheckbox "FPS"  showfps
    ]
    guifreerow 18    -2 [
             ] 12    -2 [ guicheckbox "hudgun"  hudgun
             ] 11.25 -2 [ if $hudgun [ guicheckbox "sway"          hudgunsway  ]
             ] 12.25 -2 [ if $hudgun [ guicheckbox "muzzle flash"  muzzleflash ]
             ] 12    -2 [ if $hudgun [ guicheckbox "muzzle light"  muzzlelight ]
    ]
    guibar
    guifreerow 18  1 [ guitext "crosshair: " 0
             ]  3 -2 [ guiimage (getcrosshair) [showgui "crosshair"] 0.5
             ]  1 -2 [ guibutton "change" [showgui "crosshair"] 0
    ]
    guifreerow 18  1 [
             ] 12 -2 [ guitext "size:"
             ] 36 -2 [ guislider crosshairsize
    ]
    guifreerow 18    -2 [
             ] 12    -2 [ guicheckbox "effects"  crosshairfx
             ] 11.25 -2 [ if $crosshairfx [ guicheckbox "hits"           hitcrosshair 425 ]
             ] 12.25 -2 [ if $crosshairfx [ guicheckbox "teammates"      teamcrosshair    ]
             ]  1    -2 [ if $crosshairfx [ guicheckbox "health colors"  crosshaircolors  ]
    ]
    guibar
    guifreerow 18    1 [ guitext "damage indication: " 0
             ] 23.5 -2 [ guicheckbox "compass"        damagecompass
             ]  1   -2 [ guicheckbox "screen effect"  damagescreen
    ]
    guibar
    guifreerow 18    1 [ guitext "radar: " 0
             ] 23.5 -2 [ guicheckbox "numbered capture bases"  basenumbers
             ]  1   -2 [ guicheckbox "show teammates"          radarteammates 1
    ]
    guibar
    guifreerow 18    1 [ guitext "capture bases: " 0
             ] 23.5 -2 [ guicheckbox "outline meters"  outlinemeters
             ]  1   -2 [ guicheckbox "tether effect"     capturetether
    ]
]

//#####################################################################################################################################
_bindactions = [
    "forward" "backward" "left" "right" "jump" "attack" "togglezoom"
    "saycommand" "sayteamcommand" "showscores" "toggleconsole" "screenshot" "edittoggle"
    "setweapon FI" "setweapon SG" "setweapon CG" "setweapon RL" "setweapon RI" "setweapon GL" "setweapon PI" "weapon"
    "universaldelta 1" "universaldelta -1" "dropflag" "addbot" "delbot"
]
_OPT_TAB_Input = [
    guistayopen [
        _MyMouseSens = $sensitivity
        guifreerow 19  1 [ guitext "mouse-aim sensitivity: " 0
                 ]  1  0 [ guifield _MyMouseSens 5 [sensitivity $_MyMouseSens]
                 ]  1 -1 [ guibutton "" [sensitivity (+fround $sensitivity -0.1)]  "val_dec"
                 ]  7 -1 [ guibutton "" [sensitivity (+fround $sensitivity  0.1)]  "val_inc"
                 ]  1 -1 [ guicheckbox "mouse-aim invert Y-axis"  invmouse
        ]
    ]
    guistrut 0.6
    guititle "select action to bind and press desired keys (ESC when done)"
    guistrut 0.4
    _iBind = 0
    guilistsplit n 2 $_bindactions [
        guistrut 32 1
        guilist [
            guitext (tabify (concatword $n ": ") 4)
            [_newbinds@_iBind] = (searchbinds $n)
            guikeyfield [_newbinds@_iBind] -12 [
                _oldbinds = (searchbinds [@@n])
                looplist _iOld $_oldbinds           [bind $_iOld ""]
                looplist _iNew $[_newbinds@@_iBind] [bind $_iNew [@@@n]]
            ]
            _iBind = (+ $_iBind 1)
        ]
    ] [guibar]
]

//#####################################################################################################################################
_OPT_TAB_Menus = [
    guistayopen [
        guifreerow 18  1 [ guitext "general: " 0
                 ] 16 -2 [ guitext "autotab thresh:"
                 ] 32 -2 [ guislider guiautotab
        ]
        guifreerow 18 -2 [
                 ] 16 -2 [ guitext "menufps: "
                 ] 32 -2 [ guilistslider menufps [60 75 90 120 144 165 240 360 480 600]
        ]
        guifreerow 18 -2 [
                 ] 16 -2 [ guicheckbox "click tab"  guiclicktab
        ]
        guifreerow 18 -2 [
                 ] 16 -2 [ guicheckbox "transition zoom"  guifadein
        ]
        guibar
        guifreerow 18  1 [ guitext "cursor: " 0
                 ] 16 -2 [ guitext "size:"
                 ] 32 -2 [ guislider cursorsize 4 50
        ]
        _MyMouseSensGui = $guisens
        guifreerow 18 -2 [
                 ] 16 -2 [ guitext "sensitivity: "
                 ]  1 -2 [ guifield _MyMouseSensGui 5 [guisens $_MyMouseSensGui]
                 ]  1 -2 [ guibutton "" [guisens (+fround $guisens -0.1)]  "val_dec"
                 ]  1 -2 [ guibutton "" [guisens (+fround $guisens  0.1)]  "val_inc"
        ]
        guibar
        guifreerow 18  1 [ guitext "texture gallery: " 0
                 ] 16 -2 [ guicheckbox "show name"  texguiname
        ]
        guifreerow 18  1 [
                 ] 16 -2 [ guitext "width:"
                 ] 32 -2 [ guislider texguiwidth 4 64
        ]
        guifreerow 18  1 [
                 ] 16 -2 [ guitext "height:"
                 ] 32 -2 [ guislider texguiheight 4 64
        ]
        guifreerow 18  1 [
                 ] 16 -2 [ guitext "scale:"
                 ]  1 -2 [ guifield texguiscale 5
                 ]  1 -2 [ guibutton "" [texguiscale (+fround $texguiscale -0.1)]  "val_dec"
                 ]  1 -2 [ guibutton "" [texguiscale (+fround $texguiscale  0.1)]  "val_inc"
        ]
        guibar
        guifreerow 18  1 [ guitext "3D: " 0
                 ] 16 -2 [ guicheckbox "2D menus"  gui2d
                 ] 16 -2 [ if (= gui2d 1) [ guicheckbox "^f42D texture menu"  texgui2d ][ guicheckbox "2D texture menu"  texgui2d ]
        ]
    ]
]

//#####################################################################################################################################
macro guiresbutton [ guibutton "%1x%2" "screenres %1 %2" (if (&& (= $scr_w %1) (= $scr_h %2)) [result "radio_on"] [result "radio_off"]) ]
_OPT_TAB_Res = [
    guistayopen [
        guilist [
            guilist [guistrut 16] //  Force Height.
            guilist [
                guistrut 11.6 1
                guialign 0 [guitext "4:3" 0]
                @@@@(guiresbutton 320 240)
                @@@@(guiresbutton 640 480)
                @@@@(guiresbutton 800 600)
                @@@@(guiresbutton 1024 768)
                @@@@(guiresbutton 1152 864)
                @@@@(guiresbutton 1280 960)
                @@@@(guiresbutton 1400 1050)
                @@@@(guiresbutton 1600 1200)
                @@@@(guiresbutton 1792 1344)
                @@@@(guiresbutton 1856 1392)
                @@@@(guiresbutton 1920 1440)
                @@@@(guiresbutton 2048 1536)
                @@@@(guiresbutton 2800 2100)
                @@@@(guiresbutton 3200 2400)
            ]
            guibar
            guilist [
                guistrut 11.6 1
                guialign 0 [guitext "16:10" 0]
                @@@@(guiresbutton 320 200)
                @@@@(guiresbutton 640 400)
                @@@@(guiresbutton 1024 640)
                @@@@(guiresbutton 1280 800)
                @@@@(guiresbutton 1440 900)
                @@@@(guiresbutton 1600 1000)
                @@@@(guiresbutton 1680 1050)
                @@@@(guiresbutton 1920 1200)
                @@@@(guiresbutton 2048 1280)
                @@@@(guiresbutton 2560 1600)
                @@@@(guiresbutton 2880 1800)
                @@@@(guiresbutton 3200 2000)
                @@@@(guiresbutton 3840 2400)
            ]
            guibar
            guilist [
                guistrut 11.6 1
                guialign 0 [guitext "16:9" 0]
                @@@@(guiresbutton 1024 600)
                @@@@(guiresbutton 1280 720)
                @@@@(guiresbutton 1366 768)
                @@@@(guiresbutton 1600 900)
                @@@@(guiresbutton 1920 1080)
                @@@@(guiresbutton 2048 1152)
                @@@@(guiresbutton 2560 1440)
                @@@@(guiresbutton 2880 1620)
                @@@@(guiresbutton 3200 1800)
                @@@@(guiresbutton 3840 2160)
            ]
            guibar
            guilist [
                guistrut 11.6 1
                guialign 0 [guitext "21:9" 0]
                @@@@(guiresbutton 2560 1080)
                @@@@(guiresbutton 3440 1440)
                @@@@(guiresbutton 3840 1600)
                guistrut 1
                guialign 0 [guitext "32:9" 0]
                @@@@(guiresbutton 3840 1080)
                @@@@(guiresbutton 5120 1440)
                guistrut 1
                guialign 0 [guitext "1:1" 0]
                @@@@(guiresbutton  512  512)
                @@@@(guiresbutton  768  768)
                @@@@(guiresbutton 1024 1024)
                @@@@(guiresbutton 2048 2048)
            ]
            guibar
            guilist [
                guistrut 11.6 1
                guialign 0 [guitext "5:4" 0]
                @@@@(guiresbutton 600 480)
                @@@@(guiresbutton 1280 1024)
                @@@@(guiresbutton 1600 1280)
                @@@@(guiresbutton 2560 2048)
                guistrut 1
                guialign 0 [guitext "5:3" 0]
                @@@@(guiresbutton 800 480)
                @@@@(guiresbutton 1280 768)
                guistrut 1
                guibar
                guialign 0 [guitext "Custom" 0]
                guistrut 0.25
                guialign 0 [
                    _MyResX = $scr_w
                    _MyResY = $scr_h
                    guifield _MyResX 4 [scr_w $_MyResX]
                    guitext " " 0
                    guifield _MyResY 4 [scr_h $_MyResY]
                ]
                guistrut 1.5
                guialign 0 [guicheckbox "fullscreen"  fullscreen]
            ]
        ]
    ]
]

//#####################################################################################################################################
_OPT_TAB_Sound = [

//@@  added "minimizedsounds" var to control whether sounds play when window is minimized

    guifreerow 18  1 [ guitext "sound volume: " 0
             ] 48 -2 [ guislider soundvol
    ]
    guifreerow 18  1 [ guitext "music volume: " 0
             ] 48 -2 [ guislider musicvol
    ]
    guibar
    guifreerow 18  1 [ guitext "sound channels: " 0
             ] 48 -2 [ guislider soundchans
    ]
    guifreerow 18  1 [ guitext "sound frequency: " 0
             ] 48 -2 [ guilistslider soundfreq [11025 22050 44100 48000]
    ]
    guifreerow 18  1 [ guitext "sound buffer length: " 0
             ] 48 -2 [ guislider soundbufferlen
    ]
    guibar
    guifreerow 18 -2 [
             ] 48 -2 [ guicheckbox "mumble positional audio" mumble
    ]
]

//#####################################################################################################################################
_OPT_TAB_Cfg = [
    guitext "autoexec.cfg"
    guitexteditor "autoexec.cfg" -63 14
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
adjustammobar = [showgui "ammobar"]
newgui "ammobar" [
    guinoautotab [
        guistayopen [
            guicheckbox "AmmoBar" ammobar
            guibar
            guilist [
                guitext "AmmoBarAlign:^t^t^t"
                guiradio "-1 " ammobaralign -1
                guiradio "0 "  ammobaralign  0
                guiradio "1"   ammobaralign  1
            ]

            guicheckbox "AmmoBarHorizontal" ammobarhorizontal

            guicheckbox "AmmoBarFlip" ammobarflip

            guicheckbox "AmmoBarHideEmpty" ammobarhideempty

            guitext "AmmoBarSep:"
            guislider ammobarsep

            guitext "AmmoBarCountSep:"
            guislider ammobarcountsep

            guilist [
                guitext "AmmoBarCountScale:^t "
                guifield ammobarcountscale 5
                guibutton "" [ammobarcountscale (+fround $ammobarcountscale -0.1)]  "val_dec"
                guibutton "" [ammobarcountscale (+fround $ammobarcountscale  0.1)]  "val_inc"
            ]

            guilist [
                guitext "AmmoBarScale:^t^t^t "
                guifield ammobarscale 5
                guibutton "" [ammobarscale (+fround $ammobarscale -0.01)]           "val_dec"
                guibutton "" [ammobarscale (+fround $ammobarscale  0.01)]           "val_inc"
            ]

            guilist [
                guitext "AmmoBarX:^t^t^t "
                guifield ammobarx 5
                guibutton "" [ammobarx (+fround $ammobarx -0.01)]                   "val_dec"
                guibutton "" [ammobarx (+fround $ammobarx  0.01)]                   "val_inc"
            ]
            guilist [
                guitext "AmmoBarY:^t^t^t "
                guifield ammobary 5
                guibutton "" [ammobary (+fround $ammobary -0.01)]                   "val_dec"
                guibutton "" [ammobary (+fround $ammobary  0.01)]                   "val_inc"
            ]
        ]
    ]
] "AmmoBar"


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
adjustgameclock = [showgui "gameclock"]
newgui "gameclock" [
    guinoautotab [
        guistayopen [
            guicheckbox "GameClock" gameclock
            guibar
            guilist [
                guitext "GameClockAlign:^t"
                guiradio "-1 " gameclockalign -1
                guiradio "0 "  gameclockalign  0
                guiradio "1"   gameclockalign  1
            ]

            guitext "GameClockAlpha:"
            guislider gameclockalpha

            _GameClockColorR = (& (>> $gameclockcolour 16) 0xFF)
            _GameClockColorG = (& (>> $gameclockcolour  8) 0xFF)
            _GameClockColorB = (&     $gameclockcolour     0xFF)
            guilist [guitext "GameClockColour:^t^t"; guicolor $gameclockcolour]
            guislider _GameClockColorR 0 255 [gameclockcolour $_GameClockColorR $_GameClockColorG $_GameClockColorB]
            guislider _GameClockColorG 0 255 [gameclockcolour $_GameClockColorR $_GameClockColorG $_GameClockColorB]
            guislider _GameClockColorB 0 255 [gameclockcolour $_GameClockColorR $_GameClockColorG $_GameClockColorB]

            _GameClockLowColorR = (& (>> $gameclocklowcolour 16) 0xFF)
            _GameClockLowColorG = (& (>> $gameclocklowcolour  8) 0xFF)
            _GameClockLowColorB = (&     $gameclocklowcolour     0xFF)
            guilist [guitext "GameClockLowColour:^t"; guicolor $gameclocklowcolour]
            guislider _GameClockLowColorR 0 255 [gameclocklowcolour $_GameClockLowColorR $_GameClockLowColorG $_GameClockLowColorB]
            guislider _GameClockLowColorG 0 255 [gameclocklowcolour $_GameClockLowColorR $_GameClockLowColorG $_GameClockLowColorB]
            guislider _GameClockLowColorB 0 255 [gameclocklowcolour $_GameClockLowColorR $_GameClockLowColorG $_GameClockLowColorB]

            guilist [
                guitext "GameClockScale:^t "
                guifield gameclockscale 5
                guibutton "" [gameclockscale (+fround $gameclockscale -0.01)]   "val_dec"
                guibutton "" [gameclockscale (+fround $gameclockscale  0.01)]   "val_inc"
            ]

            guilist [
                guitext "GameClockX:^t^t "
                guifield gameclockx 5
                guibutton "" [gameclockx (+fround $gameclockx -0.01)]           "val_dec"
                guibutton "" [gameclockx (+fround $gameclockx  0.01)]           "val_inc"
            ]
            guilist [
                guitext "GameClockY:^t^t "
                guifield gameclocky 5
                guibutton "" [gameclocky (+fround $gameclocky -0.01)]           "val_dec"
                guibutton "" [gameclocky (+fround $gameclocky  0.01)]           "val_inc"
            ]
        ]
    ]
] "GameClock"


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
adjusthudscore = [showgui "hudscore"]
newgui "hudscore" [
    guinoautotab [
        guistayopen [
            guicheckbox "HudScore" hudscore
            guibar
            guilist [
                guitext "HudScoreAlign:^t^t"
                guiradio "-1 " hudscorealign -1
                guiradio "0 "  hudscorealign  0
                guiradio "1"   hudscorealign  1
            ]

            guitext "HudScoreAlpha:"
            guislider hudscorealpha

            _HudScoreEnemyColorR  = (& (>> $hudscoreenemycolour  16) 0xFF)
            _HudScoreEnemyColorG  = (& (>> $hudscoreenemycolour   8) 0xFF)
            _HudScoreEnemyColorB  = (&     $hudscoreenemycolour      0xFF)
            guilist [guitext "HudScoreEnemyColour:^t"; guicolor $hudscoreenemycolour]
            guislider _HudScoreEnemyColorR 0 255 [hudscoreenemycolour $_HudScoreEnemyColorR $_HudScoreEnemyColorG $_HudScoreEnemyColorB]
            guislider _HudScoreEnemyColorG 0 255 [hudscoreenemycolour $_HudScoreEnemyColorR $_HudScoreEnemyColorG $_HudScoreEnemyColorB]
            guislider _HudScoreEnemyColorB 0 255 [hudscoreenemycolour $_HudScoreEnemyColorR $_HudScoreEnemyColorG $_HudScoreEnemyColorB]

            _HudScorePlayerColorR = (& (>> $hudscoreplayercolour 16) 0xFF)
            _HudScorePlayerColorG = (& (>> $hudscoreplayercolour  8) 0xFF)
            _HudScorePlayerColorB = (&     $hudscoreplayercolour     0xFF)
            guilist [guitext "HudScorePlayerColour:^t"; guicolor $hudscoreplayercolour]
            guislider _HudScorePlayerColorR 0 255 [hudscoreplayercolour $_HudScorePlayerColorR $_HudScorePlayerColorG $_HudScorePlayerColorB]
            guislider _HudScorePlayerColorG 0 255 [hudscoreplayercolour $_HudScorePlayerColorR $_HudScorePlayerColorG $_HudScorePlayerColorB]
            guislider _HudScorePlayerColorB 0 255 [hudscoreplayercolour $_HudScorePlayerColorR $_HudScorePlayerColorG $_HudScorePlayerColorB]

            guitext "HudScoreSep:"
            guislider hudscoresep

            guilist [
                guitext "HudScoreScale:^t^t "
                guifield hudscorescale 5
                guibutton "" [hudscorescale (+fround $hudscorescale -0.01)]     "val_dec"
                guibutton "" [hudscorescale (+fround $hudscorescale  0.01)]     "val_inc"
            ]

            guilist [
                guitext "HudScoreX:^t^t "
                guifield hudscorex 5
                guibutton "" [hudscorex (+fround $hudscorex -0.01)]             "val_dec"
                guibutton "" [hudscorex (+fround $hudscorex  0.01)]             "val_inc"
            ]
            guilist [
                guitext "HudScoreY:^t^t "
                guifield hudscorey 5
                guibutton "" [hudscorey (+fround $hudscorey -0.01)]             "val_dec"
                guibutton "" [hudscorey (+fround $hudscorey  0.01)]             "val_inc"
            ]
        ]
    ]
] "HudScore"


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
_crosshairs = [
    "data/crosshair.png                   data/hit.png"
    "packages/crosshairs/x.png            packages/crosshairs/x-hit.png"
    "packages/crosshairs/x_dot.png        packages/crosshairs/x_dot-hit.png"
    "packages/crosshairs/o.png            packages/crosshairs/o-hit.png"
    "packages/crosshairs/o_x.png          packages/crosshairs/o_x-hit.png"
    "packages/crosshairs/o_dot.png        packages/crosshairs/o_dot-hit.png"
    "packages/crosshairs/dot.png          packages/crosshairs/dot-hit.png"
    "packages/crosshairs/dot_wide.png     packages/crosshairs/dot_wide-hit.png"
    "packages/crosshairs/star.png         packages/crosshairs/star-hit.png"
    "packages/crosshairs/wide.png         packages/crosshairs/wide-hit.png"
    "packages/crosshairs/circle_dot.png   packages/crosshairs/circle_dot_hit.png"
    "packages/crosshairs/cross_normal.png packages/crosshairs/cross_normal_hit.png"
    "packages/crosshairs/cross_whole.png  packages/crosshairs/cross_whole_hit.png"
    "packages/crosshairs/dot_normal.png   packages/crosshairs/dot_normal_hit.png"
    "packages/crosshairs/dot_whole.png    packages/crosshairs/dot_whole_hit.png"
    "packages/crosshairs/dot2.png         packages/crosshairs/dot2_hit.png"
]
newgui "crosshair" [
    guifreerow 12  1 [ guitext "crosshair size: " 0
             ] 32 -2 [ guislider crosshairsize
    ]
    guistrut 0.5
    guifreerow 12  1 [ guitext "crosshair: " 0
             ] 32 -2 [
        loop _iRow (div (+ (listlen $_crosshairs) 6) 8) [
            guilist [
                loop _iCol 8 [
                    _ch = (at $_crosshairs (+ (* $_iRow 8) $_iCol))
                    if (!=s $_ch "") [
                        guiimage (at $_ch 0) [
                            cleargui 1
                            loadcrosshair @(at $_ch 0) 0
                            loadcrosshair @(at $_ch (if (> (listlen $_ch) 1) 1 0)) 2
                        ] 1
                    ]
                ]
            ]
        ]
    ]
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
postfx = [showgui "postfx"]
newgui "postfx" [
    guibutton "(effect OFF)"            [clearpostfx]
    guibutton "bloom (subtle: 30%)"     [bloom 0.3]
    guibutton "bloom (bright: 55%)"     [bloom 0.55]
    guibutton "bloom (intense: 80%)"    [bloom 0.8]
    guibutton "rotoscope"               [rotoscope 1]
    guibutton "rotoscope + blur3"       [rotoscope 1 1]
    guibutton "rotoscope + blur5"       [rotoscope 1 2]
    guibutton "sobel"                   [setpostfx sobel]
    guibutton "invert"                  [setpostfx invert]
    guibutton "gbr"                     [setpostfx gbr]
    guibutton "bw"                      [setpostfx bw]
    guibutton "blur3"                   [setpostfx hblur3; addpostfx vblur3]
    guibutton "blur5"                   [setpostfx hblur5; addpostfx vblur5]
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Text Editor:
guitexteditor = [
    guinoautotab [
        guieditor $arg1 $arg2 $arg3
        textinit $arg1 $arg1
        guistayopen [
            guialign 0 [
                guilist [
                    guifreerow  9.25 -2 [ guibutton "load"   [textfocus @arg1; textload @arg1]
                             ]  9.25 -2 [ guibutton "save"   [textfocus @arg1; textsave @arg1]
                             ]  9.25 -2 [ guibutton "exec"   [textfocus @arg1; textexec]
                             ]  9.25 -2 [ guibutton "copy"   [textfocus @arg1; textcopy]
                             ]  9.25 -2 [ guibutton "paste"  [textfocus @arg1; textpaste]
                             ]  9.25 -2 [ guibutton "select" [textfocus @arg1; textselectall]
                             ]  9.25 -2 [ guibutton "clear"  [textfocus @arg1; textclear]
                    ]
                ]
            ]
        ]
    ]
]

_notepadfile = "untitled.txt"
notepad = [ if (> $numargs 0) [_notepadfile = $arg1]; showgui "notepad" ]
newgui "notepad" [
    guifield _notepadfile -30
    guitexteditor $_notepadfile -80 20
]

pastebuffer = [showgui "pastebuffer"]
newgui "pastebuffer" [
    guinoautotab [
        guieditor "#pastebuffer" -80 20
        guistayopen [
            guilist [
                guibutton "exec" [textfocus "#pastebuffer"; textexec]
                guibar
                guibutton "clear" [textfocus "#pastebuffer"; textclear]
            ]
        ]
    ]
]

scratchpad = [showgui "scratchpad"]
newgui "scratchpad" [
    guinoautotab [
        guieditor "#scratchpad" -80 20
        guistayopen [
            guialign 0 [
                guilist [
                    guifreerow  9.25 -2 [ guibutton "exec"   [textfocus "#scratchpad"; textexec]
                             ]  9.25 -2 [ guibutton "copy"   [textfocus "#scratchpad"; textcopy]
                             ]  9.25 -2 [ guibutton "paste"  [textfocus "#scratchpad"; textpaste]
                             ]  9.25 -2 [ guibutton "select" [textfocus "#scratchpad"; textselectall]
                             ]  9.25 -2 [ guibutton "clear"  [textfocus "#scratchpad"; textclear]
                    ]
                ]
            ]
        ]
    ]
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Editing:

_NewMap_Size  = 10
_SaveMap_Name = "untitled"

newgui "editing" [
    guikeeptab 1
    guinoautotab [
    //=========================================================================================================================
        guibutton "toggle edit mode^t^t^f2[E]"  [edittoggle]
        guibar //-------------------------------------------------------------------------
        guilist [
            guilist [
                guistrut 38 1
                guitext "select^t^t^t^t^f2[MouseLeft ^f6drag^f2]"
                guitext "extend selection^t^t^f2[MouseRight]"
                guitext "move selection^t^t^t^f2[MouseRight ^f6drag^f2]"
                guitext "deselect^t^t^t^t^f2[SPACE]"
                guitext "select all ents in box^t^f2[NumPad-ENTER]"
            ]
            guilist [
                guitext "select corners^t^t^t^f2[MouseMiddle ^f6drag^f2]"
                guitext "reorient^t^t^t^t^f2[MouseRight]"
                guitext "passthrough^t^t^t^f2[LCTRL ^f6hold^f2]"
                guitext "center view on ent^t^t^f2[, + MouseWheel]"
                guitext "select all matching ents^t^f2[N]"
            ]
        ]
        guibar //-------------------------------------------------------------------------
        guilist [
            guilist [
                guistrut 38 1
                guitext "push/pull selection^t^t^f2[MouseWheel]"
                guitext "change gridsize^t^t^f2[G + MouseWheel]"
                guitext "mirror on selected axis^t^f2[X]"
            ]
            guilist [
                guitext "push cube face^t^t^f2[F + MouseWheel]"
                guitext "push corner with cursor^t^f2[Q + MouseWheel]"
                guitext "rotate^t^t^t^t^f2[R + MouseWheel]"
            ]
        ]
        guibar //-------------------------------------------------------------------------
        guilist [
            guilist [
                guistrut 38 1
                guitext "undo^t^t^t^t^t^f2[U] [Z]"
                guitext "copy^t^t^t^t^t^f2[C]"
                guitext "delete^t^t^t^t^f2[DEL]"
            ]
            guilist [
                guitext "redo^t^t^t^t^t^f2[I]"
                guitext "paste^t^t^t^t^f2[V]"
            ]
        ]
        guibar //-------------------------------------------------------------------------
        guilist [
            guilist [
                guistrut 38 1
                guitext     "change textures^t^t^f2[Y + MouseWheel]"
                guicheckbox "texture all faces^t^t^f2[0]"               [allfaces]
                guicheckbox "toggle outline^t^t^t^f2[7]"                [outline]
            ]
            guilist [
                guibutton   "texture gallery..^t^t^f2[F2]"              [showtexgui]            "menu"
                guibutton   "materials.."                               [showgui "materials"]
                guicheckbox "toggle wireframe^t^t^f2[8]"                [wireframe]
            ]
        ]

    guitab "ents" //===========================================================================================================
        guilist [
            guilist [
                guistrut 38 1
                guibutton   "edit selected ent..^f2^t[F3]"              showentgui
                guicheckbox "entity snap-to-grid^f2^t[6]"               entselsnap
                guicheckbox "new entities take yaw from camera"         entcamdir
            ]
            guilist [
                guibutton   "edit selected entity in console^f2 [.]"    selentedit
                guicheckbox "entity selection"                          entediting
            ]
        ]
        guibar //-------------------------------------------------------------------------
        guilist [
            guilist [
                guistrut 12 1
                guitext "^f2/entdrop *"
            ]
            guilist [
                guistrut 26 1
                guiradio "(2) at selection"                       entdrop 2
                guiradio "(3) at selection, to the floor"         entdrop 3
            ]
            guilist [
                guiradio "(0) at current position"                entdrop 0
                guiradio "(1) at current position, to the floor"  entdrop 1
            ]
        ]
        guibar //-------------------------------------------------------------------------
        guilist [
            guilist [
                guistrut 12 1
                guitext "^f2/newent *"
            ]
            guilist [
                guistrut 22 1
                guibutton "light.."             [showgui "newlight"]
                guibutton "mapmodel..  ^f2[F4]" [showgui "mapmodels"]
                guibutton "teleport.."          [showgui "newteleport"]
            ]
            guilist [
                guistrut 22 1
                guibutton "playerstart"         [newent playerstart]
                guibutton "playerstart 1"       [newent playerstart 1]
                guibutton "playerstart 2"       [newent playerstart 2]

            ]
            guilist [
                guibutton "base"                [newent base]
                guibutton "flag 1"              [newent flag 1]
                guibutton "flag 2"              [newent flag 2]
            ]
        ]
        guistrut 1
        guilist [
            guilist [
                guistrut 12 1
                guialign 1 [guitext "pickups: " 0]
            ]
            guilist [
                guistrut 22 1
                guibutton "shells"        [newent shells]
                guibutton "bullets"       [newent bullets]
                guibutton "rockets"       [newent rockets]
                guibutton "riflerounds"   [newent riflerounds]
                guibutton "grenades"      [newent grenades]
                guibutton "cartridges"    [newent cartridges]
            ]
            guilist [
                guibutton "health"        [newent health]
                guibutton "healthboost"   [newent healthboost]
                guibutton "greenarmour"   [newent greenarmour]
                guibutton "yellowarmour"  [newent yellowarmour]
                guibutton "quaddamage"    [newent quaddamage]
                guibutton "select all pickup ents  ^f2/entfindpickups"   [entfindpickups]
            ]
        ]
        guistrut 1
        guilist [
            guilist [
                guistrut 12 1
                guialign 1 [guitext "SP mode: " 0]
            ]
            guilist [
                guistrut 22 1
                guibutton "monster.."  [showgui "monsters"]
            ]
            guilist [
                guistrut 22 1
                guibutton "trigger.."  [showgui "triggers"]
            ]
        ]

    guitab "lighting" //=======================================================================================================
        guibutton "maplighting..^t^f2/maplighting"  [showgui "maplighting"]
        guibar //-------------------------------------------------------------------------
        _lightingstuff

    guitab "skybox" //=========================================================================================================
        guilist [
            guistayopen [
                guilist [_genskyitems $_skies1]
                guilist [_genskyitems $_skies2]
                guilist [_genskyitems $_skies3]
                _skyboxpreview
            ]
        ]

    guitab "sky" //============================================================================================================
        guistayopen [
            _SkyboxColorR = (& (>> $skyboxcolour 16) 0xFF)
            _SkyboxColorG = (& (>> $skyboxcolour  8) 0xFF)
            _SkyboxColorB = (&     $skyboxcolour     0xFF)
            guilist [
                guilist [
                    guistrut 18 1
                    guialign 1 [guitext "skyboxcolour: " 0]
                    guistrut 1
                    guialign 1 [guicolor $skyboxcolour; guistrut 1]
                ]
                guilist [
                    guistrut 57 1
                    guislider _SkyboxColorR 0 255 [skyboxcolour $_SkyboxColorR $_SkyboxColorG $_SkyboxColorB]
                    guislider _SkyboxColorG 0 255 [skyboxcolour $_SkyboxColorR $_SkyboxColorG $_SkyboxColorB]
                    guislider _SkyboxColorB 0 255 [skyboxcolour $_SkyboxColorR $_SkyboxColorG $_SkyboxColorB]
                ]
            ]
            guistrut 1 //-------------------------------------------------------------------------
            guifreerow 18  1 [ guitext "rotation: " 0
                     ] 12 -2 [ guitext "yawsky:"
                     ] 45 -2 [ guislider yawsky
            ]
            guistrut 1 //-------------------------------------------------------------------------
            guifreerow 18  1 [ guitext "animation: " 0
                     ]  1 -2 [ guifield2 "spinsky:" 12 spinsky 6 -0.1 0.1
            ]
        ]



        //@@    Do atmo stuff...



    guitab "cloudlayer" //=====================================================================================================
        guistayopen [
            guilist [
                guilist [
                    guistrut 18 1
                    guialign 1 [guitext "cloudlayer: " 0]
                ]
                guilist [
                    guistrut 28 1
                    guibutton "skyboxes/clouds01.png"  [cloudlayer "skyboxes/clouds01.png"]
                    guibutton "skyboxes/clouds02.png"  [cloudlayer "skyboxes/clouds02.png"]
                    guibutton "skyboxes/clouds03.png"  [cloudlayer "skyboxes/clouds03.png"]
                    guibutton "skyboxes/white_bk.jpg"  [cloudlayer "skyboxes/white_bk.jpg"]
                    guibutton "^f8*no clouds*"         [cloudlayer ""]
                ]
                guilist [
                    guibutton "blindabuser/blindaclouds.png"  [cloudlayer "blindabuser/blindaclouds.png"]
                    guibutton "skyboxes/cloud_ring.png"       [cloudlayer "skyboxes/cloud_ring.png"]
                    guibutton "textures/razgriz/cloud03.png"  [cloudlayer "textures/razgriz/cloud03.png"]
                    guibutton "textures/razgriz/cloud04.png"  [cloudlayer "textures/razgriz/cloud04.png"]
                ]
            ]
            guistrut 1 //-------------------------------------------------------------------------
            _CloudColorR = (& (>> $cloudcolour 16) 0xFF)
            _CloudColorG = (& (>> $cloudcolour  8) 0xFF)
            _CloudColorB = (&     $cloudcolour     0xFF)
            guilist [
                guilist [
                    guistrut 18 1
                    guialign 1 [guitext "cloudcolour: " 0]
                    guistrut 1
                    guialign 1 [guicolor $cloudcolour; guistrut 1]
                ]
                guilist [
                    guistrut 57 1
                    guislider _CloudColorR 0 255 [cloudcolour $_CloudColorR $_CloudColorG $_CloudColorB]
                    guislider _CloudColorG 0 255 [cloudcolour $_CloudColorR $_CloudColorG $_CloudColorB]
                    guislider _CloudColorB 0 255 [cloudcolour $_CloudColorR $_CloudColorG $_CloudColorB]
                ]
            ]
            guistrut 1 //-------------------------------------------------------------------------
            guifreerow 18  1 [ guitext "transparency: " 0
                     ] 28 -2 [ guifield2 "cloudalpha:"     15.5  cloudalpha     6  -0.05 0.05
                     ] 28 -2 [ guifield2 "cloudfade:"      15.5  cloudfade      6  -0.05 0.05
            ]
            guistrut 1 //-------------------------------------------------------------------------
            guifreerow 18  1 [ guitext "position/scale: " 0
                     ] 28 -2 [ guifield2 "cloudoffsetx:"   15.5  cloudoffsetx   6  -0.01 0.01
                     ] 28 -2 [ guifield2 "cloudheight:"    15.5  cloudheight    6  -0.01 0.01
            ]
            guifreerow 18 -2 [
                     ] 28 -2 [ guifield2 "cloudoffsety:"   15.5  cloudoffsety   6  -0.01   0.01
                     ] 28 -2 [ guifield2 "cloudscale:"     15.5  cloudscale     6  -0.0625 0.0625
            ]
            guistrut 1 //-------------------------------------------------------------------------
            guifreerow 18  1 [ guitext "animation: " 0
                     ] 28 -2 [ guifield2 "cloudscrollx:"   15.5  cloudscrollx   6  -0.0005 0.0005
                     ] 28 -2 [ guifield2 "spincloudlayer:" 15.5  spincloudlayer 6  -0.1    0.1
            ]
            guifreerow 18 -2 [
                     ] 28 -2 [ guifield2 "cloudscrolly:"   15.5  cloudscrolly   6  -0.0005 0.0005
            ]
        ]

    guitab "fog" //============================================================================================================
        guistayopen [
            guifreerow 18  1 [ guitext "fog: " 0
                     ] 57 -2 [ guislider fog 128 16384
            ]
            guistrut 1
            _FogColorR = (& (>> $fogcolour 16) 0xFF)
            _FogColorG = (& (>> $fogcolour  8) 0xFF)
            _FogColorB = (&     $fogcolour     0xFF)
            guilist [
                guilist [
                    guistrut 18 1
                    guialign 1 [guitext "fogcolour: " 0]
                    guistrut 1
                    guialign 1 [guicolor $fogcolour; guistrut 1]
                ]
                guilist [
                    guistrut 57 1
                    guislider _FogColorR 0 255 [fogcolour $_FogColorR $_FogColorG $_FogColorB]
                    guislider _FogColorG 0 255 [fogcolour $_FogColorR $_FogColorG $_FogColorB]
                    guislider _FogColorB 0 255 [fogcolour $_FogColorR $_FogColorG $_FogColorB]
                ]
            ]
            guibar //-------------------------------------------------------------------------
            _FogDomeColorR = (& (>> $fogdomecolour 16) 0xFF)
            _FogDomeColorG = (& (>> $fogdomecolour  8) 0xFF)
            _FogDomeColorB = (&     $fogdomecolour     0xFF)
            guilist [
                guilist [
                    guistrut 18 1
                    guialign 1 [guitext "fogdomecolour: " 0]
                    guialign 1 [guitext "^f4(0 = use fogcolour) " 0]
                    guialign 1 [guicolor $fogdomecolour; guistrut 1]
                ]
                guilist [
                    guistrut 57 1
                    guislider _FogDomeColorR 0 255 [fogdomecolour $_FogDomeColorR $_FogDomeColorG $_FogDomeColorB]
                    guislider _FogDomeColorG 0 255 [fogdomecolour $_FogDomeColorR $_FogDomeColorG $_FogDomeColorB]
                    guislider _FogDomeColorB 0 255 [fogdomecolour $_FogDomeColorR $_FogDomeColorG $_FogDomeColorB]
                ]
            ]
            guistrut 1 //-------------------------------------------------------------------------
            guilist [
                guilist [
                    guistrut 18 1
                ]
                guilist [
                    guistrut 28 1
                    guifield2 "fogdomeclip:"   15.5  fogdomeclip   6  -0.01 0.01
                    guifield2 "fogdomemin:"    15.5  fogdomemin    6  -0.01 0.01
                    guifield2 "fogdomemax:"    15.5  fogdomemax    6  -0.01 0.01
                    guifield2 "fogdomeheight:" 15.5  fogdomeheight 6  -0.01 0.01
                ]
                guilist [
                    guicheckbox "fogdomeclouds" fogdomeclouds
                    guistrut 0.5
                    guicheckbox "fogdomesquare" fogdomesquare
                    guistrut 0.5
                    guicheckbox "fogdomecap"    fogdomecap
                ]
            ]
        ]

    guitab "height/blend" //================================================================================================
        guilist [
            guilist [
                guistrut 38 1
                guitext "toggle heightmap mode^t^t^f2[H]"
                guitext "push/pull heightmap^t^t^t^f2[MouseWheel]"
            ]
            guitext "select heightmap brush^t^f2[B + MouseWheel]"
        ]
        guibar //-------------------------------------------------------------------------
        guitext "blend paint mode:"
        guilist [
            guilist [
                guistrut 38 1
                guiradio (concatword (at $_Labels_BlendPaintMode 0) "^t^t^t^t^t^t^f2[NumPad 0]")    blendpaintmode 0
                guiradio (concatword (at $_Labels_BlendPaintMode 2) "^t^t^t^t^t^t^f2[NumPad 2]")    blendpaintmode 2
                guiradio (concatword (at $_Labels_BlendPaintMode 4) "^t^t^t^t^f2[NumPad 4]"    )    blendpaintmode 4
            ]
            guilist [
                guiradio (concatword (at $_Labels_BlendPaintMode 1) "^t^t^t^t^t^f2[NumPad 1]"  )    blendpaintmode 1
                guiradio (concatword (at $_Labels_BlendPaintMode 3) "^t^t^t^t^f2[NumPad 3]"    )    blendpaintmode 3
                guiradio (concatword (at $_Labels_BlendPaintMode 5) "^t^t^t^t^t^f2[NumPad 5]"  )    blendpaintmode 5
            ]
        ]
        guistrut 1
        guitext "paint blendmap^t^t^t^f2[MouseLeft]"
        guitext "change blend brush^t^t^t^f2[MouseWheel]"
        guitext "rotate blend brush^t^t^t^f2[MouseRight]"
        guistrut 1
        guibutton "clear entire blendmap^t^t^f2/clearblendmap"        [clearblendmap]
        guibutton "clear blendmap in selection^t^f2/clearblendmapsel" [clearblendmapsel]

    guitab "map" //============================================================================================================
        guifreerow 18  1 [ guitext "newmap: " 0
                 ] 12 -2 [ guitext "scale:"
                 ] 45 -2 [ guislider _NewMap_Size 10 16
        ]
        guifreerow 18 -2 [
                 ] 12 -2 [ guitext "size:"
                 ] 45 -2 [ guinameslider _NewMap_Size ["1024" "2048" "4096" "8192" "16384" "32768" "65536"] [10 11 12 13 14 15 16] []
        ]
        guifreerow 18 -2 [
                 ]  1 -2 [ guibutton "create newmap" [newmap $_NewMap_Size]
        ]
        guistrut 1 //-------------------------------------------------------------------------
        guifreerow 18  1 [ guitext "loadmap: " 0
                 ] 24 -2 [ guibutton "offical maps" [showgui "maps"] "menu"
                 ]  1 -2 [ guibutton "custom maps"   showcustommaps  "menu"
        ]
        guistrut 1 //-------------------------------------------------------------------------
        guifreerow 18  1 [ guitext "savemap: " 0
                 ] 24 -2 [ guifield  _SaveMap_Name 22
                 ]  1 -2 [ guibutton "save" [savemap $_SaveMap_Name]
        ]
        guibar //-------------------------------------------------------------------------
        guifreerow 18 -2 [
                 ]  1 -2 [ guibutton "set map title/author"  [saycommand [/maptitle @maptitle]]
        ]
        guibar //-------------------------------------------------------------------------
        guifreerow 18 -2 [
                 ] 24 -2 [ guibutton "^f2/remip"   [remip]
                 ]  1 -2 [ guitext "^f4(optimize octree/geometry)" 0
        ]
        guifreerow 18 -2 [
                 ] 24 -2 [ guibutton "^f2/recalc"  [recalc]
                 ]  1 -2 [ guitext "^f4(regenerate: envmaps, minimap, etc)" 0
        ]
        guibar //-------------------------------------------------------------------------
        guifreerow 18  1 [ guitext "resize: " 0
                 ]  1 -2 [ guibutton "increase mapsize (2x)"  mapenlarge
        ]
        guifreerow 18 -2 [
                 ] 24 -2 [ guibutton "decrease mapsize (1/2)"  shrinkmap
                 ]  1 -2 [ guitext "^f4(shrunk area must be empty)" 0
        ]
        guibar //-------------------------------------------------------------------------
        guifreerow 18  1 [ guitext "undo size: " 0
                 ] 57 -2 [ guilistslider undomegs [1 2 3 4 6 8 12 16 20 24 32 40 48 56 64 72 80 88 96 100]
        ]
        guifreerow 18 -2 [
                 ] 24 -2 [ guitext "^f4(in megabytes)" 0
                 ] 33  1 [ guitext "^f4(default 8)" 0
        ]
    guitab "cfg" //============================================================================================================
        guitext (mapcfgname)
        guitexteditor (mapcfgname) -72 17
    ]
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
_skies1 = [
    skyboxes/black
    skyboxes/white
    aftas/sky/orbe
    blindabuser/blindasky
    blindabuser/blindasky2
    darc/forge
    dash/moon
    hazel/green
    humus/meadow
    ik2k/env/iklake
    jon/prarie
    mayhem/duomo
    mayhem/eclipse
    meister/uzze
    penguins/arid
    penguins/arid2
    penguins/cocoa
    penguins/divine
    penguins/harmony
]
_skies2 = [
    penguins/mellow
    penguins/mystic
    penguins/violence
    penguins/wrath
    penguins/yonder
    penguins/zeus
    ratboy/skyboxes/coward
    shmutzwurst/london
    skyboxes/bbk/d
    skyboxes/bbk/dark2
    skyboxes/bbk/n1
    skyboxes/bbk/t
    skyboxes/clearsky052
    skyboxes/darkness
    skyboxes/evilsky
    skyboxes/morning
    skyboxes/remus/sky01
    skyboxes/turbulent
    skyboxes/ulukai/corona
]
_skies3 = [
    socksky/barren
    socksky/desert
    socksky/devpun
    socksky/emerald
    socksky/frozen
    socksky/frozendusk
    socksky/grave
    socksky/mars
    socksky/nightball
    socksky/stratosphere
    socksky/valley
    staffy/staffy
]

_genskyitems = [
    looplist cursky $arg1 [
        guibutton $cursky (concat skybox $cursky)
    ]
]

_skyboxpreview = [
    guibar
    _skyshotname = (checkrolloveraction "skybox " [if (> $numargs 0) [result $arg1] [at $guirollovername 0]])
    guiimage (concatword "<thumbnail:128>packages/" $_skyshotname "_ft") (checkrolloveraction "skybox ") 4 1 "data/cube.png" (chopstr $_skyshotname -16 "..")
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
maplighting = [showgui "maplighting"]
newgui "maplighting" [
    //=========================================================================================================================
    guinoautotab [
        guibutton "Get sun pitch & yaw from camera.  ^f2/getsundir" [getsundir]
        guitext "sunlightpitch: "; guislider sunlightpitch
        guitext "sunlightyaw: "  ; guislider sunlightyaw
        guibar //-------------------------------------------------------------------------
        _SunLightR = (& (>> $sunlight 16) 0xFF)
        _SunLightG = (& (>> $sunlight  8) 0xFF)
        _SunLightB = (&     $sunlight     0xFF)
        guilist [guitext "sunlightscale: "; guifield sunlightscale 8]
        guilist [guitext "sunlight:^t^t^t^t^t^t^t^t"; guicolor $sunlight]
        guislider _SunLightR 0 255 [sunlight $_SunLightR $_SunLightG $_SunLightB]
        guislider _SunLightG 0 255 [sunlight $_SunLightR $_SunLightG $_SunLightB]
        guislider _SunLightB 0 255 [sunlight $_SunLightR $_SunLightG $_SunLightB]
        guibar //-------------------------------------------------------------------------
        _SkyLightR = (& (>> $skylight 16) 0xFF)
        _SkyLightG = (& (>> $skylight  8) 0xFF)
        _SkyLightB = (&     $skylight     0xFF)
        guilist [guitext "skylight:^t^t^t^t^t^t^t^t"; guicolor $skylight]
        guislider _SkyLightR 0 255 [skylight $_SkyLightR $_SkyLightG $_SkyLightB]
        guislider _SkyLightG 0 255 [skylight $_SkyLightR $_SkyLightG $_SkyLightB]
        guislider _SkyLightB 0 255 [skylight $_SkyLightR $_SkyLightG $_SkyLightB]
        guibar //-------------------------------------------------------------------------
        _AmbientR  = (& (>> $ambient  16) 0xFF)
        _AmbientG  = (& (>> $ambient   8) 0xFF)
        _AmbientB  = (&     $ambient      0xFF)
        guilist [guitext "ambient:^t^t^t^t^t^t^t^t"; guicolor $ambient]
        guislider _AmbientR 0 255 [ambient $_AmbientR $_AmbientG $_AmbientB]
        guislider _AmbientG 0 255 [ambient $_AmbientR $_AmbientG $_AmbientB]
        guislider _AmbientB 0 255 [ambient $_AmbientR $_AmbientG $_AmbientB]
    //=========================================================================================================================
    guitab "settings"
        _lightingstuff
    ]
] "sunlight"


_lightingstuff = [
    guicheckbox "fullbright^t^t^f2[L]" [fullbright]

//@@    fullbrightlevel
//@@    clearlightmaps    dumplms
//@@    edgetolerance
//@@    adaptivesample

//@@@@@@@@  roundlightmaptex ???

//@@    lerpangle
//@@    lerpsubdiv
//@@    lerpsubdivsize
//@@    lerptjoints


    guibar //-------------------------------------------------------------------------
    guilist [
        guitext "blurskylight:^t"
        guiradio "0 " blurskylight 0
        guiradio "1 " blurskylight 1
        guiradio "2 " blurskylight 2
    ]
    guilist [
        guitext "blurlightmaps:^t"
        guiradio "0 " blurlms 0
        guiradio "1 " blurlms 1
        guiradio "2 " blurlms 2
    ]
    guistrut 1
    guitext "lightprecision:"
    guilistslider lightprecision [8 16 32 48 64 96 128 256]
    guitext "lightlod: (>= use lightprecision, < use lightprecision/2)"
    guislider lightlod
    guistrut 1
    guitext "lighterror:"
    guislider lighterror
    guibar //-------------------------------------------------------------------------
    guibutton "calclight^t1^t^f~(slow: 8xAA)"                       [calclight  1]
    guibutton "calclight^t-1^t^f~(quick: no AA, no model shadows)"  [calclight -1]
    guitext "lightthreads:"
    guislider lightthreads
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
newgui "materials" [
    guibutton "air"                        [editmat air]
    guibutton "water"                      [editmat water]
    guibutton "lava"                       [editmat lava]
    guibutton "clip"                       [editmat clip]
    guibutton "glass"                      [editmat glass]
    guibutton "noclip"                     [editmat noclip]
    guibutton "gameclip"                   [editmat gameclip]
    guibutton "death"                      [editmat death]
    guibutton "alpha"                      [editmat alpha]

    guicheckbox "show material volumes"    [showmat]

    //guibutton "waterlod very high (3)"  [waterlod 3]
    //guibutton "waterlod high      (2)"  [waterlod 2]
    //guibutton "waterlod default   (1)"  [waterlod 1]
    //guibutton "waterlod low       (0)"  [waterlod 0]

    //guibutton "watersubdiv very fine (0)"  [watersubdiv 0]
    //guibutton "watersubdiv fine      (1)"  [watersubdiv 1]
    //guibutton "watersubdiv default   (2)"  [watersubdiv 2]
    //guibutton "watersubdiv coarse    (3)"  [watersubdiv 3]

    //guibutton "water waves on"  [waterwaves 1]
    //guibutton "water waves off" [waterwaves 0]
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
newgui "triggers" [
    guibutton "invisible"   [BTmm = -1; showgui "trigger2"]
    guibutton "a carrot"    [BTmm = 23; showgui "trigger2"]
    guibutton "switch #1"   [BTmm = 24; showgui "trigger2"]
    guibutton "switch #2"   [BTmm = 25; showgui "trigger2"]
    guibutton "door #1"     [BTmm = 26; showgui "trigger2"]
    guibutton "door #2"     [BTmm = 27; showgui "trigger2"]
    guibutton "door #3"     [BTmm = 28; showgui "trigger2"]
]

newgui "trigger2" [
    guibutton "animation loop"                      [BTtt =  0; showgui "trigger3"]
    guibutton "trigger once (popback)"              [BTtt =  1; showgui "trigger3"]
    guibutton "trigger once (popback & rumble)"     [BTtt =  2; showgui "trigger3"]
    guibutton "trigger once"                        [BTtt =  3; showgui "trigger3"]
    guibutton "trigger once (rumble)"               [BTtt =  4; showgui "trigger3"]
    guibutton "trigger multiple (popback)"          [BTtt =  5; showgui "trigger3"]
    guibutton "trigger multiple (popback & rumble)" [BTtt =  6; showgui "trigger3"]
    guibutton "trigger multiple"                    [BTtt =  7; showgui "trigger3"]
    guibutton "trigger multiple (rumble)"           [BTtt =  8; showgui "trigger3"]
    guibutton "door (open once)"                    [BTtt =  9; showgui "trigger3"]
    guibutton "door (auto close)"                   [BTtt = 10; showgui "trigger3"]
    guibutton "door (locked)"                       [BTtt = 11; showgui "trigger3"]
    guibutton "vanishing trigger"                   [BTtt = 12; showgui "trigger3"]
    guibutton "vanishing trigger (rumble)"          [BTtt = 13; showgui "trigger3"]
    guibutton "End Of Level Trigger"                [BTtt = 29; showgui "trigger3"]
]

build_trigger = [newent mapmodel $BTmm $BTtt $BTti]
newgui "trigger3" [
    loop i 9 [
        _tjt = (concat tag (concatword # $i))
        _tjc = (concat BTti "=" $i ";" "build_trigger")
        guibutton $_tjt $_tjc
    ]
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
_NL_ResetLight = [
    _NL_ColorPreset =   0
    _NL_Bright      =   1
    _NL_Radius      = 128
    _NL_LightSet 255 255 255
]

_NL_LightSet = [
    _NL_ColorR = $arg1
    _NL_ColorG = $arg2
    _NL_ColorB = $arg3
    if (= $_NL_Bright 0) _NL_LightScale
]

_NL_LightScale = [
    if (= $_NL_Bright 0) [
        _NL_ColorR = (div $_NL_ColorR 2); _NL_ColorG = (div $_NL_ColorG 2); _NL_ColorB = (div $_NL_ColorB 2)
    ] [
        _NL_ColorR = (min 255 (* $_NL_ColorR 2)); _NL_ColorG = (min 255 (* $_NL_ColorG 2)); _NL_ColorB = (min 255 (* $_NL_ColorB 2))
    ]
]

_NL_CreateLight = [result (concat newent light $_NL_Radius $_NL_ColorR $_NL_ColorG $_NL_ColorB)]
newgui "newlight" [
    guialign 0 [guibutton (concatword " ^f7/newent ^f2light ^f3" (_NL_ColorR) " ^f0" (_NL_ColorG) " ^f1" (_NL_ColorB) ) (_NL_CreateLight) 0]
    guibar
    guitext "color:"
    guicolor (+ (* (+ (* $_NL_ColorR 256) $_NL_ColorG) 256) $_NL_ColorB)
    guislider _NL_ColorR 0 255
    guislider _NL_ColorG 0 255
    guislider _NL_ColorB 0 255
    guilist [
        guicheckbox "bright"    _NL_Bright 1 0 [_NL_LightScale]
        guibar
        guiradio "white"        _NL_ColorPreset 0 [_NL_LightSet 255 255 255]
        guiradio "red"          _NL_ColorPreset 1 [_NL_LightSet 255 192 192]
        guiradio "yellow"       _NL_ColorPreset 2 [_NL_LightSet 255 255 192]
        guiradio "green"        _NL_ColorPreset 3 [_NL_LightSet 192 255 192]
        guiradio "cyan"         _NL_ColorPreset 4 [_NL_LightSet 192 255 255]
        guiradio "blue"         _NL_ColorPreset 5 [_NL_LightSet 192 192 255]
        guiradio "purple"       _NL_ColorPreset 6 [_NL_LightSet 255 192 255]
    ]
    guitext "radius:"
    guislider _NL_Radius 0 512
    guistrut 1
    guialign 0 [guibutton "^f2Create Light" (_NL_CreateLight) 0]
] "" [_NL_ResetLight]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
newgui "newteleport" [
    guibutton "newent teleport 1"
    guibutton "newent teledest 1"

    guibutton "newent teleport 2"
    guibutton "newent teledest 2"

    guibutton "newent teleport 3"
    guibutton "newent teledest 3"

    guibutton "newent teleport 4"
    guibutton "newent teledest 4"
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
newgui "monsters" [
    guialign 0 [guitext " type        weapon" 0]
    guibar
    guibutton "(0) Ogro^t^t^t^f5FireBall"           [newent monster 0];
    guibutton "(1) Rhino^t^t^t^f0Chaingun"          [newent monster 1];
    guibutton "(2) Ratamahatta^t^f3Shotgun"         [newent monster 2];
    guibutton "(3) Slith^t^t^t^f1Rifle"             [newent monster 3];
    guibutton "(4) Bauul^t^t^t^f6RocketLauncher"    [newent monster 4];
    guibutton "(5) Hellpig^t^t^f2Bite"              [newent monster 5];
    guibutton "(6) Knight^t^t^t^f5IceBall"          [newent monster 6];
    guibutton "(7) Goblin^t^t^t^f5SlimeBall"        [newent monster 7];
    guibutton "(8) Spider^t^t^t^f8Grenade"          [newent monster 8];
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
entupdate = [ entset $entguitype $entguiattr0 $entguiattr1 $entguiattr2 $entguiattr3 $entguiattr4 ]

initentgui = [
    entguitype = (enttype)
    @(loopconcat i 5 [
        result [ entguiattr@i = (entattr @i) ]
        ]
    )
]

genentattributes = [
    entattributes = (loopconcat i (listlen $arg2) [
        entattribname = (at $arg2 $i)
        entattriblimits = (at $arg3 (* 2 $i))
        entattriblimits2 = (at $arg3 (+ 1 (* 2 $i)))
        if (=s (at $entattriblimits 0) $entattriblimits) [
            result [
                guitext @entattribname
                guislider entguiattr@i @entattriblimits @entattriblimits2 entupdate
            ]
        ][
            result [
                guitext @entattribname
                guinameslider entguiattr@i [@@entattriblimits] [@@entattriblimits2] entupdate
            ]
        ]
    ])
]

quickeditmenu = [
    guitext "Quick Commands:"
    guibar
    guifield  _SaveMap_Name 10 [ savemap $_SaveMap_Name ]
    guibutton "quick light"       [calclight -1]
    guibutton "optimize map"      [remip]
    guibutton "new entity"        [newent shells]
    guibar
    guibutton newmap
    guibar
    guibutton help "showgui editing"
]

matmenu = [
    guibutton "air"                        [editmat air]
    guibutton "water"                      [editmat water]
    guibutton "lava"                       [editmat lava]
    guibutton "clip"                       [editmat clip]
    guibutton "glass"                      [editmat glass]
    guibutton "noclip"                     [editmat noclip]
    guibutton "gameclip"                   [editmat gameclip]
    guibutton "death"                      [editmat death]
    guibutton "alpha"                      [editmat alpha]
    guicheckbox "show material volumes"    [showmat]
]

brushmenu = [
    guititle diamonds
    guilist [
        guiimage "packages/icons/brush_1c.png"   [brush_0]
        guiimage "packages/icons/brush_21c.png"  [brush_1]
        guiimage "packages/icons/brush_421c.png" [brush_2]
    ]
    guititle squares
    guilist [
        guiimage "packages/icons/brush_3s.png"   [brush_3]
        guiimage "packages/icons/brush_5s.png"   [brush_4]
        guiimage "packages/icons/brush_7s.png"   [brush_5]
    ]
    guititle smooth
    guilist [
        guiimage "packages/icons/brush_3s.png"   [brush_6]
        guiimage "packages/icons/brush_5s.png"   [brush_7]
        guiimage "packages/icons/brush_7s.png"   [brush_8]
    ]
]

newentgui = [
    genentattributes $arg1 $arg2 $arg3
    newgui $arg1 [
        guitext $entguitype
        guibar
        @entattributes
        guitab type
        guilistsplit n 2 $_EntityTypes [
            guibutton $n [ entset @n ]
        ]
        guitab misc
        @quickeditmenu
    ] "" [initentgui]
]

looplist i $_EntityTypes [
    newentgui $i "" ""
]

newgui "materials" [
    @matmenu
    guitab misc
    @quickeditmenu
]

newgui "brushes" [
    @brushmenu
    guitab misc
    @quickeditmenu
]

newgui "quickedit" [
    @quickeditmenu
    guitab materials
    @matmenu
]

newentgui light "radius red green blue" "1 400 0 255 0 255 0 255"
newentgui spotlight "radius" "0 200"
newentgui playerstart "direction" "0 360"
newentgui teleport "tag" "0 20"
newentgui teledest "direction tag" "0 360 0 20"
newentgui monster "direction type" "0 360 0 7"
newentgui mapmodel "direction model" "0 360 0 372"
newentgui envmap "radius" "0 512"
newentgui jumppad "Z Y X" "-128 128 -128 127 -128 127"
newentgui sound "type radius size" "0 20 0 500 0 500"
newentgui particles "type" "0 13"

showentgui = [ contexteditgui ] // legacy bind
contexteditgui = [
    if $hmapedit [showgui "brushes"] [
        if (enthavesel) [
            showgui (enttype)
        ][
            showgui (? (havesel) materials quickedit)
        ]
    ]
]

newgui "seltexinfo" [
    guitext (concatword "Slot: " (getseltex))
    guilist [
        loop _iSubSlot 5 [
            _texname = (gettexname (getseltex) $_iSubSlot)
            _texname = (substr $_texname (+ (strstr $_texname ">") 1)) // chop off any leading commands
            guiimage (concatword "packages/" $_texname) "" 1 1
        ]
    ]
    loop _iSubSlot 5 [
        _texname = (gettexname (getseltex) $_iSubSlot)
        if (strcmp $_texname "") [] [guitext $_texname]
    ]
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
vcolorgui = [if (cleargui 1) [] [showgui "vcoloredit"]]

_VCE_LiveSet = 0
_VCE_Get = [
    _VCE_clrR = (*f (at (getvcolor $getseltex) 0) 1000)
    _VCE_clrG = (*f (at (getvcolor $getseltex) 1) 1000)
    _VCE_clrB = (*f (at (getvcolor $getseltex) 2) 1000)
]
_VCE_Set = [
    vcolor (*f $_VCE_clrR 0.001) (*f $_VCE_clrG 0.001) (*f $_VCE_clrB 0.001)
]

newgui "vcoloredit" [
    if (|| (= $editing 0) (= $isconnected 0)) [cleargui 1] [
        guistayopen [
            guilist [ // H
                guilist [ // V
                    guistrut 64 1
                    guialign 1 [guitext "^f2vcolor: " 0]
                ]
                guilist [guistrut 6 1; guitext (concatword "^f3" (at (getvcolor $getseltex) 0)) 0]
                guilist [guistrut 6 1; guitext (concatword "^f0" (at (getvcolor $getseltex) 1)) 0]
                guilist [guistrut 6 1; guitext (concatword "^f1" (at (getvcolor $getseltex) 2)) 0]
                guistrut 3
                guibutton "^f2[GET]" [_VCE_Get] 0
                guistrut 5
                guibutton "^f8[~~~~~~~SET~~~~~~~]" [_VCE_Set] 0
                guistrut 4
                if (= $connectedip 0) [guicheckbox "Live Set" _VCE_LiveSet 1 0]
            ]
            guistrut 0.5
            guilist [ // H
                guilist [ // V
                    guistrut 64 1
                    guialign 1 [guitext "^f8vcolor: " 0] // H
                    guistrut 2
                    guialign 1 [ // H
                        guicolor (colorvectoint (getvcolor $getseltex))
                        guistrut 1
                    ]
                ]
                guilist [ // V
                    guistrut 64 1
                    guislider _VCE_clrR 0 1000 [if (&& (_VCE_LiveSet) (= $connectedip 0)) [_VCE_Set]]
                    guistrut 0.5
                    guislider _VCE_clrG 0 1000 [if (&& (_VCE_LiveSet) (= $connectedip 0)) [_VCE_Set]]
                    guistrut 0.5
                    guislider _VCE_clrB 0 1000 [if (&& (_VCE_LiveSet) (= $connectedip 0)) [_VCE_Set]]
                ]
                guilist [ // V
                    guistrut 64 1
                ]
            ]
        ]
    ]
] 0 []


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  Map Model Gallery:
showmmgui = [ if (= $editing 1) [if (|| (cleargui) (cleartexgui)) [] [showgui "mapmodels"] ] ]
mmguiwidth  = 7
mmguiheight = 4
mmguiname   = 1
newgui "mapmodels" [
    if (|| (= $editing 0) (= $isconnected 0)) [cleargui 1] [
        guikeeptab 1
        _MMG_ModelsPerTab = (* $mmguiwidth $mmguiheight)
        guinoautotab [
            loop _iTab (div (+ $nummapmodels (- $_MMG_ModelsPerTab 1)) $_MMG_ModelsPerTab) [
                if $_iTab [guitab (+ $_iTab 1)]
                if (= $guitabnum (+ $_iTab 1)) [
                    loop _iY $mmguiheight [
                        guilist [
                            loop _iX $mmguiwidth [
                                _iMdl = (+ (* $_iTab $_MMG_ModelsPerTab) (+ (* $_iY $mmguiwidth) $_iX))
                                guimodelpreview (mapmodelname $_iMdl) "mapmodel" [newent mapmodel @_iMdl] 3 1 $_iMdl 1
                            ]
                        ]
                    ]
                    if (!= $mmguiname 0) [
                        guistrut 0.2
                        guititle (concatword "^f7" (strreplace (strreplace $guirolloverpreviewname "\" "/") "/" "^f~/^f7"))
                    ]
                ]
            ]
        ]
    ]
]

//getmapmodels = [
//    loopconcat i $nummapmodels [
//        if $arg2 [ if (mapmodelloaded $i) [ $arg1 $i ] ] [ $arg1 $i ]
//    ]
//]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  Entity Manager:
entman = [if (= $editing 1) [ if (|| (cleargui) (cleartexgui)) [] [_EM_GenEntList; showgui "entman"] ]]
_EM_EntsPerColumn = 32
_EM_ColumnsPerTab =  2
_EM_EntsPerTab    = -1
_EM_NumOf_Columns = -1
_EM_NumOf_Tabs    = -1
_EM_Width         = -1
_EM_Height        = -1
_EM_Recalc = [
    _EM_EntsPerTab    = (* $_EM_EntsPerColumn $_EM_ColumnsPerTab)
    _EM_NumOf_Columns = (+ (ceil (divf (+ $_EM_NumOf_Ents 1) $_EM_EntsPerColumn)) 0) //  (+ n 0) <-- Float to Int hack.
    _EM_NumOf_Tabs    = (+ (ceil (divf    $_EM_NumOf_Columns $_EM_ColumnsPerTab)) 0) //  Why do round,floor,ceil return floats?
    _EM_Width         = (* $_EM_ColumnsPerTab 48)
    _EM_Height        =    $_EM_EntsPerColumn
]
_EM_EntList_Type  = ""
_EM_EntList_Attr0 = ""
_EM_EntList_Attr1 = ""
_EM_EntList_Attr2 = ""
_EM_EntList_Attr3 = ""
_EM_EntList_Attr4 = ""
_EM_NumOf_Ents   = -1
_EM_GenEntList = [
    _EM_EntList_Type  = ""
    _EM_EntList_Attr0 = ""
    _EM_EntList_Attr1 = ""
    _EM_EntList_Attr2 = ""
    _EM_EntList_Attr3 = ""
    _EM_EntList_Attr4 = ""
    _EM_NumOf_Ents = -1
    cancelsel; entfind *
    entloop [
        _EM_EntList_Type  = (concat $_EM_EntList_Type  (enttype  ))
        _EM_EntList_Attr0 = (concat $_EM_EntList_Attr0 (at $entget 1)) //(entattr 0))
        _EM_EntList_Attr1 = (concat $_EM_EntList_Attr1 (at $entget 2)) //(entattr 1))
        _EM_EntList_Attr2 = (concat $_EM_EntList_Attr2 (at $entget 3)) //(entattr 2))
        _EM_EntList_Attr3 = (concat $_EM_EntList_Attr3 (at $entget 4)) //(entattr 3))
        _EM_EntList_Attr4 = (concat $_EM_EntList_Attr4 (at $entget 5)) //(entattr 4))
    ]
    _EM_NumOf_Ents = (- $numoctaents 1)
    cancelsel
    _EM_Recalc
]
newgui "entman" [
    if (|| (= $editing 0) (= $isconnected 0)) [cleargui 1] [
        if (!= $_EM_NumOf_Ents (- $numoctaents 1)) [_EM_GenEntList]
        guistayopen [
            //#####################################################################################################################################
            guilist [ // H
                guistrut $_EM_Height 1
                guilist [ // V
                    guistrut $_EM_Width 1
                    guititle "Entity Manager"
                    guibar
                    guialign 0 [ // H
                        guilist [ // V
                            guistrut 1
                            guitext (concatword " NumOf Entities: " (_EM_NumOf_Ents)) 0
                            guitext " ^f4(Last index, count is +1.)" 0
                            guistrut 1
                            guibar
                            guistrut 1
                            guilist [
                                guitext (format " EntsPerColumn:^t%1^t" (_EM_EntsPerColumn)) 0
                                guibutton "" [_EM_EntsPerColumn = (+ $_EM_EntsPerColumn -1); if (< $_EM_EntsPerColumn 16) [_EM_EntsPerColumn = 16]; _EM_Recalc] "val_dec"
                                guibutton "" [_EM_EntsPerColumn = (+ $_EM_EntsPerColumn  1); if (> $_EM_EntsPerColumn 64) [_EM_EntsPerColumn = 64]; _EM_Recalc] "val_inc"
                            ]
                            guilist [
                                guitext (format " ColumnsPerTab: ^t%1^t" (_EM_ColumnsPerTab)) 0
                                guibutton "" [_EM_ColumnsPerTab = (+ $_EM_ColumnsPerTab -1); if (< $_EM_ColumnsPerTab 1) [_EM_ColumnsPerTab = 1]; _EM_Recalc] "val_dec"
                                guibutton "" [_EM_ColumnsPerTab = (+ $_EM_ColumnsPerTab  1); if (> $_EM_ColumnsPerTab 8) [_EM_ColumnsPerTab = 8]; _EM_Recalc] "val_inc"
                            ]
                            guistrut 1
                            guitext (concatword " NumOf Columns: ^t" (_EM_NumOf_Columns)) 0
                            guitext (concatword " NumOf Tabs:    ^t" (_EM_NumOf_Tabs   )) 0
                            guistrut 1
                        ]
                        //guibar
                    ]
                ]
            ]
            //#####################################################################################################################################
            loop _iTab $_EM_NumOf_Tabs [
                guitab (* $_iTab $_EM_EntsPerTab)

//@@    figure out why framerate is worse only on later Tabs...
//@@        framerate is unaffected by Tab selection in TexSlotMan
//@@        perhaps list access is the culprit ?
//@@            would be nice to access entity data directly via command+index rather than only via "entloop"

//guitext $_iTab
//guitext (- $guitabnum 2)

                guilist [ // H
                    //=====================================================================================================
                    // Is this the active Tab? If so, populate.
                    if (= $_iTab (- $guitabnum 2)) [
                        _iMdl = (* $_iTab $_EM_EntsPerTab)
                        _iCol = 1
                        while [<= $_iCol $_EM_ColumnsPerTab] [
                            guilist [ // V
                                _iEND = (+ (* $_iTab $_EM_EntsPerTab) (- (* $_iCol $_EM_EntsPerColumn) 1))
                                while [<= $_iMdl $_iEND] [
                                    if (> $_iMdl $_EM_NumOf_Ents) [
                                        _iMdl = (+ $_iEND 1) // exitloop/break
                                    ][
                                        guifreerow  7  1 [ guitext (concatword "^f7[^f2" (_iMdl) "^f7]") 0
                                                 ] 11  1 [ guitext (concatword "^f7" (at $_EM_EntList_Type  $_iMdl)) 0
                                                 ]  6  1 [ guitext (concatword "^f7" (at $_EM_EntList_Attr0 $_iMdl)) 0
                                                 ]  6  1 [ guitext (concatword "^f7" (at $_EM_EntList_Attr1 $_iMdl)) 0
                                                 ]  6  1 [ guitext (concatword "^f7" (at $_EM_EntList_Attr2 $_iMdl)) 0
                                                 ]  6  1 [ guitext (concatword "^f7" (at $_EM_EntList_Attr3 $_iMdl)) 0
                                                 ]  6  1 [ guitext (concatword "^f7" (at $_EM_EntList_Attr4 $_iMdl)) 0
                                        ]
                                        _iMdl = (+ $_iMdl 1)
                                    ]
                                ]
                                _iCol = (+ $_iCol 1)
                            ]
                        ]
                    ]
                    //=====================================================================================================
                ]
            ]
            //#####################################################################################################################################
        ]
    ]
] "Info"


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  Texture Slot Manager:
texslotman = [ if $editing [if (|| (cleargui) (cleartexgui)) [] [showgui "texslotman"] ]]
_TSM_SlotsPerColumn = 36
_TSM_ColumnsPerTab  =  2
_TSM_SlotsPerTab    = -1
_TSM_NumOf_Columns  = -1
_TSM_NumOf_Tabs     = -1
_TSM_Width          = -1
_TSM_Height         = -1
_TSM_ColumnWidth    = 48
_TSM_SideBarWidth   = 50
_TSM_Recalc = [
    _TSM_SlotsPerTab   = (* $_TSM_SlotsPerColumn $_TSM_ColumnsPerTab)
    _TSM_NumOf_Columns = (+ (ceil (divf (+ $_TSM_NumOf_AllSlots 1) $_TSM_SlotsPerColumn)) 0) //  (+ n 0) <-- Float to Int hack.
    _TSM_NumOf_Tabs    = (+ (ceil (divf    $_TSM_NumOf_Columns     $_TSM_ColumnsPerTab )) 0) //  Why do round,floor,ceil return floats?
    _TSM_Width         = (+ (+ (* $_TSM_ColumnsPerTab $_TSM_ColumnWidth) $_TSM_SideBarWidth) 2) //<-- +2? to account for guibar.
    _TSM_Height        = $_TSM_SlotsPerColumn
]
_TSM_NumOf_tSlots   = -1
_TSM_NumOf_vSlots   = -1
_TSM_NumOf_AllSlots = -1
_TSM_SelectedSlot   = -1
_TSM_Update = [
    _TSM_NumOf_tSlots   = (- $numslots 1)
    _TSM_NumOf_vSlots   = (- $numvslots $numslots)
    _TSM_NumOf_AllSlots = (- $numvslots 1)
    _TSM_SelectedSlot   = -1
    _TSM_Recalc
]
newgui "texslotman" [
    if (|| (= $editing 0) (= $isconnected 0)) [cleargui 1] [
        if (!= $_TSM_NumOf_AllSlots (- $numvslots 1)) [_TSM_Update]
        guistayopen [
            //#####################################################################################################################################
            guilist [ // H
                guistrut $_TSM_Height 1
                guilist [ // V
                    guistrut $_TSM_Width 1
                    guititle "Texture Slot Manager"
                    guibar
                    guialign 0 [ // H
                        guilist [ // V
                            guistrut 1
                            guitext (concatword " NumOf tSlots:  ^t"  (_TSM_NumOf_tSlots  )) 0
                            guitext (concatword " NumOf vSlots:  ^t"  (_TSM_NumOf_vSlots  )) 0
                            guitext (concatword " Total TexSlots: ^t" (_TSM_NumOf_AllSlots)) 0
                            guitext " ^f4(Last index, count is +1.)" 0
                            guistrut 1
                            guibar
                            guistrut 1
                            guilist [ // H
                                guitext (format " SlotsPerColumn:^t%1^t"  (_TSM_SlotsPerColumn)) 0
                                guibutton "" [_TSM_SlotsPerColumn = (+ $_TSM_SlotsPerColumn -1); if (< $_TSM_SlotsPerColumn 16) [_TSM_SlotsPerColumn = 16]; _TSM_Recalc] "val_dec"
                                guibutton "" [_TSM_SlotsPerColumn = (+ $_TSM_SlotsPerColumn  1); if (> $_TSM_SlotsPerColumn 64) [_TSM_SlotsPerColumn = 64]; _TSM_Recalc] "val_inc"
                            ]
                            guilist [ // H
                                guitext (format " ColumnsPerTab: ^t%1^t"  (_TSM_ColumnsPerTab)) 0
                                guibutton "" [_TSM_ColumnsPerTab = (+ $_TSM_ColumnsPerTab -1); if (< $_TSM_ColumnsPerTab 1) [_TSM_ColumnsPerTab = 1]; _TSM_Recalc] "val_dec"
                                guibutton "" [_TSM_ColumnsPerTab = (+ $_TSM_ColumnsPerTab  1); if (> $_TSM_ColumnsPerTab 8) [_TSM_ColumnsPerTab = 8]; _TSM_Recalc] "val_inc"
                            ]
                            guistrut 1
                            guitext (concatword " NumOf Columns: ^t" (_TSM_NumOf_Columns)) 0
                            guitext (concatword " NumOf Tabs:    ^t" (_TSM_NumOf_Tabs   )) 0
                            guistrut 1
                        ]
                        guibar
                        guilist [ // V
                            guistrut 1
                            guitext "   ^f7White^t = Texture is loaded." 0
                            guitext "   ^f4Grey^t = Texture is not loaded." 0
                            guitext "   ^f2Yellow^t = Texture is vSlot." 0
                            guistrut 1
                            guitext " A texture can be loaded and un-used." 0
                            guitext " Reload map if you would like to see if the map is using a texture." 0
                            guistrut 1
                            guibar
                            guistrut 1
                            guitext " This can remove unneeded vSlots." 0
                            guitext " This sets the texture of all hidden cube faces to slot:1 (default texture)." 0
                            guitext " Note: vSlots are optimized upon ^f2/savemap^f~." 0
                            guistrut 1
                            guilist [guistrut 3; guibutton " ^f2/fixinsidefaces" [fixinsidefaces]]
                            guistrut 1


//@@@@@@@@  edittex
//@@@@@@@@  getcurtex   "get current texture" appears to be last used texture
//@@@@@@@@  getreptex   "get replace texture" last replaced texture (/replace uses this)

//@@@@@@@@  getseltex
//@@@@@@@@  getslottex
//@@@@@@@@  gettex
//@@@@@@@@  gettexname

//@@@@@@@@  reloadtex
//@@@@@@@@
//@@@@@@@@  reloadtex
//@@@@@@@@  replacetex
//@@@@@@@@  replacetexsel
//@@@@@@@@
//@@@@@@@@  texcompress
//@@@@@@@@  texcompressquality
//@@@@@@@@
//@@@@@@@@  texloaded
//@@@@@@@@
//@@@@@@@@  texreduce
//@@@@@@@@
//@@@@@@@@  texalpha
//@@@@@@@@  texcolor
//@@@@@@@@  texlayer
//@@@@@@@@  texoffset
//@@@@@@@@  texrotate
//@@@@@@@@  texscale
//@@@@@@@@  texscroll
//@@@@@@@@  texsmooth


                        ]
                    ]
                ]
            ]
            //#####################################################################################################################################
            loop _iTab $_TSM_NumOf_Tabs [
                guitab (* $_iTab $_TSM_SlotsPerTab)
                guilist [ // H
                    guistrut $_TSM_Height 1
                    //=========================================================================================================
                    // Is this the active Tab? If so, populate.
                    if (= $_iTab (- $guitabnum 2)) [
                        _iTex = (* $_iTab $_TSM_SlotsPerTab)
                        _iCol = 1
                        while [<= $_iCol $_TSM_ColumnsPerTab] [
                            guilist [ // V
                                guistrut $_TSM_ColumnWidth 1
                                _iEND = (+ (* $_iTab $_TSM_SlotsPerTab) (- (* $_iCol $_TSM_SlotsPerColumn) 1))
                                while [<= $_iTex $_iEND] [
                                    if (> $_iTex $_TSM_NumOf_AllSlots) [
                                        _iTex = (+ $_iEND 1) // exitloop/break
                                    ][
                                        if (= (getslottex $_iTex) -1) [
                                            // vSlot:
                                            guibutton (concatword " ^f7[^f2" (_iTex) "^f7] " (? (= $_iTex $_TSM_SelectedSlot) "^f8" "^f2") (gettexname $_iTex)) (concat "_TSM_SelectedSlot =" $_iTex) 0
                                        ][
                                            if (texloaded $_iTex) [
                                                // tSlot Loaded:
                                                guibutton (concatword " ^f7[" (_iTex) "] " (? (= $_iTex $_TSM_SelectedSlot) "^f8" "^f7") (gettexname $_iTex)) (concat "_TSM_SelectedSlot =" $_iTex) 0
                                            ][
                                                // tSlot Not Loaded:
                                                guibutton (concatword " ^f7[" (_iTex) "] " (? (= $_iTex $_TSM_SelectedSlot) "^f8" "^f4") (gettexname $_iTex)) (concat "_TSM_SelectedSlot =" $_iTex) 0
                                            ]
                                        ]
                                        _iTex = (+ $_iTex 1)
                                    ]
                                ]
                                _iCol = (+ $_iCol 1)
                            ]
                        ]
                        guibar //----------------------------------------------------------------------------------------------
                        guilist [
                            guistrut $_TSM_SideBarWidth 1
                            guialign 0 [ guiimage (concatword "packages/" (gettexname $_TSM_SelectedSlot)) 0 7 0 "data/cube.png" ] // <thumbnail:256>  <-- Breaks non-square textures.
                            if        (!= $_TSM_SelectedSlot -1) [guitext (concatword " ^f~[^f2" $_TSM_SelectedSlot "^f~,^f20^f~] ^f2^"" (gettexname $_TSM_SelectedSlot 0) "^"" ) 0][guitext " ^f~[^f2~^f~,^f2~^f~] ^f2^"~^"" 0]
                            if (gettexname $_TSM_SelectedSlot 1) [guitext (concatword " ^f~[^f2" $_TSM_SelectedSlot "^f~,^f21^f~] ^f2^"" (gettexname $_TSM_SelectedSlot 1) "^"" ) 0]
                            if (gettexname $_TSM_SelectedSlot 2) [guitext (concatword " ^f~[^f2" $_TSM_SelectedSlot "^f~,^f22^f~] ^f2^"" (gettexname $_TSM_SelectedSlot 2) "^"" ) 0]
                            if (gettexname $_TSM_SelectedSlot 3) [guitext (concatword " ^f~[^f2" $_TSM_SelectedSlot "^f~,^f23^f~] ^f2^"" (gettexname $_TSM_SelectedSlot 3) "^"" ) 0]
                            if (gettexname $_TSM_SelectedSlot 4) [guitext (concatword " ^f~[^f2" $_TSM_SelectedSlot "^f~,^f24^f~] ^f2^"" (gettexname $_TSM_SelectedSlot 4) "^"" ) 0]
                            if (gettexname $_TSM_SelectedSlot 5) [guitext (concatword " ^f~[^f2" $_TSM_SelectedSlot "^f~,^f25^f~] ^f2^"" (gettexname $_TSM_SelectedSlot 5) "^"" ) 0]
                            if (gettexname $_TSM_SelectedSlot 6) [guitext (concatword " ^f~[^f2" $_TSM_SelectedSlot "^f~,^f26^f~] ^f2^"" (gettexname $_TSM_SelectedSlot 6) "^"" ) 0]
                            if (gettexname $_TSM_SelectedSlot 7) [guitext (concatword " ^f~[^f2" $_TSM_SelectedSlot "^f~,^f27^f~] ^f2^"" (gettexname $_TSM_SelectedSlot 7) "^"" ) 0]
                            guibar
                            guifreerow 12  1 [ guitext "color:" 0
                                     ]  8  1 [ guitext (concatword "^f3" (at (getvcolor $_TSM_SelectedSlot) 0)) 0
                                     ]  8  1 [ guitext (concatword "^f0" (at (getvcolor $_TSM_SelectedSlot) 1)) 0
                                     ]  8  1 [ guitext (concatword "^f1" (at (getvcolor $_TSM_SelectedSlot) 2)) 0
                            ]
                            guistrut 0.09375
                            guifreerow 12  1 [ guitext "alpha:" 0
                                     ]  8  1 [ guitext (concatword "^f5" (at (getvalpha $_TSM_SelectedSlot) 0)) 0
                                     ]  8  1 [ guitext (concatword "^f5" (at (getvalpha $_TSM_SelectedSlot) 1)) 0
                            ]
                            guistrut 0.09375
                            guifreerow 12  1 [ guitext "rotate:" 0
                                     ]  8  1 [ guitext (concatword "^f2" (getvrotate $_TSM_SelectedSlot)) 0
                                     ]  1 -1 [ guitext (concatword " ^f7^"" (at $_Labels_TexRotate (getvrotate $_TSM_SelectedSlot)) "^"") 0
                            ]
                            guistrut 0.09375
                            guifreerow 12  1 [ guitext "offset:" 0
                                     ]  8  1 [ guitext (concatword "^f3" (at (getvoffset $_TSM_SelectedSlot) 0)) 0
                                     ]  8  1 [ guitext (concatword "^f0" (at (getvoffset $_TSM_SelectedSlot) 1)) 0
                            ]
                            guistrut 0.09375
                            guifreerow 12  1 [ guitext "scale:" 0
                                     ]  8  1 [ guitext (concatword "^f2" (getvscale  $_TSM_SelectedSlot)) 0
                            ]
                            guistrut 0.09375
                            guifreerow 12  1 [ guitext "scroll:" 0
                                     ]  8  1 [ guitext (concatword "^f3" (at (getvscroll $_TSM_SelectedSlot) 0)) 0
                                     ]  8  1 [ guitext (concatword "^f0" (at (getvscroll $_TSM_SelectedSlot) 1)) 0
                            ]
                            guistrut 0.09375
                            guifreerow 12  1 [ guitext "vshaderparam:" 0
                                     ]  1 -1 [ guitext (concatword " ^f2^"" (getvshaderparamnames $_TSM_SelectedSlot) "^" " (getvshaderparam $_TSM_SelectedSlot (getvshaderparamnames $_TSM_SelectedSlot)) ) 0
                            ]
                            guibar
                            if (!= (getvlayer $_TSM_SelectedSlot) 0) [
                                guialign 0 [guiimage (concatword "packages/" (gettexname (getvlayer $_TSM_SelectedSlot)) ) () 3 0 "data/cube.png"] // <thumbnail:256>  <-- Breaks non-square textures.
                                guitext (format " ^f~layer: ^f~[^f6%1^f~] ^f6^"%2^""  (getvlayer  $_TSM_SelectedSlot) (gettexname (getvlayer $_TSM_SelectedSlot)) ) 0

                                //@@  Show params for layer!

                            ][
                                guitext " ^f~layer: ^f~[^f6~^f~] ^f6^"none^"" 0
                            ]
                        ]
                    ]
                    //=========================================================================================================
                ]
            ]
            //#####################################################################################################################################
        ]
    ]
] "Info"


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  Miscellaneous:

testfont = [cleargui; showgui "testfont"]
newgui "testfont" [
    guinoautotab [
        guitext " A B C D E F G H          " 0
        guitext " I J K L M N O P          " 0
        guitext " Q R S T U V W X          " 0
        guitext " Y Z 0 1 2 3 4              " 0
        guitext " a b c d e f g h          " 0
        guitext " i j k l m n o p          " 0
        guitext " q r s t u v w x          " 0
        guitext " y z 5 6 7 8 9         ` ~ ^^ " 0
        guistrut 1
        guitext "                  " 0
        guitext "                  " 0
        guitext "                  " 0
        guitext "                  " 0
        guitext "                  " 0
        guitext "                    " 0
        guitext " , . ; : ! ? ' ^"   @ # $  % &   " 0
        guitext " < > ( ) [ ] { }  + - * = / \ _ | " 0
    guitab "2"
        guitext " ABCDEFGH   " 0
        guitext " IJKLMNOP   " 0
        guitext " QRSTUVWX   " 0
        guitext " YZ01234      " 0
        guitext " abcdefgh   " 0
        guitext " ijklmnop   " 0
        guitext " qrstuvwx   " 0
        guitext " yz56789   `~^^ " 0
        guistrut 1
        guitext "    " 0
        guitext "    " 0
        guitext "    " 0
        guitext "    " 0
        guitext "    " 0
        guitext "      " 0
        guitext " ,.;:!?'^"  @#$%&  " 0
        guitext " <>()[]{}  +-*=/\_| " 0
    guitab "3"
        guitext "^tThe quick brown fox jumps over the lazy dog." 0
        guistrut 1
        guitext "^tLorem ipsum dolor sit amet, consectetur adipiscing elit." 0
        guitext "Phasellus malesuada urna nec lectus scelerisque vehicula." 0
        guitext "In id lorem nec dui fringilla vestibulum. Integer" 0
        guitext "scelerisque consequat nibh. Integer feugiat tortor eget" 0
        guitext "tortor posuere, et consequat sem venenatis. Etiam mi purus," 0
        guitext "iaculis vel orci vehicula, efficitur facilisis sem. Nunc" 0
        guitext "bibendum nisi vel dignissim efficitur. Nulla in libero eget" 0
        guitext "neque scelerisque interdum. Nullam cursus metus sed libero" 0
        guitext "tincidunt aliquam. Integer aliquet felis elit, ut viverra" 0
        guitext "nibh mollis ut. Pellentesque congue, urna a lobortis" 0
        guitext "porttitor, ipsum justo pulvinar lorem, non vehicula magna" 0
        guitext "arcu in tellus. Fusce id odio velit. Nullam et massa dui." 0
        guitext "Suspendisse potenti. Aenean laoreet justo id aliquet tempus." 0
    //guitab "~"
    //    guitext "-|-----------------|-|-----------------|-" 0
    //    guitext "0| A B C D E F G H |0|         |0" 0
    //    guitext "1| I J K L M N O P |1|         |1" 0
    //    guitext "2| Q R S T U V W X |2|         |2" 0
    //    guitext "3| Y Z 0 1 2 3 4   |3|           |3" 0
    //    guitext "4| a b c d e f g h |4|         |4" 0
    //    guitext "5| i j k l m n o p |5|         |5" 0
    //    guitext "6| q r s t u v w x |6|         |6" 0
    //    guitext "7| y z 5 6 7 8 9   |7|      ` ~ ^^ |7" 0
    //    guitext "-|-----------------|-|-----------------|-" 0
    //    guitext "0|         |0|         |0" 0
    //    guitext "1|         |1|         |1" 0
    //    guitext "2|         |2|         |2" 0
    //    guitext "3|         |3|         |3" 0
    //    guitext "4|         |4|         |4" 0
    //    guitext "5|           |5|         |5" 0
    //    guitext "6| , . ; : ! ? ' ^" |6|  @ # $  % &   |6" 0
    //    guitext "7| < > ( ) [ ] { } |7| + - * = / \ _ | |7" 0
    //    guitext "-|-----------------|-|-----------------|-" 0
    ]
]

