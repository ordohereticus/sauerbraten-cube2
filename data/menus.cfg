///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  Standard menu definitions:
//  (Don't modify, add personal menus to 'autoexec.cfg' instead.)
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
exec "data/maplists.cfg"
exec "data/menus_utility.cfg"
exec "data/menus_texteditor.cfg"

exec "data/menus_editing.cfg"
exec "data/menus_managers.cfg"
exec "data/menus_master.cfg"
exec "data/menus_options.cfg"


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
togglemainmenu = [ || (cleartexgui) [cleargui 1] [showgui "main"] ]


//  GAME:
adjustammobar     = [showgui "adjustammobar"]
adjustgameclock   = [showgui "adjustgameclock"]
adjusthudscore    = [showgui "adjusthudscore"]
adjusthud         = [showgui "adjusthud"]; TODO 0 "menus.cfg -- Create AdjustHUD gui."
chooseplayermodel = [guirolloveraction = (PM_button $playermodel); showgui "playermodel"]
master            = [showgui "master"]
showcustommaps    = [showgui "custommaps"]


//  EDIT:
adjust_fog            = [if (|| [= $mode 1] [! (ingamemp)] ) [showgui "TODO"           ]]
adjust_lighting       = [if (|| [= $mode 1] [! (ingamemp)] ) [showgui "adjust_lighting"]]
adjust_materials      = [if (|| [= $mode 1] [! (ingamemp)] ) [showgui "TODO"           ]]
adjust_sky_atmo       = [if (|| [= $mode 1] [! (ingamemp)] ) [showgui "TODO"           ]]
adjust_sky_cloudbox   = [if (|| [= $mode 1] [! (ingamemp)] ) [showgui "TODO"           ]]
adjust_sky_cloudlayer = [if (|| [= $mode 1] [! (ingamemp)] ) [showgui "TODO"           ]]
adjust_sky_fogdome    = [if (|| [= $mode 1] [! (ingamemp)] ) [showgui "TODO"           ]]
adjust_sky_skybox     = [if (|| [= $mode 1] [! (ingamemp)] ) [showgui "TODO"           ]]

toggleentgui    = [if $editing [if (|| [cleargui] [cleartexgui]) [] [showentgui              ]]]; TODO 0 "menus.cfg -- Rework 'entgui' stuff..."
togglemmgui     = [if $editing [if (|| [cleargui] [cleartexgui]) [] [showgui "mapmodels"     ]]]
toggletexgui    = [if $editing [if (|| [cleargui] [cleartexgui]) [] [showtexgui              ]]]
toggleentman    = [if $editing [if (|| [cleargui] [cleartexgui]) [] [showgui "entitymanager" ]]]
toggletexman    = [if $editing [if (|| [cleargui] [cleartexgui]) [] [showgui "texslotmanager"]]]

vcolorgui       = [if $editing [if (|| [cleargui] [cleartexgui]) [] [showgui "vcolor"]]]
vshaderparamgui = [if $editing [if (|| [cleargui] [cleartexgui]) [] [showgui "vshaderparam"]]]


//  MISC:
notepad     = [if (> $numargs 0) [notepadfile = $arg1]; showgui "notepad"]
pastebuffer = [showgui "pastebuffer"]
scratchpad  = [showgui "scratchpad"]
pakman      = [showgui "packagemanager"]
postfx      = [showgui "postfx"]


//  TEST:
guitest_font    = [showgui "testfont"]
guitest_padding = [showgui "testpadding"]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
nodebug [
    defvarp menu_showbotmatch      0 1 1
    defvarp menu_showcampaign      0 1 1

    defvarp menu_shownewmap        0 0 1
    defvarp menu_showloadmap       0 1 1
    defvarp menu_showloadcustommap 0 1 1
]


newgui "main" [
    guilist [
        guiimage (concatword "packages/icons/" (playermodelicon) ".jpg") [chooseplayermodel] 1.15
        guistrut 0.25
        guilist [
            MyName = $getname
            guifield MyName 15 [name $MyName]
            guispring
            guilist [
                guibutton (playermodelname) [chooseplayermodel] 0
                guistrut 1
                guiimage (getcrosshair) [showgui "crosshair"] 0.5
            ]
        ]
    ]
    guibar
    guibutton "server browser"              [showgui "serverbrowser"]
    //guibutton "lanconnect"
    //guibutton "connect localhost"
    guibar
    if (ingame) [
        if (|| $editing (= $getmode 1)) [
            guibutton "editing"             [showgui "editing"]
            guistrut 0.5
        ]
        if (m_teammode $getmode) [
            guibutton "switch team"         [switchteam]
        ]
        if (ingamemp) [
            guibutton "toggle spectator"    [togglespectator]           "spectator"
            guibutton "master"              [showgui "master"]
        ]
        if (|| (m_teammode $getmode) (ingamemp)) [
            guistrut 0.5
        ]
    ][
        guibutton "bot match"               [showgui "botmatch"]
        guibutton "campaign"                [showgui "campaign"]
        guistrut 0.5
    ]
  //guibutton "new map"                     [newmap]
    guibutton "load map"                    [showgui "maps"]
    guibutton "load custom map"             [showgui "custommaps"]
    guibar
    if (ingame) [
        guibutton "options"                 [showgui "options"]
        guibutton "disconnect"              [disconnect]                "exit"
    ][
        guibutton "options"                 [showgui "options"]
        guibutton "about"                   [showgui "about"]
        guibutton "quit"                    [quit]                      "exit"
    ]
] 0


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
nodebug [
    defvar playermodelcount 1 5 -1
]

macro PM_button [cleargui 1; playermodel %1]

newgui "playermodel" [
    guilist [
        guilist [
            loop iMdl $playermodelcount [
                guibutton (playermodelname $iMdl) (PM_button $iMdl) (playermodelicon $iMdl)
            ]
        ]
        guibar
        iMdl = (substr $guirolloveraction (strlen (PM_button "")))
        iMdl = (max 0 (min (- $playermodelcount 1) $iMdl))
        guilist [
            guilist [
                guispring
                guiplayerpreview $iMdl 0 6 (checkrolloveraction "playermodel ") 4 1 (playermodelname $iMdl)
                guispring
            ]
            guitextbox (playermodelstory $iMdl) 40 9
        ]
    ]
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
newgui "serverbrowser" [
    guistayopen [
        guiservers [
            guialign 0 [
                guibutton   "update from master server"  updatefrommaster
                guistrut 4
                guicheckbox (concatword (? $searchlan         "" "^f4") "search LAN" )  searchlan
                guistrut 4
                guicheckbox (concatword (? $autoupdateservers "" "^f4") "auto-update")  autoupdateservers
                guistrut 4
                if (!= $autosortservers 0) [
                    guicheckbox "auto-sort"     autosortservers
                ][
                    guicheckbox "^f4auto-sort"  autosortservers
                    guistrut 4
                    guibutton "sort"            sortservers
                ]
            ]
            guibar
        ] 17
    ]
] "servers" [initservers]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
newgui "maps" [
    guikeeptab 1
    ML_CurrentTab 0 //<-- This is gross.
    if (= $ML_ModeMaps 0) [
        guimaplist "ffa"     $MapList_FFA     15 17 3 1
        guimaplist "capture" $MapList_Capture 15 17 3 0
        guimaplist "ctf"     $MapList_CTF     15 17 3 0
        guimaplist "concept" $MapList_Concept 15 17 3 0
    ][
        if (= $ML_ModeMaps 1) [
            guimaplist "capture" $MapList_Capture 15 17 3 1
        ][
            guimaplist "ctf"     $MapList_CTF     15 17 3 1
        ]
    ]
] "maps" [ML_MapName = ""]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
MapList_Custom = ""

newgui "custommaps" [
    guikeeptab 1
    ML_CurrentTab 0 //<-- This is gross.
    ListLen = (listlen $MapList_Custom)
    guimaplist "custom" $MapList_Custom 15 17 (? (<= $ListLen 34) (? (<= $ListLen 17) [1][2])[3]) 1
] "custom maps" [ML_MapName = ""; ML_GenMapList_Custom]


ML_GenMapList_Custom = [
    MapList_Custom = ""
    loopfiles iFile "packages/base" "ogz" [
        if (< (indexof $MapList_ALL $iFile) 0) [
            MapList_Custom = (concat $MapList_Custom (escape $iFile))
        ]
    ]
    MapList_Custom = (sortlist $MapList_Custom iA iB [naturalsort $iA $iB])
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
nodebug [
    defvar BM_BotCount  1   5  32
    defvar BM_MinSkill 50  50 101
    defvar BM_MaxSkill 50 100 101
]

BM_StartMatch = [
    if (ingamemp) [
        echo "You must disconnect from the current multiplayer game before starting a bot match."
    ] [
        loop i $BM_BotCount [addbot (rnd (+ $BM_MaxSkill 1) $BM_MinSkill)]
    ]
]
newgui "botmatch" [
    guitext "bot amount"
    guislider BM_BotCount 0 32
    guitext "bot minimum skill"
    guislider BM_MinSkill 50 101 [if (< $BM_MaxSkill $BM_MinSkill) [BM_MaxSkill = $BM_MinSkill]]
    guitext "bot maximum skill"
    guislider BM_MaxSkill 50 101 [if (> $BM_MinSkill $BM_MaxSkill) [BM_MinSkill = $BM_MaxSkill]]
    guibutton "start match.." [guionclear [BM_StartMatch]; showgui "maps"]
] "bot match"


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
newgui "campaign" [
    guibutton   "start Private Stan Sauer"  [showgui "privatestansauer"]
    guibutton   "start An Army Of One"      [showgui "armyofone"]
    guibutton   "start Lost"                [sp lost]                       "cube"
    guibutton   "start Meltdown"            [sp skrsp1]                     "cube"
    guibutton   "start Missile Pass"        [sp crnsp1]                     "cube"
    guibutton   "start Level 9"             [sp level9]                     "cube"
    guibar
    guibutton   "start DMSP map.."          [mode -2; showgui "maps"]
    guicheckbox "slow motion"               [slowmosp]
    guitext     "skill (default: 3)"
    guislider skill
]


newgui "armyofone" [
    guilist [
        guilist [
            guibutton "Part I"   [sp mpsp6a] "cube"
            guibutton "Part II"  [sp mpsp6b] "cube"
            guibutton "Part III" [sp mpsp6c] "cube"
        ]
        _showmapshot (substr $guirolloveraction 3)
    ]
] "An Army Of One"


newgui "privatestansauer" [
    guilist [
        guilist [
            guibutton "Run N' Gun Part I"        [sp mpsp9a] "cube"
            guibutton "Run N' Gun Part II"       [sp mpsp9b] "cube"
            guibutton "Run N' Gun Part III"      [sp mpsp9c] "cube"
            guibutton "THE SERIOUSLY BIG VALLEY" [sp mpsp10] "cube"
        ]
        _showmapshot (substr $guirolloveraction 3)
    ]
] "Private Stan Sauer"


_showmapshot = [
    guibar
    ML_MapName = (checkrolloveraction "map " [if (> $numargs 0) [result $arg1] [at $guirollovername 0]])
    guilist [
        guiimage (concatword "packages/base/" $ML_MapName ".jpg") (checkrolloveraction "map ") 4 1 "data/cube.png" $ML_MapName
    ]
]
TODO 0 "menus.cfg -- Factor out showmapshot."


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
newgui "about" [
    guititle "Cube 2: Sauerbraten"
    guialign 0 [guitext "^f4by" 0]
    guialign 0 [guitext "^f7Wouter ^"^f2Aardappel^f7^" van Oortmerssen" 0]
    guialign 0 [guitext "^f7Lee ^"^f2eihrul^f7^" Salzman" 0]
    guialign 0 [guitext "^f7Mike ^"^f2Gilt^f7^" Dysart" 0]
    guialign 0 [guitext "^f7Robert ^"^f2baby-rabbit^f7^" Pointon" 0]
    guialign 0 [guitext "^f7John ^"^f2geartrooper^f7^" Siar" 0]
    guialign 0 [guitext "^f7Quinton ^"^f2Quin^f7^" Reeves" 0]
    guialign 0 [guitext "^f7& others" 0]
    guistrut 0.5
    guialign 0 [guitext "^f7For a full list of contributors see the ^f2readme^f7." 0]
    guistrut 0.5
    guialign 0 [guitext "^f2http://sauerbraten.org/" 0]
]

