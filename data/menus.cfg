///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  Standard menu definitions:
//  (Don't modify, add personal menus to 'autoexec.cfg' instead.)
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
reui = [
    clearconsole
    exec "data/menus.cfg"
    echo "^f2*** Menus Reinitialized ***  ^f6This is for testing; it will break stuff; ^f3delete ^"config.cfg^" after use."
]

exec "data/maplists.cfg"


//#############################################################################################################################################
//  These are hardcoded aliases that are updated upon mouse rollover:
guirollovername          = ""
guirolloveraction        = ""
guirolloverimgpath       = ""   // Are these ever used?
guirolloverimgaction     = ""   // Are these ever used?
guirolloverpreviewname   = ""
guirolloverpreviewaction = ""   // Are these ever used?
checkrolloveraction = [
    if (>= (strstr $guirolloveraction $arg1) 0) [
        if (>= $numargs 2) [arg2] [result $guirolloveraction]
    ]
]


//#############################################################################################################################################
togglemainmenu = [ || (cleartexgui) [cleargui 1] [showgui "main"] ]


// GAME:
adjustammobar     = [showgui "ammobar"]
adjustgameclock   = [showgui "gameclock"]
adjusthudscore    = [showgui "hudscore"]
adjusthud         = [showgui "adjusthud"]; TODO 0 "menus.cfg -- Create AdjustHUD gui."
chooseplayermodel = [guirolloveraction = (PM_button $playermodel); showgui "playermodel"]
master            = [showgui "master"]
showcustommaps    = [showgui "custommaps"]


// EDIT:
adjust_fog            = [if (|| (= $mode 1) (! (ingamemp)) ) [showgui "TODO"           ]]
adjust_lighting       = [if (|| (= $mode 1) (! (ingamemp)) ) [showgui "adjust_lighting"]]
adjust_sky_atmo       = [if (|| (= $mode 1) (! (ingamemp)) ) [showgui "TODO"           ]]
adjust_sky_cloudbox   = [if (|| (= $mode 1) (! (ingamemp)) ) [showgui "TODO"           ]]
adjust_sky_cloudlayer = [if (|| (= $mode 1) (! (ingamemp)) ) [showgui "TODO"           ]]
adjust_sky_fogdome    = [if (|| (= $mode 1) (! (ingamemp)) ) [showgui "TODO"           ]]
adjust_sky_skybox     = [if (|| (= $mode 1) (! (ingamemp)) ) [showgui "TODO"           ]]

toggleentgui = [if $editing [if (|| (cleargui) (cleartexgui)) [] [showentgui          ]]]; TODO 0 "menus.cfg -- Rework entgui stuff..."
togglemmgui  = [if $editing [if (|| (cleargui) (cleartexgui)) [] [showgui "mapmodels" ]]]
toggletexgui = [if $editing [if (|| (cleargui) (cleartexgui)) [] [showtexgui          ]]]
toggleentman = [if $editing [if (|| (cleargui) (cleartexgui)) [] [showgui "entman"    ]]]
toggletexman = [if $editing [if (|| (cleargui) (cleartexgui)) [] [showgui "texslotman"]]]

vcolorgui       = [if $editing [if (|| (cleargui) (cleartexgui)) [] [showgui "vcolor"]]]
vshaderparamgui = [if $editing [if (|| (cleargui) (cleartexgui)) [] [showgui "vshaderparam"]]]


// MISC:
notepad     = [if (> $numargs 0) [notepadfile = $arg1]; showgui "notepad"]
pastebuffer = [showgui "pastebuffer"]
scratchpad  = [showgui "scratchpad"]

postfx = [showgui "postfx"]


// TEST:
guitest_font    = [showgui "test_font"]
guitest_padding = [showgui "test_padding"]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
guilistsplit = [
    // ARGS 1: ...
    //      2: ...
    //      3: ...
    //      4: ...
    //      5: ...
    //      6: ...
    guilist [
        iZ = (div (+ (listlen $arg3) (- $arg2 1)) $arg2)
        loop iA $arg2 [
            guilist [
                if (>= $numargs 6) [arg6]
                loopsublist $arg1 $arg3 (* $iA $iZ) $iZ [arg4]
            ]
            if (&& (>= $numargs 5) (< (+ $iA 1) $arg2)) [arg5]
        ]
    ]
]
TODO 0 "menus.cfg -- Figure out how 'guilistsplit' works..."


//#############################################################################################################################################
guifield2 = [
    // ARGS 1: "Label"
    //      2: "Label" Width
    //      3: Variable
    //      4: Variable-Field Width
    //      5: Variable-Decrement Step
    //      6: Variable-Increment Step
    guilist [// H
        guilist [// V
            guistrut @arg2 1
            guilist [guitext @arg1]
        ]
        guifield @arg3 @arg4
        guibutton "" [@arg3 (+fround $@arg3 @arg5)] "val_dec"
        guibutton "" [@arg3 (+fround $@arg3 @arg6)] "val_inc"
    ]
]


//#############################################################################################################################################
guifreerow = [ // "Free" in the sense that it is not a part of a table.
    // ARGS 1: ColumnWidth
    //      2: ColumnAlign          -2: NoAlign  -1: Left  0: Center  1: Right
    //      3: [ColumnContents]
    //      ** Repeat, up to 9 columns. **
    guilist [//H                V                                                      H
        if (>= $numargs  3) [guilist [guistrut $arg1  1; if (= $arg2  -2) [@arg3 ][guialign $arg2  [@arg3 ]] ]] //  Column 1
        if (>= $numargs  6) [guilist [guistrut $arg4  1; if (= $arg5  -2) [@arg6 ][guialign $arg5  [@arg6 ]] ]] //  Column 2
        if (>= $numargs  9) [guilist [guistrut $arg7  1; if (= $arg8  -2) [@arg9 ][guialign $arg8  [@arg9 ]] ]] //  Column 3
        if (>= $numargs 12) [guilist [guistrut $arg10 1; if (= $arg11 -2) [@arg12][guialign $arg11 [@arg12]] ]] //  Column 4
        if (>= $numargs 15) [guilist [guistrut $arg13 1; if (= $arg14 -2) [@arg15][guialign $arg14 [@arg15]] ]] //  Column 5
        if (>= $numargs 18) [guilist [guistrut $arg16 1; if (= $arg17 -2) [@arg18][guialign $arg17 [@arg18]] ]] //  Column 6
        if (>= $numargs 21) [guilist [guistrut $arg19 1; if (= $arg20 -2) [@arg21][guialign $arg20 [@arg21]] ]] //  Column 7
        if (>= $numargs 24) [guilist [guistrut $arg22 1; if (= $arg23 -2) [@arg24][guialign $arg23 [@arg24]] ]] //  Column 8
        if (>= $numargs 27) [guilist [guistrut $arg25 1; if (= $arg26 -2) [@arg27][guialign $arg26 [@arg27]] ]] //  Column 9
    ]
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
newgui "main" [
    guilist [
        guiimage (concatword "packages/icons/" (playermodelicon) ".jpg") [chooseplayermodel] 1.15
        guistrut 0.25
        guilist [
            MyName = $getname
            guifield MyName 15 [name $MyName]
            guispring
            guilist [
                guibutton (playermodelname) [chooseplayermodel] 0
                guistrut 1
                guiimage (getcrosshair) [showgui "crosshair"] 0.5
            ]
        ]
    ]
    guistrut 0.75 //guibar
    guibutton "server browser.."            [showgui "serverbrowser"]
    //guibutton "lanconnect"
    //guibutton "connect localhost"
    guistrut 0.75 //guibar
    if (ingame) [
        if (|| $editing (= $getmode 1)) [
            guibutton "editing.."           [showgui "editing"]
            guistrut 0.75 //guibar
        ]
        guibutton "switch team"             [switchteam]
        guibutton "toggle spectator"        [togglespectator]           "spectator"
        guibutton "master.."                [showgui "master"]
    ][
        guibutton "bot match.."             [showgui "botmatch"]
        guibutton "campaign.."              [showgui "campaign"]
    ]
    guistrut 0.75 //guibar
    guibutton "load map.."                  [showgui "maps"]
    guibutton "load custom map.."           [showgui "custommaps"]      "menu"
    guistrut 0.75 //guibar
    if (ingame) [
        guibutton "options.."               [showgui "options"]
        guibutton "disconnect"              [disconnect]                "exit"
    ][
        guibutton "options.."               [showgui "options"]
        guibutton "about.."                 [showgui "about"]
        guibutton "quit"                    [quit]                      "exit"
    ]
] 0


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
nodebug [
    defvar playermodelcount 1 5 -1
]

macro PM_button [cleargui 1; playermodel %1]

newgui "playermodel" [
    guilist [
        guilist [
            loop iMdl $playermodelcount [
                guibutton (playermodelname $iMdl) (PM_button $iMdl) (playermodelicon $iMdl)
            ]
        ]
        guibar
        iMdl = (substr $guirolloveraction (strlen (PM_button "")))
        iMdl = (max 0 (min (- $playermodelcount 1) $iMdl))
        guilist [
            guilist [
                guispring
                guiplayerpreview $iMdl 0 6 (checkrolloveraction "playermodel ") 4 1 (playermodelname $iMdl)
                guispring
            ]
            guitextbox (playermodelstory $iMdl) 40 9
        ]
    ]
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
newgui "serverbrowser" [
    guistayopen [
        guiservers [
            guialign 0 [
                guibutton   "update from master server" updatefrommaster
                guistrut 4
                guicheckbox "search LAN"                searchlan
                guistrut 4
                guicheckbox "auto-update"               autoupdateservers
                guistrut 4
                if (!= $autosortservers 0) [
                    guicheckbox "^f0auto-sort"          autosortservers
                ][
                    guicheckbox "^f3auto-sort"          autosortservers
                    guistrut 4
                    guibutton "sort"                    sortservers
                ]
            ]
            guibar
        ] 17
    ]
] "servers" [initservers]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
newgui "about" [
    guititle "Cube 2: Sauerbraten"
    guialign 0 [guitext "^f4by" 0]
    guialign 0 [guitext "^f7Wouter ^"^f2Aardappel^f7^" van Oortmerssen" 0]
    guialign 0 [guitext "^f7Lee ^"^f2eihrul^f7^" Salzman" 0]
    guialign 0 [guitext "^f7Mike ^"^f2Gilt^f7^" Dysart" 0]
    guialign 0 [guitext "^f7Robert ^"^f2baby-rabbit^f7^" Pointon" 0]
    guialign 0 [guitext "^f7John ^"^f2geartrooper^f7^" Siar" 0]
    guialign 0 [guitext "^f7Quinton ^"^f2Quin^f7^" Reeves" 0]
    guialign 0 [guitext "^f7& others" 0]
    guistrut 0.5
    guialign 0 [guitext "^f7For a full list of contributors see the ^f2readme^f7." 0]
    guistrut 0.5
    guialign 0 [guitext "^f2http://sauerbraten.org/" 0]
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
nodebug [
    defvar  SM_NumOfClnt  0 0 256
    defvar  SM_NumOfSpec  0 0 256
    defvar  SM_NumOfPlyr  0 0 256
    defvar  SM_NumOfBots  0 0 256
    defvar  SM_MasterMode 0 0   3

    defvar  SM_ClntIsMstr 0 0 1
    defvar  SM_ClntIsSpec 0 0 1

    defvarp SM__ShowSelf    0 1 1
    defvarp SM__ConfirmKick 0 1 1
]

TODO 0 "menus.cfg -- Master Menu..."

newgui "master" [
    guikeeptab 1

    //if (!=s $SM_ListClnt (listclients 0 0)) [SM_Update]    Also need to detect Spec/Master/Team changes.
    SM_Update

    guistayopen [
    guinoautotab [
        guialign 0 [guititle (concatword "^f7" $getservdesc)]
        guialign 0 [guitext (concatword $connectedip ":" $connectedport) 0]
        guibar //-------------------------------------------------------------------------
        guilist [//H
            SM_MasterMode $getmastermode
            guitext "MasterMode:" 0
            guispring
            guiradio "Open ^f20  "   SM_MasterMode 0 [mastermode 0]
            guiradio "Veto ^f21  "   SM_MasterMode 1 [mastermode 1]
            guiradio "Locked ^f22  " SM_MasterMode 2 [mastermode 2]
            guiradio "Private ^f23"  SM_MasterMode 3 [mastermode 3]
        ]
        guistrut 1
        guicheckbox "Show Self"    SM__ShowSelf
        guicheckbox "Confirm Kick" SM__ConfirmKick
        guistrut 1
        guibar
        guialign 0 [//H
            guilist [//V
                guistrut 16 1
                if (ismaster $getclientnum) [
                    guialign 0 [guibutton "^f0..." [] 0]
                ][
                    guialign 0 [guibutton "^f0Claim Master" [setmaster 1] 0]
                ]
                guititle "Master"
                looplist iClnt $SM_ListMstr [
                    guialign 0 [guitext (concatword "^f0" (getclientname $iClnt) ) 0]
                ]
            ]
            guilist [//V
                guistrut 16 1
                if (isauth $getclientnum) [
                    guialign 0 [guibutton "^f5..." [] 0]
                ][
                    guialign 0 [guibutton "^f5Claim Auth" [auth] 0]
                ]
                guititle "Auth"
                looplist iClnt $SM_ListAuth [
                    guialign 0 [guitext (concatword "^f5" (getclientname $iClnt) ) 0]
                ]
            ]
            guilist [//V
                guistrut 16 1
                if (isadmin $getclientnum) [
                    guialign 0 [guibutton "..." [] 0]
                ][
                    guialign 0 [guibutton "^f6Claim Admin" [echo "How is this done?"] 0]
                ]
                guititle "Admin"
                looplist iClnt $SM_ListAdmn [
                    guialign 0 [guitext (concatword "^f6" (getclientname $iClnt) ) 0]
                ]
            ]
        ]
        //guistrut 2
        //ICOMMAND(auth, "s", (char *desc), tryauth(desc));
        //ICOMMAND(sauth, "", (), if(servauth[0]) tryauth(servauth));      What is S Auth?
        //ICOMMAND(dauth, "s", (char *desc), if(desc[0]) tryauth(desc));   What is D Auth?
        //guitext (concatword "^f2getservauth^t= " $getservauth "   What is this?") 0
        //guistrut 0.5
        //guitext (concatword "^f2SM_ListSpec^t= " $SM_ListSpec) 0
        //guitext (concatword "^f2SM_ListPlyr^t= " $SM_ListPlyr) 0
        //guitext (concatword "^f2SM_ListBots^t= " $SM_ListBots) 0
        //guistrut 0.5
        //guitext (concatword "^f2SM_NumOfClnt^t= " $SM_NumOfClnt) 0
        //guitext (concatword "^f2SM_NumOfSpec^t= " $SM_NumOfSpec) 0
        //guitext (concatword "^f2SM_NumOfPlyr^t= " $SM_NumOfPlyr) 0
        //guitext (concatword "^f2SM_NumOfBots^t= " $SM_NumOfBots) 0

    guitab (concatword "Players [" $SM_NumOfPlyr "]")
        SM_GenList $SM_ListPlyr

    guitab (concatword "Spectators [" $SM_NumOfSpec "]")
        SM_GenList $SM_ListSpec

    //guitab (concatword "Bots [" $SM_NumOfBots "]")
    //    SM_GenList $SM_ListBots

    ]]
] "Server Master" [SM_Update]


//#############################################################################################################################################
SM_Update = [
    //echo (concatword "^f" (mod $getmillis 9) "LISTS UPDATED")

    SM_ListClnt  = (listclients 1 0)            // listclients(local, bots)

    SM_ListAdmn = (listfilter i $SM_ListClnt [isadmin  $i])
    SM_ListAuth = (listfilter i $SM_ListClnt [isauth   $i])
    SM_ListMstr = (listfilter i $SM_ListClnt [ismaster $i])

    SM_ListSpec = (listfilter i $SM_ListClnt [   isspectator $i ])
    SM_ListPlyr = (listfilter i $SM_ListClnt [! (isspectator $i)])
    SM_ListBots = (listfilter i $SM_ListClnt [          isai $i ])

    SM_NumOfClnt = (listlen $SM_ListClnt)
    SM_NumOfSpec = (listlen $SM_ListSpec)
    SM_NumOfPlyr = (listlen $SM_ListPlyr)
    SM_NumOfBots = (listlen $SM_ListBots)
]


//#############################################################################################################################################
SM_GenList = [
    // ARGS  1: [LIST]  List of client numbers.

    guilist [// H
        //=====================================================================================================================
        //  CLIENT NUMBER:
        guilist [// V
            looplist iClnt $arg1 [
                guialign 1 [ guitext (concatword "[" $iClnt "]") 0 ]
            ]
        ]
        //=====================================================================================================================
        //  NAME:
        guistrut 1
        guilist [// V
            guistrut 16 1
            looplist iClnt $arg1 [
                guitext (concatword (? (isadmin $iClnt) "^f6" (? (isauth $iClnt) "^f5" (? (ismaster $iClnt) "^f0"))) (getclientname $iClnt)) (getclienticon $iClnt)
            ]
        ]
        //=====================================================================================================================
        //  BTN -- SWITCH TEAM:
        if (m_teammode $getmode) [
            guistrut 2
            guilist [// V
                guistrut 9.09 1
                if (ismaster $getclientnum) [
                    looplist iClnt $arg1 [
                        if (isspectator $iClnt) [
                            guialign 0 [guitext "^f4~" 0]
                        ][
                            guibutton (concatword (? (=s (getclientteam $getclientnum)(getclientteam $iClnt)) "^f1" "^f3") "SwitchTeam") [setteam @iClnt @(? (=s (getclientteam $iClnt) "good") "evil" "good")] 0
                        ]
                    ]
                ]
            ]
        ]
        //=====================================================================================================================
        //  BTN -- SPECTATOR:
        guistrut 2
        guilist [// V
            if (ismaster $getclientnum) [
                looplist iClnt $arg1 [
                    SM_ClntIsSpec (isspectator $iClnt)
                    guicheckbox (concatword (? $SM_ClntIsSpec "^f8" "^f4") "Spec") SM_ClntIsSpec 1 0 [spectator @(= (isspectator $iClnt) 0) @iClnt]
                ]
            ]
        ]
        //=====================================================================================================================
        //  BTN -- MASTER/AUTH/ADMIN:
        guistrut 2
        guilist [// V
            if (ismaster $getclientnum) [
                looplist iClnt $arg1 [
                    SM_ClntIsMstr (ismaster $iClnt)
                    guicheckbox (? (isadmin $iClnt) "^f6Admin" (? (isauth $iClnt) "^f5Auth" (? (ismaster $iClnt) "^f0Master" "^f4Master"))) SM_ClntIsMstr 1 0 [setmaster @(= (ismaster $iClnt) 0) @iClnt]
                ]
            ]
        ]
        //=====================================================================================================================
        //  BTN -- KICK:
        if (ismaster $getclientnum) [
            guistrut 2
            guilist [// V
                looplist iClnt $arg1 [
                    guibutton "^f2Kick" [kick @iClnt] 0
                ]
            ]
        ]
        //=====================================================================================================================
    ]
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
nodebug [
    defvar BM_BotCount =   5
    defvar BM_MinSkill =  50
    defvar BM_MaxSkill = 100
]

BM_StartMatch = [
    if (ingamemp) [
        echo "You must disconnect from the current multiplayer game before starting a bot match."
    ] [
        loop i $BM_BotCount [addbot (rnd (+ $BM_MaxSkill 1) $BM_MinSkill)]
    ]
]
newgui "botmatch" [
    guitext "bot amount"
    guislider BM_BotCount 0 32
    guitext "bot minimum skill"
    guislider BM_MinSkill 50 101 [if (< $BM_MaxSkill $BM_MinSkill) [BM_MaxSkill = $BM_MinSkill]]
    guitext "bot maximum skill"
    guislider BM_MaxSkill 50 101 [if (> $BM_MinSkill $BM_MaxSkill) [BM_MinSkill = $BM_MaxSkill]]
    guibutton "start match.." [guionclear [BM_StartMatch]; showgui "maps"] //showgui "gamemode"]
] "bot match"


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
newgui "campaign" [
    guibutton   "start Private Stan Sauer"  [showgui "privatestansauer"]
    guibutton   "start An Army Of One"      [showgui "armyofone"]
    guibutton   "start Lost"                [sp lost]                       "cube"
    guibutton   "start Meltdown"            [sp skrsp1]                     "cube"
    guibutton   "start Missile Pass"        [sp crnsp1]                     "cube"
    guibutton   "start Level 9"             [sp level9]                     "cube"
    guibar
    guibutton   "start DMSP map.."          [mode -2; showgui "maps"]
    guicheckbox "slow motion"               [slowmosp]
    guitext     "skill (default: 3)"
    guislider skill
]

newgui "armyofone" [
    guilist [
        guilist [
            guibutton "Part I"   [sp mpsp6a] "cube"
            guibutton "Part II"  [sp mpsp6b] "cube"
            guibutton "Part III" [sp mpsp6c] "cube"
        ]
        _showmapshot (substr $guirolloveraction 3)
    ]
] "An Army Of One"

newgui "privatestansauer" [
    guilist [
        guilist [
            guibutton "Run N' Gun Part I"        [sp mpsp9a] "cube"
            guibutton "Run N' Gun Part II"       [sp mpsp9b] "cube"
            guibutton "Run N' Gun Part III"      [sp mpsp9c] "cube"
            guibutton "THE SERIOUSLY BIG VALLEY" [sp mpsp10] "cube"
        ]
        _showmapshot (substr $guirolloveraction 3)
    ]
] "Private Stan Sauer"

_showmapshot = [
    guibar
    ML_MapName = (checkrolloveraction "map " [if (> $numargs 0) [result $arg1] [at $guirollovername 0]])
    guilist [
        guiimage (concatword "packages/base/" $ML_MapName ".jpg") (checkrolloveraction "map ") 4 1 "data/cube.png" $ML_MapName
    ]
]
TODO 0 "menus.cfg -- Factor out showmapshot."


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
nodebug [
    defvar  ML_NumOfMaps  -1 0 16384
    defvar  ML_NumOfCols   0 0   512
    defvar  ML_MapsPerTab  0 0   512
    defvar  ML_NumOfTabs   0 0   256

    defvar  ML_CurrentTab  0 0   256

    defvar  ML_ModeType    0 0     7
    defvar  ML_ModeVrty    0 0     3
    defvar  ML_Mode        0 0    22
    defvar  ML_ModeMaps    0 0     2
    defsvar ML_ModeName    "coop"

    defsvar ML_MapName     ""
]

guimaplist = [
    //  This generates full-tabs with:  MapList, MapScreenshot, GameModeSelection.
    // ARGS 1: FirstTabLabel = "STRING"
    //      2: MapList       = [LIST]
    //      3: ColumnWidth   = INT
    //      4: MapsPerColumn = INT
    //      5: ColumnsPerTab = INT
    //      6: IsFirstTab    = 0|1

    ML_NumOfMaps  (- (listlen $arg2) 1) // List index starts at 0.
    ML_NumOfCols  (+ (ceil (divf (+ $ML_NumOfMaps 1) $arg4)))
    ML_MapsPerTab (* $arg4 $arg5)
    ML_NumOfTabs  (max (+ (ceil (divf $ML_NumOfCols $arg5))) 1)

    loop iTab $ML_NumOfTabs [
        if (|| (!= $arg6 1) (> $iTab 0)) [
            if (= $iTab 0) [
                guitab $arg1
                ML_CurrentTab (+ $ML_CurrentTab 1) //<-- This is gross.
            ][
                guitab (+ $iTab 1)
                ML_CurrentTab (+ $ML_CurrentTab 1) //<-- This is gross.
            ]
        ]
        iMap = (* $iTab $ML_MapsPerTab)
        //=====================================================================================================
        // Is this the active Tab? If so, populate.
        if (= $ML_CurrentTab (- $guitabnum 1)) [
            guialign 0 [// H
                guistrut $arg4 1
                if (< $ML_NumOfMaps 0) [
                    guilist [// V
                        guistrut $arg3 1
                        guialign 0 [guitext "^f4~" 0] // Map list is empty.
                    ]
                ][
                    iCol = 1
                    while [<= $iCol $arg5] [
                        guilist [// V
                            guistrut $arg3 1
                            iEND = (+ (* $iTab $ML_MapsPerTab) (- (* $iCol $arg4) 1))
                            while [<= $iMap $iEND] [
                                if (> $iMap $ML_NumOfMaps) [
                                    iMap = (+ $iEND 1) // exitloop/break
                                ][
                                    guibutton (at $arg2 $iMap) (concat map (at $arg2 $iMap)) "cube"
                                    iMap = (+ $iMap 1)
                                ]
                            ]
                        ]
                        iCol = (+ $iCol 1)
                    ]
                ]
                guibar
                guistrut 0.5 // It appears menu padding on the right 1.5x~ that of the left.
                guistayopen [
                    guilist [// V
                        ML_MapName = (checkrolloveraction "map " [if (> $numargs 0) [result $arg2] [at $guirollovername 0]])
                        guialign 0 [
                            guiimage (concatword "packages/base/" $ML_MapName ".jpg") () 3.875 0 "data/cube.png"
                        ]
                        guititle (concatword "^f2/" $ML_ModeName " ^f~" $ML_MapName)
                        guistrut 0.25
                        guialign 0 [
                            guilist [
                                guistrut 9 1
                                guiradio "^f3DM"       ML_ModeType 1 [ML_SetupMode]
                                guiradio "^f3TDM"      ML_ModeType 2 [ML_SetupMode]
                                guiradio "^f6Capture"  ML_ModeType 3 [ML_SetupMode]
                                guiradio "^f6Hold"     ML_ModeType 4 [ML_SetupMode]
                                guiradio "^f2CTF"      ML_ModeType 5 [ML_SetupMode]
                                guiradio "^f2Protect"  ML_ModeType 6 [ML_SetupMode]
                                guiradio "^f2Collect"  ML_ModeType 7 [ML_SetupMode]
                                guiradio "^f0CoopEdit" ML_ModeType 0 [ML_SetupMode]
                            ]
                            guistrut 1
                            guilist [
                                guistrut 9.5 1
                                cond [= $ML_ModeType 0][ // Coopedit.
                                    guiradio  "^f7Normal" [] "radio_on"
                                ][<= $ML_ModeType 2][
                                    guiradio  "^f7Normal"     ML_ModeVrty 0 [ML_SetupMode]
                                    guiradio  "^f8InstaGib"   ML_ModeVrty 1 [ML_SetupMode]
                                    guiradio  "^f1Efficiency" ML_ModeVrty 2 [ML_SetupMode]
                                    guiradio  "^f5Tactics"    ML_ModeVrty 3 [ML_SetupMode]
                                ][ = $ML_ModeType 3][
                                    guiradio  "^f7Normal"     ML_ModeVrty 0 [ML_SetupMode]
                                    guibutton "^f8Regen"      [if (< $ML_ModeVrty 1) [ML_ModeVrty 1]; ML_SetupMode] (if (>= $ML_ModeVrty 1) [result "radio_on"] [result "radio_off"])
                                ][>= $ML_ModeType 4][
                                    guiradio  "^f7Normal"     ML_ModeVrty 0 [ML_SetupMode]
                                    guiradio  "^f8InstaGib"   ML_ModeVrty 1 [ML_SetupMode]
                                    guibutton "^f1Efficiency" [if (< $ML_ModeVrty 2) [ML_ModeVrty 2]; ML_SetupMode] (if (>= $ML_ModeVrty 2) [result "radio_on"] [result "radio_off"])
                                ]
                            ]
                        ]
                    ]
                ]
            ]
        ]
        //=====================================================================================================
    ]
]
ML_SetupMode = [
    case        ($ML_ModeType) 0 [ML_ModeMaps 0; mode  1; ML_Mode  1; ML_ModeName "coop"         //  0 "CoopEdit"
    ] 1 [case   ($ML_ModeVrty) 0 [ML_ModeMaps 0; mode  0; ML_Mode  0; ML_ModeName "ffa"          //  1 "DM"
                             ] 1 [ML_ModeMaps 0; mode  3; ML_Mode  3; ML_ModeName "insta"
                             ] 2 [ML_ModeMaps 0; mode  5; ML_Mode  5; ML_ModeName "effic"
                             ] 3 [ML_ModeMaps 0; mode  7; ML_Mode  7; ML_ModeName "tac"]
    ] 2 [case   ($ML_ModeVrty) 0 [ML_ModeMaps 0; mode  2; ML_Mode  2; ML_ModeName "teamplay"     //  2 "TDM"
                             ] 1 [ML_ModeMaps 0; mode  4; ML_Mode  4; ML_ModeName "instateam"
                             ] 2 [ML_ModeMaps 0; mode  6; ML_Mode  6; ML_ModeName "efficteam"
                             ] 3 [ML_ModeMaps 0; mode  8; ML_Mode  8; ML_ModeName "tacteam"]
    ] 3 [cond [=  $ML_ModeVrty 0][ML_ModeMaps 1; mode  9; ML_Mode  9; ML_ModeName "capture"      //  3 "Capture"
             ][>= $ML_ModeVrty 1][ML_ModeMaps 1; mode 10; ML_Mode 10; ML_ModeName "regencapture"]
    ] 4 [cond [=  $ML_ModeVrty 0][ML_ModeMaps 1; mode 15; ML_Mode 15; ML_ModeName "hold"         //  4 "Hold"
             ][=  $ML_ModeVrty 1][ML_ModeMaps 1; mode 16; ML_Mode 16; ML_ModeName "instahold"
             ][>= $ML_ModeVrty 2][ML_ModeMaps 1; mode 19; ML_Mode 19; ML_ModeName "effichold"]
    ] 5 [cond [=  $ML_ModeVrty 0][ML_ModeMaps 2; mode 11; ML_Mode 11; ML_ModeName "ctf"          //  5 "CTF"
             ][=  $ML_ModeVrty 1][ML_ModeMaps 2; mode 12; ML_Mode 12; ML_ModeName "instactf"
             ][>= $ML_ModeVrty 2][ML_ModeMaps 2; mode 17; ML_Mode 17; ML_ModeName "efficctf"]
    ] 6 [cond [=  $ML_ModeVrty 0][ML_ModeMaps 2; mode 13; ML_Mode 13; ML_ModeName "protect"      //  6 "Protect"
             ][=  $ML_ModeVrty 1][ML_ModeMaps 2; mode 14; ML_Mode 14; ML_ModeName "instaprotect"
             ][>= $ML_ModeVrty 2][ML_ModeMaps 2; mode 18; ML_Mode 18; ML_ModeName "efficprotect"]
    ] 7 [cond [=  $ML_ModeVrty 0][ML_ModeMaps 2; mode 20; ML_Mode 20; ML_ModeName "collect"      //  7 "Collect"
             ][=  $ML_ModeVrty 1][ML_ModeMaps 2; mode 21; ML_Mode 21; ML_ModeName "instacollect"
             ][>= $ML_ModeVrty 2][ML_ModeMaps 2; mode 22; ML_Mode 22; ML_ModeName "efficcollect"]
    ]
]
TODO 0 "menus.cfg -- GuiMapList - Fix FakeRadio/Button causing guirollover ???"

TODO 0 "menus.cfg -- Generalize 'guimaplist' to work with any list of things?  Maybe not, it became less general..."

TODO 0 "menus.cfg -- Make all of these list-type GUIs use guiautotab???"
TODO 0 "             Pretty much only the ServerBrowser uses it now."


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
newgui "maps" [
    guikeeptab 1
    ML_CurrentTab 0 //<-- This is gross.
    if (= $ML_ModeMaps 0) [
        guimaplist "ffa"     $MapList_FFA     15 17 3 1
        guimaplist "capture" $MapList_Capture 15 17 3 0
        guimaplist "ctf"     $MapList_CTF     15 17 3 0
        guimaplist "concept" $MapList_Concept 15 17 3 0
    ][
        if (= $ML_ModeMaps 1) [
            guimaplist "capture" $MapList_Capture 15 17 3 1
        ][
            guimaplist "ctf"     $MapList_CTF     15 17 3 1
        ]
    ]
] "maps" [ML_MapName = ""]

//newgui "capturemaps" [
//    ML_CurrentTab 0 //<-- This is gross.
//    guimaplist "capture" $MapList_Capture 15 17 3 1
//] "capture maps" [ML_MapName = ""]
//
//newgui "ctfmaps"     [
//    ML_CurrentTab 0 //<-- This is gross.
//    guimaplist "ctf"     $MapList_CTF     15 17 3 1
//] "ctf maps" [ML_MapName = ""]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
MapList_Custom = ""

ML_GenMapList_Custom = [
    MapList_Custom = ""
    loopfiles iFile "packages/base" "ogz" [
        if (< (indexof $MapList_ALL $iFile) 0) [
            MapList_Custom = (concat $MapList_Custom (escape $iFile))
        ]
    ]
    MapList_Custom = (sortlist $MapList_Custom iA iB [naturalsort $iA $iB])
]

newgui "custommaps" [
    guikeeptab 1
    ML_CurrentTab 0 //<-- This is gross.
    ListLen = (listlen $MapList_Custom)
    guimaplist "custom" $MapList_Custom 15 17 (? (<= $ListLen 34) (? (<= $ListLen 17) [1][2])[3]) 1
] "custom maps" [ML_MapName = ""; ML_GenMapList_Custom]



///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Options:

nodebug [
    defvar  OPT_Width  1 66 -1 // in units of (fontscale Width )
    defvar  OPT_Height 1 16 -1 // in units of (fontscale Height)

    defvar  GfxPreset_Decals           0 0 10
    defvar  GfxPreset_DynamicShadows   0 0 10
    defvar  GfxPreset_Glare            0 0 10
    defvar  GfxPreset_Grass            0 0 10
    defvar  GfxPreset_MotionBlur       0 0 10
    defvar  GfxPreset_ShadowmapQuality 0 0 10
    defvar  GfxPreset_SoftParticles    0 0 10

    defvar  GfxShowTexReduce 0 0 1

    deffvar GuiSens 0.001 1.0 1000.0
]

newgui "options" [
    guikeeptab 1
    // Save the frames 'guitabnum' hack:        H                                V
                      if (= $guitabnum  1) [ guilist [guistrut $OPT_Height 1; guilist [guistrut $OPT_Width 1; OPT_TAB_Game   ]] ]
    guitab "console"; if (= $guitabnum  2) [ guilist [guistrut $OPT_Height 1; guilist [guistrut $OPT_Width 1; OPT_TAB_Console]] ]
    guitab "display"; if (= $guitabnum  3) [ guilist [guistrut $OPT_Height 1; guilist [guistrut $OPT_Width 1; OPT_TAB_Display]] ]
    guitab "gfx"    ; if (= $guitabnum  4) [ guilist [guistrut $OPT_Height 1; guilist [guistrut $OPT_Width 1; OPT_TAB_Gfx    ]] ]
    guitab "hud"    ; if (= $guitabnum  5) [ guilist [guistrut $OPT_Height 1; guilist [guistrut $OPT_Width 1; OPT_TAB_Hud    ]] ]
    guitab "input"  ; if (= $guitabnum  6) [ guilist [guistrut $OPT_Height 1; guilist [guistrut $OPT_Width 1; OPT_TAB_Input  ]] ]
    guitab "menus"  ; if (= $guitabnum  7) [ guilist [guistrut $OPT_Height 1; guilist [guistrut $OPT_Width 1; OPT_TAB_Menus  ]] ]
    guitab "res"    ; if (= $guitabnum  8) [ guilist [guistrut $OPT_Height 1; guilist [guistrut $OPT_Width 1; OPT_TAB_Res    ]] ]
    guitab "sound"  ; if (= $guitabnum  9) [ guilist [guistrut $OPT_Height 1; guilist [guistrut $OPT_Width 1; OPT_TAB_Sound  ]] ]
    guitab "cfg"    ; if (= $guitabnum 10) [ guilist [guistrut $OPT_Height 1; guilist [guistrut $OPT_Width 1; OPT_TAB_Cfg    ]] ]
] "game"






TODO 0 "menus.cfg -- More Options..."
// showparticles
// maxparticles 4000
// maxparticledistance 1024

// showfps
// showfpsrange
// showmodeinfo

// showboundingbox
// showblendmap
// showeditstats
// showentradius
// showmat
// showsky
// showskyoutline
// showwaypoints
// showwaypointsradius

//  SCORE BOARD
//      showservinfo      0 1 1
//      showclientnum     0 0 1
//      showpj            0 0 1
//      showping          0 1 2
//      showspectators    0 1 1
//      showspectatorping 0 0 1
//      highlightscore    0 1 1
//      showconnecting    0 0 1
//      hidefrags         0 1 1
//      showdeaths        0 0 1
//      tiesort           0 0 2


TODO 0 "menus.cfg -- Options>Hud stuff..."
//@@    healthcolors
//@@    show status icons over players ("statusicons")
//@@    show health status bars for teammates ("statusbars")



//#############################################################################################################################################
//#############################################################################################################################################
OPT_TAB_Game = [
    guifreerow 18  1 [ guitext "hit indication: " 0
             ] 15 -2 [ guicheckbox "hit crosshair"  hitcrosshair 425 0
             ] 15 -2 [ guicheckbox "hit sound"      hitsound
    ]
    guistrut 0.75
    guifreerow 18  1 [ guitext "scoreboard: " 0
             ] 48 -2 [ guicheckbox "show upon death"  deathscore
    ]
    guifreerow 18  1 [
             ] 15 -2 [ guicheckbox "show frags"       hidefrags 0 1
             ] 15 -2 [ guicheckbox "show deaths"      showdeaths
             ]  1 -2 [ guicheckbox "show packetjump"  showpj
    ]
    guifreerow 18 -2 [
             ] 12 -2 [ guitext "show ping: "
             ]  9 -2 [ guiradio "off"                showping 0
             ]  9 -2 [ guiradio "ping"               showping 1
             ]  9 -2 [ guiradio "ping & packetjump"  showping 2
    ]
    guistrut 0.75
    guifreerow 18  1 [ guitext "respawn: " 0
             ] 12 -2 [ guicheckbox "via ^f2attack"  attackspawn
             ] 12 -2 [ guicheckbox "via ^f2jump"    jumpspawn
             ] 12 -2 [ if (&& (= $attackspawn 0) (= $jumpspawn 0)) [guitext (? (mod (div $getmillis 500) 2) "^f4use ^f6/bind KEY [respawn]" "^f~use ^f2/bind KEY [respawn]")]
    ]
    guistrut 0.75
    guifreerow 18  1 [ guitext "player models: " 0
             ] 12 -1 [ guicheckbox "fullbright" fullbrightmodels 60 0
             ]  9 -1 [ if (!= $fullbrightmodels 0) [ guiradio "^f5subtle"   fullbrightmodels  60 ][ guiradio "^f4subtle"   fullbrightmodels  60 ]
             ]  9 -1 [ if (!= $fullbrightmodels 0) [ guiradio "^f1bright"   fullbrightmodels 100 ][ guiradio "^f4bright"   fullbrightmodels 100 ]
             ]  9 -1 [ if (!= $fullbrightmodels 0) [ guiradio "^f8intense"  fullbrightmodels 150 ][ guiradio "^f4intense"  fullbrightmodels 150 ]
             ]  9 -1 [ if (!= $fullbrightmodels 0) [ guiradio "^f7max"      fullbrightmodels 200 ][ guiradio "^f4max"      fullbrightmodels 200 ]
    ]
    guifreerow 18 -2 [
             ] 27 -2 [ guicheckbox "force matching player models" forceplayermodels
             ]  1 -2 [ guicheckbox "always use team skins" teamskins
    ]
    guistrut 0.75
    guifreerow 18  1 [ guitext "violence: " 0
             ] 48 -2 [ guicheckbox "blood"  blood
    ]
    guifreerow 18 -2 [
             ] 12 -2 [ guitext "hide dead:"
             ]  9 -2 [ guiradio "off"          hidedead 0
             ]  9 -2 [ guiradio "all"          hidedead 1
             ]  9 -2 [ guiradio "enemies only" hidedead 2
    ]
    guifreerow 18 -2 [
             ] 18 -2 [ if (= $hidedead 1) [ guicheckbox "^f4ragdoll deaths" ragdoll ][ guicheckbox "ragdoll deaths" ragdoll ]
             ] 30 -2 [ if (|| (= $hidedead 1) (= $ragdoll 0)) [ guicheckbox "^f4keep after respawn" ragdollmillis 10000 0 ][ guicheckbox "keep after respawn" ragdollmillis 10000 0 ]
    ]
    guifreerow 18 -2 [
             ] 18 -2 [ if (|| (= $hidedead 1) (= $ragdoll 0)) [guitext "^f4velocity multiplier:"][guitext "velocity multiplier:"]
             ] 30 -2 [ guislider deadpush
    ]
    guibar
    guifreerow 18 -2 [
             ] 48 -2 [ guibutton "^f6restore defaults (clears ALL settings)" [showgui "options_restore"]
    ]
]

newgui "options_restore" [
    guistrut 28 1
    guialign 0 [guitext "^f7Restore defaults, are you sure?" 0 ]
    guialign 0 [guitext "^f2(clears ^f6ALL ^f2settings)" 0 ]
    guistrut 1
    guialign 0 [
        guibutton "^f2Restore" [exec "restore.cfg"; cleargui] 0
        guistrut 5
        guibutton "^f7Cancel" [cleargui 1] 0
    ]
] 0






TODO 0 "menus.cfg -- Use 'AreYouSure' style for other prompt menus?"
newgui "old" [
    guitext "the following settings have changed:"
    guitext "anti-aliasing"
    guitext "sound configuration"
    guibar
    guitext "apply changes now?"
    guibutton "yes"
    guibutton "no"
] 0

newgui "new" [
    //guistrut 1
    guialign 0 [guitext "^f7The following settings have changed:" 0]
    guialign 0 [guitext "^f6anti-aliasing" 0]
    guialign 0 [guitext "^f6sound configuration" 0]
    guistrut 1
    guialign 0 [
        guilist [guistrut 14 1; guialign 0 [guibutton "^f2Apply Now" [] 0]]
        //guistrut 5
        guilist [guistrut 12 1; guialign 0 [guibutton "^f7Skip"      [] 0]]
    ]
] 0






//#############################################################################################################################################
//#############################################################################################################################################
nodebug [
    defvarp fullconscroll 0 0 1
]

OPT_TAB_Console = [
    guifreerow 18  1 [ guitext "text size: " 0
             ]  1 -2 [ guifield conscale 5
             ]  1 -2 [ guistayopen [guibutton "" [conscale (+fround $conscale -0.05)]  "val_dec"]
             ]  1 -2 [ guistayopen [guibutton "" [conscale (+fround $conscale  0.05)]  "val_inc"]
    ]
    guistrut 0.5
    guifreerow 18  1 [
             ] 24 -1 [ guicheckbox "chat console" miniconfilter 0x300 0
             ]  1 -1 [ guicheckbox "toggle full console ^f2[F11]" fullconsole
    ]
    guistrut 0.25
    guifreerow 18  1 [ guitext "console size: " 0
             ] 48 -2 [ guislider consize
    ]
    guifreerow 18  1 [ guitext "^f4(# of lines) " 0 ] 0.5
    guifreerow 18  1 [ guitext "console filter: " 0
             ] 16 -1 [ guibitfield "important info"   confilter 0x11
             ] 16 -1 [ guibitfield "chat"             confilter 0x100
             ] 16 -1 [ guibitfield "self frags"       confilter 0x800
    ]
    guifreerow 18 -1 [
             ] 16 -1 [ guibitfield "warnings"         confilter 0x02
             ] 16 -1 [ guibitfield "team chat"        confilter 0x200
             ] 16 -1 [ guibitfield "other frags"      confilter 0x1000
    ]
    guifreerow 18 -1 [
             ] 16 -1 [ guibitfield "errors"           confilter 0x04
             ] 16 -1 [ guibitfield "game events"      confilter 0x400
             ] 16 -1 [ guibitfield "team kills"       confilter 0x2000
    ]
    guifreerow 18 -1 [
             ] 16 -1 [ guibitfield "script messages"  confilter 0x20
    ]
    guistrut 0.25

//fullconscroll

    guifreerow 18  1 [ guitext "full console size: " 0
             ] 48 -2 [ guislider fullconsize
    ]
    guifreerow 18  1 [ guitext "^f4(% of screen) " 0 ] 0.5
    guifreerow 18  1 [ guitext "full console filter: " 0
             ] 16 -1 [ guibitfield "important info"   fullconfilter 0x11
             ] 16 -1 [ guibitfield "chat"             fullconfilter 0x100
             ] 16 -1 [ guibitfield "self frags"       fullconfilter 0x800
    ]
    guifreerow 18 -1 [
             ] 16 -1 [ guibitfield "warnings"         fullconfilter 0x02
             ] 16 -1 [ guibitfield "team chat"        fullconfilter 0x200
             ] 16 -1 [ guibitfield "other frags"      fullconfilter 0x1000
    ]
    guifreerow 18 -1 [
             ] 16 -1 [ guibitfield "errors"           fullconfilter 0x04
             ] 16 -1 [ guibitfield "game events"      fullconfilter 0x400
             ] 16 -1 [ guibitfield "team kills"       fullconfilter 0x2000
    ]
    guifreerow 18 -1 [
             ] 16 -1 [ guibitfield "script messages"  fullconfilter 0x20
    ]
]


//#############################################################################################################################################
//#############################################################################################################################################
//  Multiples of common monitor refresh rates:
//_TheHertz_60 = [  60 120 240 360 480 600 720 840 960 ]
//_TheHertz_75 = [  75 150 225 300 450 600 900 ]
//_TheHertz_90 = [  90 180 270 360 540 720 900 ]
//_TheHertz144 = [     144 288 432 576 720 864 ]
//_TheHertz165 = [     165 330 495 660 825 990 ]
OPT_TAB_Display = [
    guifreerow 18  1 [ guitext "field of view: " 0
             ] 36 -2 [ guislider fov;
             ] 12  0 [ guitext "(default 100)" 0
    ]
    guibar
    guifreerow 18  1 [ guitext "maxfps: " 0
             ] 36 -2 [ guilistslider maxfps [60 75 90 120 144 165 240 360 480 600 720 840 960]
             ] 12  1 [ guifield maxfps 10
    ]
    guistrut 0.25
    guifreerow 18 -1 [
             ] 36 -1 [ guicheckbox "fullscreen"  fullscreen
    ]
    guifreerow 18 -1 [
             ] 36 -1 [ guicheckbox "v-sync"      vsync 1 0
    ]
    guistrut 0.25
    guifreerow 18  1 [ guitext "gamma: " 0
             ] 36 -2 [ guislider gamma
             ] 12  0 [ guitext "(default 100)" 0
    ]
    guifreerow 18  1 [ guitext "z-buffer depth: " 0
             ] 36 -2 [ guilistslider depthbits [0 16 24 32]
             ] 12  0 [ guitext "(default   0)" 0
    ]
    guibar
    guifreerow 18 -1 [
             ] 36 -1 [ guicheckbox "bilinear^t(texture smoothing)"  bilinear
    ]
    guifreerow 18 -1 [
             ] 36 -1 [ guicheckbox "bilinear^t(texture smoothing, map geo only)"  reducefilter
    ]
    guifreerow 18 -1 [
             ] 36 -1 [ guicheckbox "trilinear^t(mipmap-level blending)"  trilinear
    ]
    guistrut 0.25
    guifreerow 18  1 [ guitext "mipmap anisotropy: " 0
             ] 36 -2 [ guinameslider aniso ["off" "2x" "4x" "8x" "16x"] [0 2 4 8 16] //guilistslider aniso [0 2 4 8 16]
             ] 12  0 [ guitext "(default   0)" 0
    ]
    guifreerow 18  1 [ guitext "anti-aliasing: " 0
             ] 36 -2 [ guinameslider fsaa ["auto?" "off" "2x" "4x" "8x" "16x"] [-1 0 2 4 8 16] //guilistslider fsaa [-1 0 2 4 8 16]
             ] 12  0 [ guitext "(default  -1)" 0
    ]
]


//#############################################################################################################################################
//#############################################################################################################################################
OPT_TAB_Gfx = [
    guifreerow 17.75  1 [ guitext "^f7*performance key*: " 0
             ] 48     0 [ guitext "^f0fast^f~, ^f2moderate^f~, ^f6moderate-ish^f~, ^f3slow & pretty^f~" 0
    ]
    guistrut 0.25
    guifreerow 17.75  1 [ guitext "shader detail: " 0
             ] 10    -2 [ guiradio "^f0low"    shaderdetail 0
             ] 10    -2 [ guiradio "^f2medium" shaderdetail 1
             ] 10    -2 [ guiradio "^f6high"   shaderdetail 2
             ]  1    -2 [ guiradio "^f3ultra"  shaderdetail 3
    ]
    guistrut 0.25
    GfxPreset_Glare 0
    if (= $glare 1) [
        if (= $glarescale 1) [
            if (= $blurglare 4) [GfxPreset_Glare 1]
            if (= $blurglare 7) [GfxPreset_Glare 3]
        ]
        if (= $glarescale 2) [
            if (= $blurglare 3) [GfxPreset_Glare 2]
            if (= $blurglare 7) [GfxPreset_Glare 4]
        ]
    ]
    guifreerow  17.75  1 [ guitext "glare: " 0
             ]  10    -2 [ guiradio "^f4off"     GfxPreset_Glare 0 [glare 0]
             ]  10    -2 [ guiradio "^f2subtle"  GfxPreset_Glare 1 [glare 1; blurglare 4; glarescale 1]
             ]  10    -2 [ guiradio "^f2glowy"   GfxPreset_Glare 2 [glare 1; blurglare 3; glarescale 2]
             ]   8.6  -2 [ guiradio "^f3soft"    GfxPreset_Glare 3 [glare 1; blurglare 7; glarescale 1]
             ]   1    -2 [ guiradio "^f3intense" GfxPreset_Glare 4 [glare 1; blurglare 7; glarescale 2]
    ]
    GfxPreset_MotionBlur 0
    if     (=  $motionblur 0)         [GfxPreset_MotionBlur 0] [
        if (=f $motionblurscale 0.5 ) [GfxPreset_MotionBlur 1]
        if (=f $motionblurscale 0.65) [GfxPreset_MotionBlur 2]
        if (=f $motionblurscale 0.8 ) [GfxPreset_MotionBlur 3]
    ]
    guifreerow 17.75  1 [ guitext "motion blur: " 0
             ] 10    -2 [ guiradio "^f4off"      GfxPreset_MotionBlur 0 [motionblur 0]
             ] 10    -2 [ guiradio "^f3subtle"   GfxPreset_MotionBlur 1 [motionblur 1; motionblurscale 0.5 ]
             ] 10    -2 [ guiradio "^f3moderate" GfxPreset_MotionBlur 2 [motionblur 1; motionblurscale 0.65]
             ]  1    -2 [ guiradio "^f3intense"  GfxPreset_MotionBlur 3 [motionblur 1; motionblurscale 0.8 ]
    ]
    guifreerow 17.75  1 [ guitext "dynamic lights: " 0
             ] 10    -2 [ guiradio "^f4off"    maxdynlights 0
             ] 10    -2 [ guiradio "^f0max 3"  maxdynlights 3
             ] 10    -2 [ guiradio "^f2max 4"  maxdynlights 4
             ]  1    -2 [ guiradio "^f6max 5"  maxdynlights 5
    ]
    GfxPreset_SoftParticles 0
    if         (= $depthfx 0)                          [GfxPreset_SoftParticles 0] [
        if (&& (= $depthfxsize  7) (= $blurdepthfx 1)) [GfxPreset_SoftParticles 1]
        if (&& (= $depthfxsize 10) (= $blurdepthfx 0)) [GfxPreset_SoftParticles 2]
        if (&& (= $depthfxsize 12) (= $blurdepthfx 0)) [GfxPreset_SoftParticles 3]
    ]
    guifreerow 17.75  1 [ guitext "soft particles: " 0
             ] 10    -2 [ guiradio "^f4off"    GfxPreset_SoftParticles 0 [depthfx 0]
             ] 10    -2 [ guiradio "^f0low"    GfxPreset_SoftParticles 1 [depthfx 1; depthfxsize  7; blurdepthfx 1]
             ] 10    -2 [ guiradio "^f2medium" GfxPreset_SoftParticles 2 [depthfx 1; depthfxsize 10; blurdepthfx 0]
             ]  1    -2 [ guiradio "^f3high"   GfxPreset_SoftParticles 3 [depthfx 1; depthfxsize 12; blurdepthfx 0]
    ]
    GfxPreset_DynamicShadows 0
    if (&& (= $blobs 1) (= $shadowmap 0)) [GfxPreset_DynamicShadows 1]
    if                  (= $shadowmap 1)  [GfxPreset_DynamicShadows 2]
    guifreerow 17.75  1 [ guitext "dynamic shadows: " 0
             ] 10    -2 [ guiradio "^f4off"         GfxPreset_DynamicShadows 0 [blobs 0; shadowmap 0]
             ] 10    -2 [ guiradio "^f2blobs"       GfxPreset_DynamicShadows 1 [blobs 1; shadowmap 0]
             ]  1    -2 [ guiradio "^f3shadowmaps"  GfxPreset_DynamicShadows 2 [blobs 0; shadowmap 1]
    ]
    if (= $shadowmap 1) [
        GfxPreset_ShadowmapQuality 0
        if (= $blurshadowmap 0) [
            if (= $shadowmapsize  9) [GfxPreset_ShadowmapQuality 1]
        ][
            if (= $shadowmapsize  9) [GfxPreset_ShadowmapQuality 2]
            if (= $shadowmapsize 10) [GfxPreset_ShadowmapQuality 3]
            if (= $shadowmapsize 11) [GfxPreset_ShadowmapQuality 4]
        ]
        guifreerow 17.75  1 [ guitext "shadowmap quality: " 0
                 ] 10    -2 [ guiradio "^f2low"    GfxPreset_ShadowmapQuality 1 [shadowmapsize  9; blurshadowmap 0]
                 ] 10    -2 [ guiradio "^f2medium" GfxPreset_ShadowmapQuality 2 [shadowmapsize  9; blurshadowmap 1]
                 ] 10    -2 [ guiradio "^f6high"   GfxPreset_ShadowmapQuality 3 [shadowmapsize 10; blurshadowmap 1]
                 ]  1    -2 [ guiradio "^f3ultra"  GfxPreset_ShadowmapQuality 4 [shadowmapsize 11; blurshadowmap 1]
        ]
        guistrut 0.25
    ][
        guifreerow 17.75  1 [ guitext "shadowmap quality: " 0
                 ] 10    -2 [ guiradio "^f4low"    GfxPreset_ShadowmapQuality 1 [shadowmapsize  9; blurshadowmap 0]
                 ] 10    -2 [ guiradio "^f4medium" GfxPreset_ShadowmapQuality 2 [shadowmapsize  9; blurshadowmap 1]
                 ] 10    -2 [ guiradio "^f4high"   GfxPreset_ShadowmapQuality 3 [shadowmapsize 10; blurshadowmap 1]
                 ]  1    -2 [ guiradio "^f4ultra"  GfxPreset_ShadowmapQuality 4 [shadowmapsize 11; blurshadowmap 1]
        ]
        guistrut 0.25
    ]
    guifreerow 17.75  1 [ guitext "models: " 0
             ] 10    -2 [ guicheckbox "^f0reflectenv"  envmapmodels
             ]  1    -2 [ guicheckbox "^f2bumpmap"     bumpmodels
    ]
    guistrut 0.25
    if (= $GfxShowTexReduce 0) [
        guifreerow  17.75  1 [ guistayopen [guibutton "max texture size: " [GfxShowTexReduce 1] 0 ]
                 ]  10    -2 [ guiradio "^f0256"        maxtexsize  256
                 ]  10    -2 [ guiradio "^f0512"        maxtexsize  512
                 ]  10    -2 [ guiradio "^f21024"       maxtexsize 1024
                 ]   8.6  -2 [ guiradio "^f62048"       maxtexsize 2048
                 ]   1    -2 [ guiradio "^f6unlimited"  maxtexsize    0
        ]
    ][
        guifreerow 17.75  1 [ guistayopen [guibutton "reduce texture size: " [GfxShowTexReduce 0] 0 ] // Secret texture option. :o
                 ] 48    -2 [ guinameslider texreduce ["0" "1/2" "1/4" "1/8" "1/16" "1/32" "1/64" "1/128" "1/256" "1/512" "1/1024" "1/2048" "1/4096"] [0 1 2 3 4 5 6 7 8 9 10 11 12] []
        ]
    ]
    GfxPreset_Grass 0
    if     (= $grass 0)       [GfxPreset_Grass 0] [
        if (= $grassdist 128) [GfxPreset_Grass 1]
        if (= $grassdist 256) [GfxPreset_Grass 2]
        if (= $grassdist 512) [GfxPreset_Grass 3]
    ]
    guifreerow 17.75  1 [ guitext "grass: " 0
             ] 10    -2 [ guiradio "^f4off"    GfxPreset_Grass 0 [grass 0]
             ] 10    -2 [ guiradio "^f2low"    GfxPreset_Grass 1 [grass 1; grassdist 128]
             ] 10    -2 [ guiradio "^f6medium" GfxPreset_Grass 2 [grass 1; grassdist 256]
             ]  1    -2 [ guiradio "^f3high"   GfxPreset_Grass 3 [grass 1; grassdist 512]
    ]
    GfxPreset_Decals 0
    if         (= $decals 0)                                [GfxPreset_Decals 0] [
        if (&& (= $decalfade 10000) (= $maxdecaltris 1024)) [GfxPreset_Decals 1]
        if (&& (= $decalfade 30000) (= $maxdecaltris 2048)) [GfxPreset_Decals 2]
        if (&& (= $decalfade 60000) (= $maxdecaltris 4096)) [GfxPreset_Decals 3]
    ]
    guifreerow 17.75  1 [ guitext "decals: " 0
             ] 10    -2 [ guiradio "^f4off"    GfxPreset_Decals 0 [decals 0]
             ] 10    -2 [ guiradio "^f0low"    GfxPreset_Decals 1 [decals 1; decalfade 10000; maxdecaltris 1024]
             ] 10    -2 [ guiradio "^f2medium" GfxPreset_Decals 2 [decals 1; decalfade 30000; maxdecaltris 2048]
             ]  1    -2 [ guiradio "^f6high"   GfxPreset_Decals 3 [decals 1; decalfade 60000; maxdecaltris 4096]
    ]
    guifreerow 17.75  1 [ guitext "glass: " 0
             ] 10    -1 [ guicheckbox "^f0reflectenv"  glassenv
    ]
    guifreerow  17.75  1 [ guitext "water: " 0
             ]  10    -2 [ guicheckbox "^f0reflectenv"  waterenvmap
             ]  10    -2 [ guicheckbox "^f3reflection"  waterreflect
             ]  10    -2 [ guicheckbox "^f2refraction"  waterrefract
             ]   8.6  -2 [ guicheckbox "^f0caustics"    caustics
             ]   1    -2 [ guicheckbox "^f2animation"   vertwater
    ]
    guifreerow 17.75  1 [ guitext "waterfalls: " 0
             ] 10    -2 [ guicheckbox "^f0reflectenv"  waterfallenv
             ] 10    -2 [
             ]  1    -2 [ guicheckbox "^f2refraction"  waterfallrefract
    ]
]


//#############################################################################################################################################
//#############################################################################################################################################
OPT_TAB_Hud = [
    guifreerow 18  1 [ guitext "show/hide: " 0
             ] 12 -2 [ guicheckbox "ammobar"  ammobar
             ] 12 -2 [ if (= $ammobar 1) [guibutton "adjust ammobar^t^f2/adjustammobar"      adjustammobar    "menu"]
    ]
    guifreerow 18 -2 [
             ] 12 -2 [ guicheckbox "gameclock"  gameclock
             ] 12 -2 [ if (= $gameclock 1) [guibutton "adjust gameclock^t^f2/adjustgameclock"  adjustgameclock  "menu"]
    ]
    guifreerow 18 -2 [
             ] 12 -2 [ guicheckbox "hudscore"  hudscore
             ] 12 -2 [ if (= $hudscore 1) [guibutton "adjust hudscore^t^f2/adjusthudscore"    adjusthudscore   "menu"]
    ]
    guifreerow 18    -2 [
             ] 12    -2 [ guicheckbox "wallclock"  wallclock
             ] 11.25 -2 [ if $wallclock [ guicheckbox "24 hour"  wallclock24   ]
             ] 12    -2 [ if $wallclock [ guicheckbox "seconds"  wallclocksecs ]
    ]
    guifreerow 18 -2 [
             ] 12 -2 [ guicheckbox "FPS"  showfps
    ]
    guifreerow 18    -2 [
             ] 12    -2 [ guicheckbox "hudgun"  hudgun
             ] 11.25 -2 [ if $hudgun [ guicheckbox "sway"          hudgunsway  ]
             ] 12.25 -2 [ if $hudgun [ guicheckbox "muzzle flash"  muzzleflash ]
             ] 12    -2 [ if $hudgun [ guicheckbox "muzzle light"  muzzlelight ]
    ]
    guibar
    guifreerow 18  1 [ guitext "crosshair: " 0
             ]  3 -2 [ guiimage (getcrosshair) [showgui "crosshair"] 0.5
             ]  1 -2 [ guibutton "change" [showgui "crosshair"] 0
    ]
    guifreerow 18  1 [
             ] 12 -2 [ guitext "size:"
             ] 36 -2 [ guislider crosshairsize
    ]
    guifreerow 18    -2 [
             ] 12    -2 [ guicheckbox "effects"  crosshairfx
             ] 11.25 -2 [ if $crosshairfx [ guicheckbox "hits"           hitcrosshair 425 ]
             ] 12.25 -2 [ if $crosshairfx [ guicheckbox "teammates"      teamcrosshair    ]
             ]  1    -2 [ if $crosshairfx [ guicheckbox "health colors"  crosshaircolors  ]
    ]
    guibar
    guifreerow 18    1 [ guitext "damage indication: " 0
             ] 23.5 -2 [ guicheckbox "compass"        damagecompass
             ]  1   -2 [ guicheckbox "screen effect"  damagescreen
    ]
    guibar
    guifreerow 18    1 [ guitext "radar: " 0
             ] 23.5 -2 [ guicheckbox "numbered capture bases"  basenumbers
             ]  1   -2 [ guicheckbox "show teammates"          radarteammates 1
    ]
    guibar
    guifreerow 18    1 [ guitext "capture bases: " 0
             ] 23.5 -2 [ guicheckbox "outline meters"  outlinemeters
             ]  1   -2 [ guicheckbox "tether effect"   capturetether
    ]
]


//#############################################################################################################################################
//#############################################################################################################################################
BindActions = [
    "forward" "backward" "left" "right" "jump" "attack" "togglezoom"
    "saycommand" "sayteamcommand" "showscores" "toggleconsole" "screenshot" "edittoggle"
    "setweapon FI" "setweapon SG" "setweapon CG" "setweapon RL" "setweapon RI" "setweapon GL" "setweapon PI" "weapon"
    "universaldelta 1" "universaldelta -1" "dropflag" "addbot" "delbot"
]
OPT_TAB_Input = [
    guistayopen [
        _MyMouseSens = $sensitivity
        guifreerow 21    -2 [ guitext "mouse-aim sensitivity:"
                 ]  6.5  -2 [ guifield _MyMouseSens 5 [sensitivity $_MyMouseSens]
                 ]  2    -2 [ guibutton "" [sensitivity (+fround $sensitivity -0.1)]  "val_dec"
                 ]  4.35 -2 [ guibutton "" [sensitivity (+fround $sensitivity  0.1)]  "val_inc"
                 ]  1    -2 [ guicheckbox "mouse-aim invert Y-axis"  invmouse
        ]
    ]
    guistrut 0.6
    guititle "select action to bind and press desired keys (ESC when done)"
    guistrut 0.4
    iBind = 0
    guilistsplit n 2 $BindActions [
        guistrut 32 1
        guilist [
            guitext (tabify (concatword $n ": ") 4)
            [NewBinds@iBind] = (searchbinds $n)
            guikeyfield [NewBinds@iBind] -12 [
                OldBinds = (searchbinds [@@n])
                looplist iOld  $OldBinds         [bind $iOld ""]
                looplist iNew $[NewBinds@@iBind] [bind $iNew [@@@n]]
            ]
            iBind = (+ $iBind 1)
        ]
    ] [guibar]
]


//#############################################################################################################################################
//#############################################################################################################################################
OPT_TAB_Menus = [
    guistayopen [
        guifreerow 18  1 [ guitext "general: " 0
                 ] 16 -2 [ guitext "autotab thresh:"
                 ] 32 -2 [ guislider guiautotab
        ]
        guifreerow 18 -2 [
                 ] 16 -2 [ guitext "menufps: "
                 ] 32 -2 [ guilistslider menufps [60 75 90 120 144 165 240 360 480 600]
        ]
        guifreerow 18 -2 [
                 ] 16 -2 [ guicheckbox "click tab"  guiclicktab
        ]
        guifreerow 18 -2 [
                 ] 16 -2 [ guicheckbox "transition zoom"  guifadein
        ]
        guibar
        guifreerow 18  1 [ guitext "cursor: " 0
                 ] 16 -2 [ guitext "size:"
                 ] 32 -2 [ guislider cursorsize 4 50
        ]
        GuiSens $guisens
        guifreerow 18 -2 [
                 ] 16 -2 [ guitext "sensitivity: "
                 ]  1 -2 [ guifield GuiSens 5 [guisens $GuiSens]
                 ]  1 -2 [ guibutton "" [guisens (+fround $guisens -0.1)]  "val_dec"
                 ]  1 -2 [ guibutton "" [guisens (+fround $guisens  0.1)]  "val_inc"
        ]
        guibar
        guifreerow 18  1 [ guitext "mapmodel browser: " 0
                 ] 16 -2 [ guicheckbox "show model name on hover"  mmguiname
        ]
        guifreerow 18  1 [
                 ] 16 -2 [ guitext "width:"
                 ] 32 -2 [ guislider mmguiwidth 4 12
        ]
        guifreerow 18  1 [
                 ] 16 -2 [ guitext "height:"
                 ] 32 -2 [ guislider mmguiheight 4 12
        ]
        guistrut 1 //guibar
        guifreerow 18  1 [ guitext "texture browser: " 0
                 ] 16 -2 [ guicheckbox "show texture name on hover"  texguiname
        ]
        guifreerow 18  1 [
                 ] 16 -2 [ guitext "width:"
                 ] 32 -2 [ guislider texguiwidth 4 24
        ]
        guifreerow 18  1 [
                 ] 16 -2 [ guitext "height:"
                 ] 32 -2 [ guislider texguiheight 4 24
        ]
        guifreerow 18  1 [
                 ] 16 -2 [ guitext "scale:"
                 ]  1 -2 [ guifield texguiscale 5
                 ]  1 -2 [ guibutton "" [texguiscale (+fround $texguiscale -0.1)]  "val_dec"
                 ]  1 -2 [ guibutton "" [texguiscale (+fround $texguiscale  0.1)]  "val_inc"
        ]
        //guibar
        //guifreerow 18  1 [ guitext "3D: " 0
        //         ] 15 -2 [ guicheckbox "2D menus"  gui2d
        //         ] 15 -2 [ if (= $gui2d 1) [ guicheckbox "^f42D scoreboard"   scoreboard2d ][ guicheckbox "2D scoreboard"   scoreboard2d ] // These behave inconsistently.
        //         ]  1 -2 [ if (= $gui2d 1) [ guicheckbox "^f42D texture browser"  texgui2d ][ guicheckbox "2D texture browser"  texgui2d ] //
        //]
    ]
]


//#############################################################################################################################################
//#############################################################################################################################################
macro guiresbutton [ guibutton "%1x%2" "screenres %1 %2" (if (&& (= $scr_w %1) (= $scr_h %2)) [result "radio_on"] [result "radio_off"]) ]
OPT_TAB_Res = [
    guistayopen [
        guilist [
            guilist [guistrut 16] //  Force Height.
            guilist [
                guistrut 11.6 1
                guialign 0 [guitext "4:3" 0]
                @@@@(guiresbutton 320 240)
                @@@@(guiresbutton 640 480)
                @@@@(guiresbutton 800 600)
                @@@@(guiresbutton 1024 768)
                @@@@(guiresbutton 1152 864)
                @@@@(guiresbutton 1280 960)
                @@@@(guiresbutton 1400 1050)
                @@@@(guiresbutton 1600 1200)
                @@@@(guiresbutton 1792 1344)
                @@@@(guiresbutton 1856 1392)
                @@@@(guiresbutton 1920 1440)
                @@@@(guiresbutton 2048 1536)
                @@@@(guiresbutton 2800 2100)
                @@@@(guiresbutton 3200 2400)
            ]
            guibar
            guilist [
                guistrut 11.6 1
                guialign 0 [guitext "16:10" 0]
                @@@@(guiresbutton 320 200)
                @@@@(guiresbutton 640 400)
                @@@@(guiresbutton 1024 640)
                @@@@(guiresbutton 1280 800)
                @@@@(guiresbutton 1440 900)
                @@@@(guiresbutton 1600 1000)
                @@@@(guiresbutton 1680 1050)
                @@@@(guiresbutton 1920 1200)
                @@@@(guiresbutton 2048 1280)
                @@@@(guiresbutton 2560 1600)
                @@@@(guiresbutton 2880 1800)
                @@@@(guiresbutton 3200 2000)
                @@@@(guiresbutton 3840 2400)
            ]
            guibar
            guilist [
                guistrut 11.6 1
                guialign 0 [guitext "16:9" 0]
                @@@@(guiresbutton 1024 600)
                @@@@(guiresbutton 1280 720)
                @@@@(guiresbutton 1366 768)
                @@@@(guiresbutton 1600 900)
                @@@@(guiresbutton 1920 1080)
                @@@@(guiresbutton 2048 1152)
                @@@@(guiresbutton 2560 1440)
                @@@@(guiresbutton 2880 1620)
                @@@@(guiresbutton 3200 1800)
                @@@@(guiresbutton 3840 2160)
            ]
            guibar
            guilist [
                guistrut 11.6 1
                guialign 0 [guitext "21:9" 0]
                @@@@(guiresbutton 2560 1080)
                @@@@(guiresbutton 3440 1440)
                @@@@(guiresbutton 3840 1600)
                guistrut 1
                guialign 0 [guitext "32:9" 0]
                @@@@(guiresbutton 3840 1080)
                @@@@(guiresbutton 5120 1440)
                guistrut 1
                guialign 0 [guitext "1:1" 0]
                @@@@(guiresbutton  512  512)
                @@@@(guiresbutton  768  768)
                @@@@(guiresbutton 1024 1024)
                @@@@(guiresbutton 2048 2048)
            ]
            guibar
            guilist [
                guistrut 11.6 1
                guialign 0 [guitext "5:4" 0]
                @@@@(guiresbutton 600 480)
                @@@@(guiresbutton 1280 1024)
                @@@@(guiresbutton 1600 1280)
                @@@@(guiresbutton 2560 2048)
                guistrut 1
                guialign 0 [guitext "5:3" 0]
                @@@@(guiresbutton 800 480)
                @@@@(guiresbutton 1280 768)
                guistrut 1
                guibar
                guialign 0 [guitext "Custom" 0]
                guistrut 0.25
                guialign 0 [
                    _MyResX = $scr_w
                    _MyResY = $scr_h
                    guifield _MyResX 4 [scr_w $_MyResX]
                    guitext " " 0
                    guifield _MyResY 4 [scr_h $_MyResY]
                ]
                guistrut 1.5
                guialign 0 [guicheckbox "fullscreen"  fullscreen]
            ]
        ]
    ]
]


//#############################################################################################################################################
//#############################################################################################################################################
OPT_TAB_Sound = [
    guifreerow 20  1 [ guitext "sound volume: " 0
             ] 46 -2 [ guislider soundvol
    ]
    guistrut 0.5
    guifreerow 20  1 [ guitext "simultaneous sounds: " 0  // "max voices"
             ] 46 -2 [ guislider soundchans
    ]
    guistrut 0.25
    guifreerow 20  1 [ guitext "sound frequency: " 0
             ] 46 -2 [ guilistslider soundfreq [11025 22050 44100 48000]
    ]
    guistrut 0.25
    guifreerow 20  1 [ guitext "sound buffer length: " 0
             ] 46 -2 [ guislider soundbufferlen
    ]
    guistrut 0.25
    guifreerow 20  1 [
             ] 46 -2 [ guicheckbox "play sound while minimized" minimizedsounds
    ]
    guibar
    guifreerow 20  1 [ guitext "music volume: " 0
             ] 46 -2 [ guislider musicvol //[if $musicvol [if (! $music) [playsong]] [music ""]]
    ]
    guistrut 0.5
    guifreerow 20  1 [
             ] 23 -2 [ guicheckbox "show nowplaying" playsong_shownowplaying
             ] 23 -2 [ if (> $musicvol 0) [guistayopen [guibutton "play next song" playsong]]
    ]
    guibar
    guifreerow 20 -2 [
             ] 46 -2 [ guicheckbox "mumble positional audio" mumble
    ]
]


//#############################################################################################################################################
//#############################################################################################################################################
OPT_TAB_Cfg = [
    guitext "autoexec.cfg"
    guitexteditor "autoexec.cfg" -63 14
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////



TODO 0 "menus.cfg -- Create AdjustHUD gui.  Combine: ammobar,gameclock,hudscore,etc..."

//  wallclock
//  showfps
//  hudgun
//  hudgunsway
//  muzzleflash
//  muzzlelight
//  swayside
//  swaystep
//  swayup
//
//
//  thirdperson
//  thirdpersondistance
//  thirdpersonside
//  thirdpersonup


//              tabify     What does this do?









newgui "ammobar" [
    guinoautotab [
        guistayopen [
            guicheckbox "AmmoBar" ammobar
            guibar
            guilist [
                guitext "AmmoBarAlign:^t^t^t"
                guiradio "-1 " ammobaralign -1
                guiradio "0 "  ammobaralign  0
                guiradio "1"   ammobaralign  1
            ]

            guicheckbox "AmmoBarHorizontal" ammobarhorizontal

            guicheckbox "AmmoBarFlip" ammobarflip

            guicheckbox "AmmoBarHideEmpty" ammobarhideempty

            guitext "AmmoBarSep:"
            guislider ammobarsep

            guitext "AmmoBarCountSep:"
            guislider ammobarcountsep

            guilist [
                guitext "AmmoBarCountScale:^t "
                guifield ammobarcountscale 5
                guibutton "" [ammobarcountscale (+fround $ammobarcountscale -0.1)]  "val_dec"
                guibutton "" [ammobarcountscale (+fround $ammobarcountscale  0.1)]  "val_inc"
            ]

            guilist [
                guitext "AmmoBarScale:^t^t^t "
                guifield ammobarscale 5
                guibutton "" [ammobarscale (+fround $ammobarscale -0.01)]           "val_dec"
                guibutton "" [ammobarscale (+fround $ammobarscale  0.01)]           "val_inc"
            ]

            guilist [
                guitext "AmmoBarX:^t^t^t "
                guifield ammobarx 5
                guibutton "" [ammobarx (+fround $ammobarx -0.01)]                   "val_dec"
                guibutton "" [ammobarx (+fround $ammobarx  0.01)]                   "val_inc"
            ]
            guilist [
                guitext "AmmoBarY:^t^t^t "
                guifield ammobary 5
                guibutton "" [ammobary (+fround $ammobary -0.01)]                   "val_dec"
                guibutton "" [ammobary (+fround $ammobary  0.01)]                   "val_inc"
            ]
        ]
    ]
] "AmmoBar"


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
newgui "gameclock" [
    guinoautotab [
        guistayopen [
            guicheckbox "GameClock" gameclock
            guibar
            guilist [
                guitext "GameClockAlign:^t"
                guiradio "-1 " gameclockalign -1
                guiradio "0 "  gameclockalign  0
                guiradio "1"   gameclockalign  1
            ]

            guitext "GameClockAlpha:"
            guislider gameclockalpha

            _GameClockColorR = (& (>> $gameclockcolour 16) 0xFF)
            _GameClockColorG = (& (>> $gameclockcolour  8) 0xFF)
            _GameClockColorB = (&     $gameclockcolour     0xFF)
            guilist [guitext "GameClockColour:^t^t"; guicolor $gameclockcolour]
            guislider _GameClockColorR 0 255 [gameclockcolour $_GameClockColorR $_GameClockColorG $_GameClockColorB]
            guislider _GameClockColorG 0 255 [gameclockcolour $_GameClockColorR $_GameClockColorG $_GameClockColorB]
            guislider _GameClockColorB 0 255 [gameclockcolour $_GameClockColorR $_GameClockColorG $_GameClockColorB]

            _GameClockLowColorR = (& (>> $gameclocklowcolour 16) 0xFF)
            _GameClockLowColorG = (& (>> $gameclocklowcolour  8) 0xFF)
            _GameClockLowColorB = (&     $gameclocklowcolour     0xFF)
            guilist [guitext "GameClockLowColour:^t"; guicolor $gameclocklowcolour]
            guislider _GameClockLowColorR 0 255 [gameclocklowcolour $_GameClockLowColorR $_GameClockLowColorG $_GameClockLowColorB]
            guislider _GameClockLowColorG 0 255 [gameclocklowcolour $_GameClockLowColorR $_GameClockLowColorG $_GameClockLowColorB]
            guislider _GameClockLowColorB 0 255 [gameclocklowcolour $_GameClockLowColorR $_GameClockLowColorG $_GameClockLowColorB]

            guilist [
                guitext "GameClockScale:^t "
                guifield gameclockscale 5
                guibutton "" [gameclockscale (+fround $gameclockscale -0.01)]   "val_dec"
                guibutton "" [gameclockscale (+fround $gameclockscale  0.01)]   "val_inc"
            ]

            guilist [
                guitext "GameClockX:^t^t "
                guifield gameclockx 5
                guibutton "" [gameclockx (+fround $gameclockx -0.01)]           "val_dec"
                guibutton "" [gameclockx (+fround $gameclockx  0.01)]           "val_inc"
            ]
            guilist [
                guitext "GameClockY:^t^t "
                guifield gameclocky 5
                guibutton "" [gameclocky (+fround $gameclocky -0.01)]           "val_dec"
                guibutton "" [gameclocky (+fround $gameclocky  0.01)]           "val_inc"
            ]
        ]
    ]
] "GameClock"


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
newgui "hudscore" [
    guinoautotab [
        guistayopen [
            guicheckbox "HudScore" hudscore
            guibar
            guilist [
                guitext "HudScoreAlign:^t^t"
                guiradio "-1 " hudscorealign -1
                guiradio "0 "  hudscorealign  0
                guiradio "1"   hudscorealign  1
            ]

            guitext "HudScoreAlpha:"
            guislider hudscorealpha

            _HudScoreEnemyColorR  = (& (>> $hudscoreenemycolour  16) 0xFF)
            _HudScoreEnemyColorG  = (& (>> $hudscoreenemycolour   8) 0xFF)
            _HudScoreEnemyColorB  = (&     $hudscoreenemycolour      0xFF)
            guilist [guitext "HudScoreEnemyColour:^t"; guicolor $hudscoreenemycolour]
            guislider _HudScoreEnemyColorR 0 255 [hudscoreenemycolour $_HudScoreEnemyColorR $_HudScoreEnemyColorG $_HudScoreEnemyColorB]
            guislider _HudScoreEnemyColorG 0 255 [hudscoreenemycolour $_HudScoreEnemyColorR $_HudScoreEnemyColorG $_HudScoreEnemyColorB]
            guislider _HudScoreEnemyColorB 0 255 [hudscoreenemycolour $_HudScoreEnemyColorR $_HudScoreEnemyColorG $_HudScoreEnemyColorB]

            _HudScorePlayerColorR = (& (>> $hudscoreplayercolour 16) 0xFF)
            _HudScorePlayerColorG = (& (>> $hudscoreplayercolour  8) 0xFF)
            _HudScorePlayerColorB = (&     $hudscoreplayercolour     0xFF)
            guilist [guitext "HudScorePlayerColour:^t"; guicolor $hudscoreplayercolour]
            guislider _HudScorePlayerColorR 0 255 [hudscoreplayercolour $_HudScorePlayerColorR $_HudScorePlayerColorG $_HudScorePlayerColorB]
            guislider _HudScorePlayerColorG 0 255 [hudscoreplayercolour $_HudScorePlayerColorR $_HudScorePlayerColorG $_HudScorePlayerColorB]
            guislider _HudScorePlayerColorB 0 255 [hudscoreplayercolour $_HudScorePlayerColorR $_HudScorePlayerColorG $_HudScorePlayerColorB]

            guitext "HudScoreSep:"
            guislider hudscoresep

            guilist [
                guitext "HudScoreScale:^t^t "
                guifield hudscorescale 5
                guibutton "" [hudscorescale (+fround $hudscorescale -0.01)]  "val_dec"
                guibutton "" [hudscorescale (+fround $hudscorescale  0.01)]  "val_inc"
            ]

            guilist [
                guitext "HudScoreX:^t^t "
                guifield hudscorex 5
                guibutton "" [hudscorex (+fround $hudscorex -0.01)]  "val_dec"
                guibutton "" [hudscorex (+fround $hudscorex  0.01)]  "val_inc"
            ]
            guilist [
                guitext "HudScoreY:^t^t "
                guifield hudscorey 5
                guibutton "" [hudscorey (+fround $hudscorey -0.01)]  "val_dec"
                guibutton "" [hudscorey (+fround $hudscorey  0.01)]  "val_inc"
            ]
        ]
    ]
] "HudScore"


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////













///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
_crosshairs = [
    "data/crosshair.png                   data/hit.png"
    "packages/crosshairs/x.png            packages/crosshairs/x-hit.png"
    "packages/crosshairs/x_dot.png        packages/crosshairs/x_dot-hit.png"
    "packages/crosshairs/o.png            packages/crosshairs/o-hit.png"
    "packages/crosshairs/o_x.png          packages/crosshairs/o_x-hit.png"
    "packages/crosshairs/o_dot.png        packages/crosshairs/o_dot-hit.png"
    "packages/crosshairs/dot.png          packages/crosshairs/dot-hit.png"
    "packages/crosshairs/dot_wide.png     packages/crosshairs/dot_wide-hit.png"
    "packages/crosshairs/star.png         packages/crosshairs/star-hit.png"
    "packages/crosshairs/wide.png         packages/crosshairs/wide-hit.png"
    "packages/crosshairs/circle_dot.png   packages/crosshairs/circle_dot_hit.png"
    "packages/crosshairs/cross_normal.png packages/crosshairs/cross_normal_hit.png"
    "packages/crosshairs/cross_whole.png  packages/crosshairs/cross_whole_hit.png"
    "packages/crosshairs/dot_normal.png   packages/crosshairs/dot_normal_hit.png"
    "packages/crosshairs/dot_whole.png    packages/crosshairs/dot_whole_hit.png"
    "packages/crosshairs/dot2.png         packages/crosshairs/dot2_hit.png"
]
newgui "crosshair" [
    guifreerow 12  1 [ guitext "crosshair size: " 0
             ] 32 -2 [ guislider crosshairsize
    ]
    guistrut 0.5
    guifreerow 12  1 [ guitext "crosshair: " 0
             ] 32 -2 [
        loop iRow (div (+ (listlen $_crosshairs) 6) 8) [
            guilist [
                loop iCol 8 [
                    _ch = (at $_crosshairs (+ (* $iRow 8) $iCol))
                    if (!=s $_ch "") [
                        guiimage (at $_ch 0) [
                            cleargui 1
                            loadcrosshair @(at $_ch 0) 0
                            loadcrosshair @(at $_ch (if (> (listlen $_ch) 1) 1 0)) 2
                        ] 1
                    ]
                ]
            ]
        ]
    ]
]
TODO 0 "menus.cfg -- Remake all crosshairs with alpha & outlines?"

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
newgui "postfx" [
    guibutton "(effect OFF)"            [clearpostfx]
    guibutton "bloom (subtle: 30%)"     [bloom 0.3]
    guibutton "bloom (bright: 55%)"     [bloom 0.55]
    guibutton "bloom (intense: 80%)"    [bloom 0.8]
    guibutton "rotoscope"               [rotoscope 1]
    guibutton "rotoscope + blur3"       [rotoscope 1 1]
    guibutton "rotoscope + blur5"       [rotoscope 1 2]
    guibutton "sobel"                   [setpostfx sobel]
    guibutton "invert"                  [setpostfx invert]
    guibutton "gbr"                     [setpostfx gbr]
    guibutton "bw"                      [setpostfx bw]
    guibutton "blur3"                   [setpostfx hblur3; addpostfx vblur3]
    guibutton "blur5"                   [setpostfx hblur5; addpostfx vblur5]
]
TODO 0 "menus.cfg -- 'CleanUp' HSV shader?"

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Text Editor:
guitexteditor = [
    guinoautotab [
        guieditor $arg1 $arg2 $arg3
        textinit $arg1 $arg1
        guistayopen [
            guialign 0 [// H
                guilist [guistrut 9.25 1; guibutton "load"   [textfocus @arg1; textload @arg1]]
                guilist [guistrut 9.25 1; guibutton "save"   [textfocus @arg1; textsave @arg1]]
                guilist [guistrut 9.25 1; guibutton "exec"   [textfocus @arg1; textexec      ]]
                guilist [guistrut 9.25 1; guibutton "copy"   [textfocus @arg1; textcopy      ]]
                guilist [guistrut 9.25 1; guibutton "paste"  [textfocus @arg1; textpaste     ]]
                guilist [guistrut 9.25 1; guibutton "select" [textfocus @arg1; textselectall ]]
                guilist [guistrut 9.25 1; guibutton "clear"  [textfocus @arg1; textclear     ]]
            ]
        ]
    ]
]

//#############################################################################################################################################
//#############################################################################################################################################
nodebug [
    defsvarp notepadfile "untitled.txt"
]

newgui "notepad" [
    guifield notepadfile -30
    guitexteditor $notepadfile -80 20
]

//#############################################################################################################################################
newgui "pastebuffer" [
    guinoautotab [
        guieditor "#pastebuffer" -80 20
        guistayopen [
            guialign 0 [
                guilist [guistrut 9.25 1; guibutton "exec"  [textfocus "#pastebuffer"; textexec ]]
                guilist [guistrut 9.25 1; guibutton "clear" [textfocus "#pastebuffer"; textclear]]
            ]
        ]
    ]
]

//#############################################################################################################################################
newgui "scratchpad" [
    guinoautotab [
        guieditor "#scratchpad" -80 20
        guistayopen [
            guialign 0 [
                guilist [guistrut 9.25 1; guibutton "exec"   [textfocus "#scratchpad"; textexec     ]]
                guilist [guistrut 9.25 1; guibutton "copy"   [textfocus "#scratchpad"; textcopy     ]]
                guilist [guistrut 9.25 1; guibutton "paste"  [textfocus "#scratchpad"; textpaste    ]]
                guilist [guistrut 9.25 1; guibutton "select" [textfocus "#scratchpad"; textselectall]]
                guilist [guistrut 9.25 1; guibutton "clear"  [textfocus "#scratchpad"; textclear    ]]
            ]
        ]
    ]
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  Editing:

nodebug [
    defvar  EDT_Width  1 76 -1 // in units of (fontscale Width )
    defvar  EDT_Height 1 19 -1 // in units of (fontscale Height)

    defvar  EDT_SunLightR     0 0 255
    defvar  EDT_SunLightG     0 0 255
    defvar  EDT_SunLightB     0 0 255
    defvar  EDT_SkyLightR     0 0 255
    defvar  EDT_SkyLightG     0 0 255
    defvar  EDT_SkyLightB     0 0 255
    defvar  EDT_AmbientR      0 0 255
    defvar  EDT_AmbientG      0 0 255
    defvar  EDT_AmbientB      0 0 255

    defvar  EDT_SkyboxColorR  0 0 255
    defvar  EDT_SkyboxColorG  0 0 255
    defvar  EDT_SkyboxColorB  0 0 255

    defvar  EDT_CloudColorR   0 0 255
    defvar  EDT_CloudColorG   0 0 255
    defvar  EDT_CloudColorB   0 0 255

    defvar  EDT_FogColorR     0 0 255
    defvar  EDT_FogColorG     0 0 255
    defvar  EDT_FogColorB     0 0 255
    defvar  EDT_FogDomeColorR 0 0 255
    defvar  EDT_FogDomeColorG 0 0 255
    defvar  EDT_FogDomeColorB 0 0 255

    defvar  EDT_NewMap_Size  10 10 16
    defsvar EDT_SaveMap_Name "untitled"
]

newgui "editing" [
    guikeeptab 1
    guinoautotab [
        // Save the frames 'guitabnum' hack:             H                                 V
                               if (= $guitabnum  1) [ guilist [guistrut $EDT_Height 1; guilist [guistrut $EDT_Width 1; EDT_TAB_Editing      ]] ]
        guitab "ents"        ; if (= $guitabnum  2) [ guilist [guistrut $EDT_Height 1; guilist [guistrut $EDT_Width 1; EDT_TAB_Entities     ]] ]
        guitab "lighting"    ; if (= $guitabnum  3) [ guilist [guistrut $EDT_Height 1; guilist [guistrut $EDT_Width 1; EDT_TAB_Lighting     ]] ]
        guitab "skybox"      ; if (= $guitabnum  4) [ guilist [guistrut $EDT_Height 1; guilist [guistrut $EDT_Width 1; EDT_TAB_Skybox       ]] ]
        guitab "sky"         ; if (= $guitabnum  5) [ guilist [guistrut $EDT_Height 1; guilist [guistrut $EDT_Width 1; EDT_TAB_Sky          ]] ]
        guitab "cloudlayer"  ; if (= $guitabnum  6) [ guilist [guistrut $EDT_Height 1; guilist [guistrut $EDT_Width 1; EDT_TAB_Cloudlayer   ]] ]
        guitab "fog"         ; if (= $guitabnum  7) [ guilist [guistrut $EDT_Height 1; guilist [guistrut $EDT_Width 1; EDT_TAB_Fog          ]] ]
        guitab "height/blend"; if (= $guitabnum  8) [ guilist [guistrut $EDT_Height 1; guilist [guistrut $EDT_Width 1; EDT_TAB_Height_Blend ]] ]
        guitab "map"         ; if (= $guitabnum  9) [ guilist [guistrut $EDT_Height 1; guilist [guistrut $EDT_Width 1; EDT_TAB_Map          ]] ]
        guitab "cfg"         ; if (= $guitabnum 10) [ guilist [guistrut $EDT_Height 1; guilist [guistrut $EDT_Width 1; EDT_TAB_Cfg          ]] ]
    ]
] "editing"


//#############################################################################################################################################
//#############################################################################################################################################
EDT_TAB_Editing = [
    guibutton "toggle edit mode^t^t^f2[E]"  [edittoggle]
    guibar //-------------------------------------------------------------------------
    guilist [
        guilist [
            guistrut 38 1
            guitext "select^t^t^t^t^f2[MouseLeft ^f6drag^f2]"
            guitext "extend selection^t^t^f2[MouseRight]"
            guitext "move selection^t^t^t^f2[MouseRight ^f6drag^f2]"
            guitext "deselect^t^t^t^t^f2[SPACE]"
            guitext "select all ents in box^t^f2[NumPad-ENTER]"
        ]
        guilist [
            guitext "select corners^t^t^t^f2[MouseMiddle ^f6drag^f2]"
            guitext "reorient^t^t^t^t^f2[MouseRight]"
            guitext "passthrough^t^t^t^f2[LCTRL ^f6hold^f2]"
            guitext "center view on ent^t^t^f2[, + MouseWheel]"
            guitext "select all matching ents^t^f2[N]"
        ]
    ]
    guibar //-------------------------------------------------------------------------
    guilist [
        guilist [
            guistrut 38 1
            guitext "push/pull selection^t^t^f2[MouseWheel]"
            guitext "change gridsize^t^t^f2[G + MouseWheel]"
            guitext "mirror on selected axis^t^f2[X]"
        ]
        guilist [
            guitext "push cube face^t^t^f2[F + MouseWheel]"
            guitext "push corner with cursor^t^f2[Q + MouseWheel]"
            guitext "rotate^t^t^t^t^f2[R + MouseWheel]"
        ]
    ]
    guibar //-------------------------------------------------------------------------
    guilist [
        guilist [
            guistrut 38 1
            guitext "undo^t^t^t^t^t^f2[U] [Z]"
            guitext "copy^t^t^t^t^t^f2[C]"
            guitext "delete^t^t^t^t^f2[DEL]"
        ]
        guilist [
            guitext "redo^t^t^t^t^t^f2[I]"
            guitext "paste^t^t^t^t^f2[V]"
        ]
    ]
    guibar //-------------------------------------------------------------------------
    guilist [
        guilist [
            guistrut 38 1
            guitext     "change textures^t^t^f2[Y + MouseWheel]"
            guicheckbox "texture all faces^t^t^f2[0]"   allfaces
            guicheckbox "toggle outline^t^t^t^f2[7]"    outline
        ]
        guilist [
            guibutton   "texture browser..^t^t^f2[F2]"  [showtexgui]              "menu"
            guibutton   "materials.."                   [showgui "materials"]
            guicheckbox "toggle wireframe^t^t^f2[8]"    [wireframe]
        ]
    ]
]


//#############################################################################################################################################
EDT_TAB_Entities = [
    guilist [
        guilist [
            guistrut 38 1
            guibutton   "edit selected ent..^t^f2[F3]"              showentgui
            guicheckbox "entity snap-to-grid^t^f2[6]"               entselsnap
            guicheckbox "new entities take yaw from camera"         entcamdir
        ]
        guilist [
            guibutton   "edit selected entity in console^t^f2[.]"    selentedit
            guicheckbox "entity selection^t^t^f2[9]"                 myentediting //entediting
        ]
    ]
    guibar //-------------------------------------------------------------------------
    guilist [
        guilist [
            guistrut 12 1
            guitext "^f2/entdrop *"
        ]
        guilist [
            guistrut 26 1
            guiradio "(2) at selection"                       entdrop 2
            guiradio "(3) at selection, to the floor"         entdrop 3
        ]
        guilist [
            guiradio "(0) at current position"                entdrop 0
            guiradio "(1) at current position, to the floor"  entdrop 1
        ]
    ]
    guibar //-------------------------------------------------------------------------
    guilist [
        guilist [
            guistrut 12 1
            guitext "^f2/newent *"
        ]
        guilist [
            guistrut 22 1
            guibutton "light.."             [showgui "newlight"]
            guibutton "mapmodel..  ^f2[F4]" [showgui "mapmodels"]
            guibutton "teleport.."          [showgui "newteleport"]
        ]
        guilist [
            guistrut 22 1
            guibutton "playerstart"         [newent playerstart]
            guibutton "playerstart 1"       [newent playerstart 1]
            guibutton "playerstart 2"       [newent playerstart 2]
         ]
        guilist [
            guibutton "base"                [newent base]
            guibutton "flag 1"              [newent flag 1]
            guibutton "flag 2"              [newent flag 2]
        ]
    ]
    guistrut 1
    guilist [
        guilist [
            guistrut 12 1
            guialign 1 [guitext "pickups: " 0]
        ]
        guilist [
            guistrut 22 1
            guibutton "shells"        [newent shells]
            guibutton "bullets"       [newent bullets]
            guibutton "rockets"       [newent rockets]
            guibutton "riflerounds"   [newent riflerounds]
            guibutton "grenades"      [newent grenades]
            guibutton "cartridges"    [newent cartridges]
        ]
        guilist [
            guibutton "health"        [newent health]
            guibutton "healthboost"   [newent healthboost]
            guibutton "greenarmour"   [newent greenarmour]
            guibutton "yellowarmour"  [newent yellowarmour]
            guibutton "quaddamage"    [newent quaddamage]
            guibutton "select all pickup ents  ^f2/entfindpickups"   [entfindpickups]
        ]
    ]
    guistrut 1
    guilist [
        guilist [
            guistrut 12 1
            guialign 1 [guitext "SP mode: " 0]
        ]
        guilist [
            guistrut 22 1
            guibutton "monster.."  [showgui "monsters"]
        ]
        guilist [
            guistrut 22 1
            guibutton "trigger.."  [showgui "triggers"]
        ]
    ]
]


//#############################################################################################################################################
EDT_TAB_Lighting = [
    guibutton "lighting..^t^f2/adjust_lighting"  [showgui "adjust_lighting"]
    guibar //-------------------------------------------------------------------------
    EDT_LightingStuff
]


newgui "adjust_lighting" [
    //=========================================================================================================================
    guinoautotab [
        guibutton "Get sun pitch & yaw from camera.  ^f2/getsundir" [getsundir]
        guitext "sunlightpitch: "; guislider sunlightpitch
        guitext "sunlightyaw: "  ; guislider sunlightyaw
        guibar //-------------------------------------------------------------------------
        EDT_SunLightR (& (>> $sunlight 16) 0xFF)
        EDT_SunLightG (& (>> $sunlight  8) 0xFF)
        EDT_SunLightB (&     $sunlight     0xFF)
        guilist [guitext "sunlightscale: "; guifield sunlightscale 8]
        guilist [guitext "sunlight:^t^t^t^t^t^t^t^t"; guicolor $sunlight]
        guislider EDT_SunLightR 0 255 [sunlight $EDT_SunLightR $EDT_SunLightG $EDT_SunLightB]
        guislider EDT_SunLightG 0 255 [sunlight $EDT_SunLightR $EDT_SunLightG $EDT_SunLightB]
        guislider EDT_SunLightB 0 255 [sunlight $EDT_SunLightR $EDT_SunLightG $EDT_SunLightB]
        guibar //-------------------------------------------------------------------------
        EDT_SkyLightR (& (>> $skylight 16) 0xFF)
        EDT_SkyLightG (& (>> $skylight  8) 0xFF)
        EDT_SkyLightB (&     $skylight     0xFF)
        guilist [guitext "skylight:^t^t^t^t^t^t^t^t"; guicolor $skylight]
        guislider EDT_SkyLightR 0 255 [skylight $EDT_SkyLightR $EDT_SkyLightG $EDT_SkyLightB]
        guislider EDT_SkyLightG 0 255 [skylight $EDT_SkyLightR $EDT_SkyLightG $EDT_SkyLightB]
        guislider EDT_SkyLightB 0 255 [skylight $EDT_SkyLightR $EDT_SkyLightG $EDT_SkyLightB]
        guibar //-------------------------------------------------------------------------
        EDT_AmbientR (& (>> $ambient  16) 0xFF)
        EDT_AmbientG (& (>> $ambient   8) 0xFF)
        EDT_AmbientB (&     $ambient      0xFF)
        guilist [guitext "ambient:^t^t^t^t^t^t^t^t"; guicolor $ambient]
        guislider EDT_AmbientR 0 255 [ambient $EDT_AmbientR $EDT_AmbientG $EDT_AmbientB]
        guislider EDT_AmbientG 0 255 [ambient $EDT_AmbientR $EDT_AmbientG $EDT_AmbientB]
        guislider EDT_AmbientB 0 255 [ambient $EDT_AmbientR $EDT_AmbientG $EDT_AmbientB]
    //=========================================================================================================================
    guitab "settings"
        EDT_LightingStuff
    ]
] "sunlight"

TODO 0 "menus.cfg -- Additional LightingStuff?"
//  fullbrightlevel

//  clearlightmaps
//  edgetolerance
//  adaptivesample

//  roundlightmaptex ???
//  dumplms

//  lerpangle
//  lerpsubdiv
//  lerpsubdivsize
//  lerptjoints

//  lmaa
//  lmshadows  0: none  1: Geo only  2: Geo + Mapmodels

//  maxmerge

//  CalcLight -1: lmshadows = 1  ; lmaa = 0  ; lerptjoints = 0
//  CalcLight  0: lmshadows = VAR; lmaa = VAR; lerptjoints = VAR
//  CalcLight  1: lmshadows = 2  ; lmaa = 3  ; lerptjoints = 1

EDT_LightingStuff = [
    guicheckbox "fullbright^t^t^f2[L]" [fullbright]
    guibar //-------------------------------------------------------------------------
    guilist [
        guitext "blurskylight:^t"
        guiradio "0 " blurskylight 0
        guiradio "1 " blurskylight 1
        guiradio "2 " blurskylight 2
    ]
    guilist [
        guitext "blurlightmaps:^t"
        guiradio "0 " blurlms 0
        guiradio "1 " blurlms 1
        guiradio "2 " blurlms 2
    ]
    guistrut 1
    guitext "lightprecision:"
    guilistslider lightprecision [8 16 32 48 64 96 128 256]
    guitext "lightlod: (>= use lightprecision, < use lightprecision/2)"
    guislider lightlod
    guistrut 1
    guitext "lighterror:"
    guislider lighterror
    guibar //-------------------------------------------------------------------------
    guibutton "calclight^t1^t^f~(slow: 8xAA)"                       [calclight  1]
    guibutton "calclight^t-1^t^f~(quick: no AA, no model shadows)"  [calclight -1]
    guitext "lightthreads:"
    guislider lightthreads
]


//#############################################################################################################################################
EDT_TAB_Skybox = [
    guilist [
        guistayopen [
            guilist [_genskyitems $_skies1]
            guilist [_genskyitems $_skies2]
            guilist [_genskyitems $_skies3]
            _skyboxpreview
        ]
    ]
]
_genskyitems = [
    looplist iSky $arg1 [
        guibutton $iSky (concat skybox $iSky)
    ]
]
_skyboxpreview = [
    guibar
    _skyshotname = (checkrolloveraction "skybox " [if (> $numargs 0) [result $arg1] [at $guirollovername 0]])
    guiimage (concatword "<thumbnail:128>packages/" $_skyshotname "_ft") (checkrolloveraction "skybox ") 4 1 "data/cube.png" (chopstr $_skyshotname -16 "..")
]


TODO 0 "menus.cfg -- Make skybox a list type menu, sepearate from editing menus.  Generalize 'guimaplist'?"

_skies1 = [
    skyboxes/black
    skyboxes/white
    aftas/sky/orbe
    blindabuser/blindasky
    blindabuser/blindasky2
    darc/forge
    dash/moon
    hazel/green
    humus/meadow
    ik2k/env/iklake
    jon/prarie
    mayhem/duomo
    mayhem/eclipse
    meister/uzze
    penguins/arid
    penguins/arid2
    penguins/cocoa
    penguins/divine
    penguins/harmony
]
_skies2 = [
    penguins/mellow
    penguins/mystic
    penguins/violence
    penguins/wrath
    penguins/yonder
    penguins/zeus
    ratboy/skyboxes/coward
    shmutzwurst/london
    skyboxes/bbk/d
    skyboxes/bbk/dark2
    skyboxes/bbk/n1
    skyboxes/bbk/t
    skyboxes/clearsky052
    skyboxes/darkness
    skyboxes/evilsky
    skyboxes/morning
    skyboxes/remus/sky01
    skyboxes/turbulent
    skyboxes/ulukai/corona
]
_skies3 = [
    socksky/barren
    socksky/desert
    socksky/devpun
    socksky/emerald
    socksky/frozen
    socksky/frozendusk
    socksky/grave
    socksky/mars
    socksky/nightball
    socksky/stratosphere
    socksky/valley
    staffy/staffy
]


//#############################################################################################################################################
EDT_TAB_Sky = [
    guistayopen [
        EDT_SkyboxColorR (& (>> $skyboxcolour 16) 0xFF)
        EDT_SkyboxColorG (& (>> $skyboxcolour  8) 0xFF)
        EDT_SkyboxColorB (&     $skyboxcolour     0xFF)
        guilist [
            guilist [
                guistrut 18 1
                guialign 1 [guitext "skyboxcolour: " 0]
                guistrut 1
                guialign 1 [guicolor $skyboxcolour; guistrut 1]
            ]
            guilist [
                guistrut 57 1
                guislider EDT_SkyboxColorR 0 255 [skyboxcolour $EDT_SkyboxColorR $EDT_SkyboxColorG $EDT_SkyboxColorB]
                guislider EDT_SkyboxColorG 0 255 [skyboxcolour $EDT_SkyboxColorR $EDT_SkyboxColorG $EDT_SkyboxColorB]
                guislider EDT_SkyboxColorB 0 255 [skyboxcolour $EDT_SkyboxColorR $EDT_SkyboxColorG $EDT_SkyboxColorB]
            ]
        ]
        guistrut 1 //-------------------------------------------------------------------------
        guifreerow 18  1 [ guitext "rotation: " 0
                 ] 12 -2 [ guitext "yawsky:"
                 ] 45 -2 [ guislider yawsky
        ]
        guistrut 1 //-------------------------------------------------------------------------
        guifreerow 18  1 [ guitext "animation: " 0
                 ]  1 -2 [ guifield2 "spinsky:" 12 spinsky 6 -0.1 0.1
        ]
    ]


    //@@    Do atmo stuff...


]
TODO 0 "menus.cfg -- editing/sky: Atmo, CloudBox?"


//#############################################################################################################################################
EDT_TAB_Cloudlayer = [
    guistayopen [
        guilist [
            guilist [
                guistrut 18 1
                guialign 1 [guitext "cloudlayer: " 0]
            ]
            guilist [
                guistrut 28 1
                guibutton "skyboxes/clouds01.png"  [cloudlayer "skyboxes/clouds01.png"]
                guibutton "skyboxes/clouds02.png"  [cloudlayer "skyboxes/clouds02.png"]
                guibutton "skyboxes/clouds03.png"  [cloudlayer "skyboxes/clouds03.png"]
                guibutton "skyboxes/white_bk.jpg"  [cloudlayer "skyboxes/white_bk.jpg"]
                guibutton "^f8*no clouds*"         [cloudlayer ""]
            ]
            guilist [
                guibutton "blindabuser/blindaclouds.png"  [cloudlayer "blindabuser/blindaclouds.png"]
                guibutton "skyboxes/cloud_ring.png"       [cloudlayer "skyboxes/cloud_ring.png"]
                guibutton "textures/razgriz/cloud03.png"  [cloudlayer "textures/razgriz/cloud03.png"]
                guibutton "textures/razgriz/cloud04.png"  [cloudlayer "textures/razgriz/cloud04.png"]
            ]
        ]
        guistrut 1 //-------------------------------------------------------------------------
        EDT_CloudColorR (& (>> $cloudcolour 16) 0xFF)
        EDT_CloudColorG (& (>> $cloudcolour  8) 0xFF)
        EDT_CloudColorB (&     $cloudcolour     0xFF)
        guilist [
            guilist [
                guistrut 18 1
                guialign 1 [guitext "cloudcolour: " 0]
                guistrut 1
                guialign 1 [guicolor $cloudcolour; guistrut 1]
            ]
            guilist [
                guistrut 57 1
                guislider EDT_CloudColorR 0 255 [cloudcolour $EDT_CloudColorR $EDT_CloudColorG $EDT_CloudColorB]
                guislider EDT_CloudColorG 0 255 [cloudcolour $EDT_CloudColorR $EDT_CloudColorG $EDT_CloudColorB]
                guislider EDT_CloudColorB 0 255 [cloudcolour $EDT_CloudColorR $EDT_CloudColorG $EDT_CloudColorB]
            ]
        ]
        guistrut 1 //-------------------------------------------------------------------------
        guifreerow 18  1 [ guitext "transparency: " 0
                 ] 28 -2 [ guifield2 "cloudalpha:"     15.5  cloudalpha     6  -0.05 0.05
                 ] 28 -2 [ guifield2 "cloudfade:"      15.5  cloudfade      6  -0.05 0.05
        ]
        guistrut 1 //-------------------------------------------------------------------------
        guifreerow 18  1 [ guitext "position/scale: " 0
                 ] 28 -2 [ guifield2 "cloudoffsetx:"   15.5  cloudoffsetx   6  -0.01 0.01
                 ] 28 -2 [ guifield2 "cloudheight:"    15.5  cloudheight    6  -0.01 0.01
        ]
        guifreerow 18 -2 [
                 ] 28 -2 [ guifield2 "cloudoffsety:"   15.5  cloudoffsety   6  -0.01   0.01
                 ] 28 -2 [ guifield2 "cloudscale:"     15.5  cloudscale     6  -0.0625 0.0625
        ]
        guistrut 1 //-------------------------------------------------------------------------
        guifreerow 18  1 [ guitext "animation: " 0
                 ] 28 -2 [ guifield2 "cloudscrollx:"   15.5  cloudscrollx   6  -0.0005 0.0005
                 ] 28 -2 [ guifield2 "spincloudlayer:" 15.5  spincloudlayer 6  -0.1    0.1
        ]
        guifreerow 18 -2 [
                 ] 28 -2 [ guifield2 "cloudscrolly:"   15.5  cloudscrolly   6  -0.0005 0.0005
        ]
    ]
]


//#############################################################################################################################################
EDT_TAB_Fog = [
    guistayopen [
        guifreerow 18  1 [ guitext "fog: " 0
                 ] 57 -2 [ guislider fog 128 16384
        ]
        guistrut 1
        EDT_FogColorR (& (>> $fogcolour 16) 0xFF)
        EDT_FogColorG (& (>> $fogcolour  8) 0xFF)
        EDT_FogColorB (&     $fogcolour     0xFF)
        guilist [// H
            guilist [// V
                guistrut 18 1
                guialign 1 [guitext "fogcolour: " 0]
                guistrut 1
                guialign 1 [guicolor $fogcolour; guistrut 1]
            ]
            guilist [// V
                guistrut 57 1
                guislider EDT_FogColorR 0 255 [fogcolour $EDT_FogColorR $EDT_FogColorG $EDT_FogColorB]
                guislider EDT_FogColorG 0 255 [fogcolour $EDT_FogColorR $EDT_FogColorG $EDT_FogColorB]
                guislider EDT_FogColorB 0 255 [fogcolour $EDT_FogColorR $EDT_FogColorG $EDT_FogColorB]
            ]
        ]
        guibar //-------------------------------------------------------------------------
        EDT_FogDomeColorR (& (>> $fogdomecolour 16) 0xFF)
        EDT_FogDomeColorG (& (>> $fogdomecolour  8) 0xFF)
        EDT_FogDomeColorB (&     $fogdomecolour     0xFF)
        guilist [// H
            guilist [// V
                guistrut 18 1
                guialign 1 [guitext "fogdomecolour: " 0]
                guialign 1 [guitext "^f4(0 = use fogcolour) " 0]
                guialign 1 [guicolor $fogdomecolour; guistrut 1]
            ]
            guilist [// V
                guistrut 57 1
                guislider EDT_FogDomeColorR 0 255 [fogdomecolour $EDT_FogDomeColorR $EDT_FogDomeColorG $EDT_FogDomeColorB]
                guislider EDT_FogDomeColorG 0 255 [fogdomecolour $EDT_FogDomeColorR $EDT_FogDomeColorG $EDT_FogDomeColorB]
                guislider EDT_FogDomeColorB 0 255 [fogdomecolour $EDT_FogDomeColorR $EDT_FogDomeColorG $EDT_FogDomeColorB]
            ]
        ]
        guistrut 1 //-------------------------------------------------------------------------
        guilist [// H
            guilist [// V
                guistrut 18 1
            ]
            guilist [// V
                guistrut 28 1
                guifield2 "fogdomeclip:"   15.5  fogdomeclip   6  -0.01 0.01
                guifield2 "fogdomemin:"    15.5  fogdomemin    6  -0.01 0.01
                guifield2 "fogdomemax:"    15.5  fogdomemax    6  -0.01 0.01
                guifield2 "fogdomeheight:" 15.5  fogdomeheight 6  -0.01 0.01
            ]
            guilist [// V
                guicheckbox "fogdomeclouds" fogdomeclouds
                guistrut 0.5
                guicheckbox "fogdomesquare" fogdomesquare
                guistrut 0.5
                guicheckbox "fogdomecap"    fogdomecap
            ]
        ]
    ]
]


//#############################################################################################################################################
EDT_TAB_Height_Blend = [
    guilist [// H
        guilist [// V
            guistrut 38 1
            guitext "toggle heightmap mode^t^t^f2[H]"
            guitext "push/pull heightmap^t^t^t^f2[MouseWheel]"
        ]
        guitext "select heightmap brush^t^f2[B + MouseWheel]"
    ]
    guibar //-------------------------------------------------------------------------
    guitext "blend paint mode:"
    guilist [// H
        guilist [// V
            guistrut 38 1
            guiradio (concatword (at $_Labels_BlendPaintMode 0) "^t^t^t^t^t^t^f2[NumPad 0]")    blendpaintmode 0
            guiradio (concatword (at $_Labels_BlendPaintMode 2) "^t^t^t^t^t^t^f2[NumPad 2]")    blendpaintmode 2
            guiradio (concatword (at $_Labels_BlendPaintMode 4) "^t^t^t^t^f2[NumPad 4]"    )    blendpaintmode 4
        ]
        guilist [// V
            guiradio (concatword (at $_Labels_BlendPaintMode 1) "^t^t^t^t^t^f2[NumPad 1]"  )    blendpaintmode 1
            guiradio (concatword (at $_Labels_BlendPaintMode 3) "^t^t^t^t^f2[NumPad 3]"    )    blendpaintmode 3
            guiradio (concatword (at $_Labels_BlendPaintMode 5) "^t^t^t^t^t^f2[NumPad 5]"  )    blendpaintmode 5
        ]
    ]
    guistrut 1
    guitext "paint blendmap^t^t^t^f2[MouseLeft]"
    guitext "change blend brush^t^t^t^f2[MouseWheel]"
    guitext "rotate blend brush^t^t^t^f2[MouseRight]"
    guistrut 1
    guibutton "clear entire blendmap^t^t^f2/clearblendmap"        [clearblendmap]
    guibutton "clear blendmap in selection^t^f2/clearblendmapsel" [clearblendmapsel]
]


//#############################################################################################################################################
EDT_TAB_Map = [
    guifreerow 18  1 [ guitext "newmap: " 0
             ] 12 -2 [ guitext "scale:"
             ] 45 -2 [ guislider EDT_NewMap_Size 10 16
    ]
    guifreerow 18 -2 [
             ] 12 -2 [ guitext "size:"
             ] 45 -2 [ guinameslider EDT_NewMap_Size ["1024" "2048" "4096" "8192" "16384" "32768" "65536"] [10 11 12 13 14 15 16] []
    ]
    guifreerow 18 -2 [
             ]  1 -2 [ guibutton "create newmap" [newmap $EDT_NewMap_Size]
    ]
    guistrut 1 //-------------------------------------------------------------------------
    guifreerow 18  1 [ guitext "^f3loadmap: " 0
             ] 24 -2 [ guibutton "^f3offical maps" [showgui "maps"] "menu"
             ]  1 -2 [ guibutton "^f3custom maps"   showcustommaps  "menu"
    ]
    guistrut 1 //-------------------------------------------------------------------------
    guifreerow 18  1 [ guitext "savemap: " 0
             ] 24 -2 [ guifield  EDT_SaveMap_Name 22
             ]  1 -2 [ guibutton "save" [savemap $EDT_SaveMap_Name]
    ]
    guibar //-------------------------------------------------------------------------
    guifreerow 18 -2 [
             ]  1 -2 [ guibutton "set map title/author"  [saycommand [/maptitle @maptitle]]
    ]
    guibar //-------------------------------------------------------------------------
    guifreerow 18 -2 [
             ] 24 -2 [ guibutton "^f2/remip"   [remip]
             ]  1 -2 [ guitext "^f4(optimize octree/geometry)" 0
    ]
    guifreerow 18 -2 [
             ] 24 -2 [ guibutton "^f2/recalc"  [recalc]
             ]  1 -2 [ guitext "^f4(regenerate: envmaps, minimap, etc)" 0
    ]
    guibar //-------------------------------------------------------------------------
    guifreerow 18  1 [ guitext "resize: " 0
             ]  1 -2 [ guibutton "increase mapsize (2x)"  mapenlarge
    ]
    guifreerow 18 -2 [
             ] 24 -2 [ guibutton "decrease mapsize (1/2)"  shrinkmap
             ]  1 -2 [ guitext "^f4(shrunk area must be empty)" 0
    ]
    guibar //-------------------------------------------------------------------------
    guifreerow 18  1 [ guitext "undo size: " 0
             ] 57 -2 [ guilistslider undomegs [1 2 3 4 6 8 12 16 20 24 32 40 48 56 64 72 80 88 96 100]
    ]
    guifreerow 18 -2 [
             ] 24 -2 [ guitext "^f4(in megabytes)" 0
             ] 33  1 [ guitext "^f4(default 8)" 0
    ]
]


//#############################################################################################################################################
EDT_TAB_Cfg = [
    guitext (mapcfgname)
    guitexteditor (mapcfgname) -73 17
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


TODO 0 "menus.cfg -- Rework all this editing stuff?"


newgui "materials" [
    guibutton "air"                        [editmat air]
    guibutton "water"                      [editmat water]
    guibutton "lava"                       [editmat lava]
    guibutton "clip"                       [editmat clip]
    guibutton "glass"                      [editmat glass]
    guibutton "noclip"                     [editmat noclip]
    guibutton "gameclip"                   [editmat gameclip]
    guibutton "death"                      [editmat death]
    guibutton "alpha"                      [editmat alpha]

    guicheckbox "show material volumes"    [showmat]

    //guibutton "waterlod very high (3)"  [waterlod 3]
    //guibutton "waterlod high      (2)"  [waterlod 2]
    //guibutton "waterlod default   (1)"  [waterlod 1]
    //guibutton "waterlod low       (0)"  [waterlod 0]

    //guibutton "watersubdiv very fine (0)"  [watersubdiv 0]
    //guibutton "watersubdiv fine      (1)"  [watersubdiv 1]
    //guibutton "watersubdiv default   (2)"  [watersubdiv 2]
    //guibutton "watersubdiv coarse    (3)"  [watersubdiv 3]

    //guibutton "water waves on"  [waterwaves 1]
    //guibutton "water waves off" [waterwaves 0]
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
newgui "triggers" [
    guibutton "invisible"   [BTmm = -1; showgui "trigger2"]
    guibutton "a carrot"    [BTmm = 23; showgui "trigger2"]
    guibutton "switch #1"   [BTmm = 24; showgui "trigger2"]
    guibutton "switch #2"   [BTmm = 25; showgui "trigger2"]
    guibutton "door #1"     [BTmm = 26; showgui "trigger2"]
    guibutton "door #2"     [BTmm = 27; showgui "trigger2"]
    guibutton "door #3"     [BTmm = 28; showgui "trigger2"]
]

newgui "trigger2" [
    guibutton "animation loop"                      [BTtt =  0; showgui "trigger3"]
    guibutton "trigger once (popback)"              [BTtt =  1; showgui "trigger3"]
    guibutton "trigger once (popback & rumble)"     [BTtt =  2; showgui "trigger3"]
    guibutton "trigger once"                        [BTtt =  3; showgui "trigger3"]
    guibutton "trigger once (rumble)"               [BTtt =  4; showgui "trigger3"]
    guibutton "trigger multiple (popback)"          [BTtt =  5; showgui "trigger3"]
    guibutton "trigger multiple (popback & rumble)" [BTtt =  6; showgui "trigger3"]
    guibutton "trigger multiple"                    [BTtt =  7; showgui "trigger3"]
    guibutton "trigger multiple (rumble)"           [BTtt =  8; showgui "trigger3"]
    guibutton "door (open once)"                    [BTtt =  9; showgui "trigger3"]
    guibutton "door (auto close)"                   [BTtt = 10; showgui "trigger3"]
    guibutton "door (locked)"                       [BTtt = 11; showgui "trigger3"]
    guibutton "vanishing trigger"                   [BTtt = 12; showgui "trigger3"]
    guibutton "vanishing trigger (rumble)"          [BTtt = 13; showgui "trigger3"]
    guibutton "End Of Level Trigger"                [BTtt = 29; showgui "trigger3"]
]

build_trigger = [newent mapmodel $BTmm $BTtt $BTti]
newgui "trigger3" [
    loop i 9 [
        _tjt = (concat tag (concatword # $i))
        _tjc = (concat BTti "=" $i ";" "build_trigger")
        guibutton $_tjt $_tjc
    ]
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
NL_ResetLight = [
    NL_ColorPreset =   0
    NL_Bright      =   1
    NL_Radius      = 128
    NL_LightSet 255 255 255
]

NL_LightSet = [
    NL_ColorR = $arg1
    NL_ColorG = $arg2
    NL_ColorB = $arg3
    if (= $NL_Bright 0) NL_LightScale
]

NL_LightScale = [
    if (= $NL_Bright 0) [
        NL_ColorR = (div $NL_ColorR 2); NL_ColorG = (div $NL_ColorG 2); NL_ColorB = (div $NL_ColorB 2)
    ] [
        NL_ColorR = (min 255 (* $NL_ColorR 2)); NL_ColorG = (min 255 (* $NL_ColorG 2)); NL_ColorB = (min 255 (* $NL_ColorB 2))
    ]
]

NL_CreateLight = [result (concat newent light $NL_Radius $NL_ColorR $NL_ColorG $NL_ColorB)]
newgui "newlight" [
    guialign 0 [guibutton (concatword " ^f7/newent ^f2light ^f3" (NL_ColorR) " ^f0" (NL_ColorG) " ^f1" (NL_ColorB) ) (NL_CreateLight) 0]
    guibar
    guitext "color:"
    guicolor (+ (* (+ (* $NL_ColorR 256) $NL_ColorG) 256) $NL_ColorB)
    guislider NL_ColorR 0 255
    guislider NL_ColorG 0 255
    guislider NL_ColorB 0 255
    guilist [
        guicheckbox "bright"    NL_Bright 1 0 [NL_LightScale]
        guibar
        guiradio "white"        NL_ColorPreset 0 [NL_LightSet 255 255 255]
        guiradio "red"          NL_ColorPreset 1 [NL_LightSet 255 192 192]
        guiradio "yellow"       NL_ColorPreset 2 [NL_LightSet 255 255 192]
        guiradio "green"        NL_ColorPreset 3 [NL_LightSet 192 255 192]
        guiradio "cyan"         NL_ColorPreset 4 [NL_LightSet 192 255 255]
        guiradio "blue"         NL_ColorPreset 5 [NL_LightSet 192 192 255]
        guiradio "purple"       NL_ColorPreset 6 [NL_LightSet 255 192 255]
    ]
    guitext "radius:"
    guislider NL_Radius 0 512
    guistrut 1
    guialign 0 [guibutton "^f2Create Light" (NL_CreateLight) 0]
] "" [NL_ResetLight]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
newgui "newteleport" [
    guibutton "newent teleport 1"
    guibutton "newent teledest 1"

    guibutton "newent teleport 2"
    guibutton "newent teledest 2"

    guibutton "newent teleport 3"
    guibutton "newent teledest 3"

    guibutton "newent teleport 4"
    guibutton "newent teledest 4"
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
newgui "monsters" [
    guialign 0 [guitext " type        weapon" 0]
    guibar
    guibutton "(0) Ogro^t^t^t^f5FireBall"           [newent monster 0];
    guibutton "(1) Rhino^t^t^t^f0Chaingun"          [newent monster 1];
    guibutton "(2) Ratamahatta^t^f3Shotgun"         [newent monster 2];
    guibutton "(3) Slith^t^t^t^f1Rifle"             [newent monster 3];
    guibutton "(4) Bauul^t^t^t^f6RocketLauncher"    [newent monster 4];
    guibutton "(5) Hellpig^t^t^f2Bite"              [newent monster 5];
    guibutton "(6) Knight^t^t^t^f5IceBall"          [newent monster 6];
    guibutton "(7) Goblin^t^t^t^f5SlimeBall"        [newent monster 7];
    guibutton "(8) Spider^t^t^t^f8Grenade"          [newent monster 8];
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
entupdate = [ entset $entguitype $entguiattr0 $entguiattr1 $entguiattr2 $entguiattr3 $entguiattr4 ]

initentgui = [
    entguitype = (enttype)
    @(loopconcat i 5 [
        result [ entguiattr@i = (entattr @i) ]
        ]
    )
]

genentattributes = [
    entattributes = (loopconcat i (listlen $arg2) [
        entattribname = (at $arg2 $i)
        entattriblimits = (at $arg3 (* 2 $i))
        entattriblimits2 = (at $arg3 (+ 1 (* 2 $i)))
        if (=s (at $entattriblimits 0) $entattriblimits) [
            result [
                guitext @entattribname
                guislider entguiattr@i @entattriblimits @entattriblimits2 entupdate
            ]
        ][
            result [
                guitext @entattribname
                guinameslider entguiattr@i [@@entattriblimits] [@@entattriblimits2] entupdate
            ]
        ]
    ])
]

quickeditmenu = [
    guitext "Quick Commands:"
    guibar
    guifield  EDT_SaveMap_Name 10 [ savemap $EDT_SaveMap_Name ]
    guibutton "quick light"       [calclight -1]
    guibutton "optimize map"      [remip]
    guibutton "new entity"        [newent shells]
    guibar
    guibutton newmap
    guibar
    guibutton help "showgui editing"
]

matmenu = [
    guibutton "air"                        [editmat air]
    guibutton "water"                      [editmat water]
    guibutton "lava"                       [editmat lava]
    guibutton "clip"                       [editmat clip]
    guibutton "glass"                      [editmat glass]
    guibutton "noclip"                     [editmat noclip]
    guibutton "gameclip"                   [editmat gameclip]
    guibutton "death"                      [editmat death]
    guibutton "alpha"                      [editmat alpha]
    guicheckbox "show material volumes"    [showmat]
]

brushmenu = [
    guititle diamonds
    guilist [
        guiimage "packages/icons/brush_1c.png"   [brush_0]
        guiimage "packages/icons/brush_21c.png"  [brush_1]
        guiimage "packages/icons/brush_421c.png" [brush_2]
    ]
    guititle squares
    guilist [
        guiimage "packages/icons/brush_3s.png"   [brush_3]
        guiimage "packages/icons/brush_5s.png"   [brush_4]
        guiimage "packages/icons/brush_7s.png"   [brush_5]
    ]
    guititle smooth
    guilist [
        guiimage "packages/icons/brush_3s.png"   [brush_6]
        guiimage "packages/icons/brush_5s.png"   [brush_7]
        guiimage "packages/icons/brush_7s.png"   [brush_8]
    ]
]

newentgui = [
    genentattributes $arg1 $arg2 $arg3
    newgui $arg1 [
        guitext $entguitype
        guibar
        @entattributes
        guitab type
        guilistsplit n 2 $EntityTypes [
            guibutton $n [ entset @n ]
        ]
        guitab misc
        @quickeditmenu
    ] "" [initentgui]
]

looplist i $EntityTypes [
    newentgui $i "" ""
]

newgui "materials" [
    @matmenu
    guitab misc
    @quickeditmenu
]

newgui "brushes" [
    @brushmenu
    guitab misc
    @quickeditmenu
]

newgui "quickedit" [
    @quickeditmenu
    guitab materials
    @matmenu
]

newentgui light "radius red green blue" "1 400 0 255 0 255 0 255"
newentgui spotlight "radius" "0 200"
newentgui playerstart "direction" "0 360"
newentgui teleport "tag" "0 20"
newentgui teledest "direction tag" "0 360 0 20"
newentgui monster "direction type" "0 360 0 7"
newentgui mapmodel "direction model" "0 360 0 372"
newentgui envmap "radius" "0 512"
newentgui jumppad "Z Y X" "-128 128 -128 127 -128 127"
newentgui sound "type radius size" "0 20 0 500 0 500"
newentgui particles "type" "0 13"

showentgui = [ contexteditgui ] // legacy bind
contexteditgui = [
    if $hmapedit [showgui "brushes"] [
        if (enthavesel) [
            showgui (enttype)
        ][
            showgui (? (havesel) materials quickedit)
        ]
    ]
]

newgui "seltexinfo" [
    guitext (concatword "Slot: " (getseltex))
    guilist [
        loop iSubSlot 5 [
            _texname = (gettexname (getseltex) $iSubSlot)
            _texname = (substr $_texname (+ (strstr $_texname ">") 1)) // chop off any leading commands
            guiimage (concatword "packages/" $_texname) "" 1 1
        ]
    ]
    loop iSubSlot 5 [
        _texname = (gettexname (getseltex) $iSubSlot)
        if (strcmp $_texname "") [] [guitext $_texname]
    ]
]



TODO 0 "menus.cfg -- ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^"












///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  vColor Editor:

nodebug [
    defvar  VCE_Red -1 100000  100000
    defvar  VCE_Grn -1 100000  100000
    defvar  VCE_Blu -1 100000  100000

    defvar  VCE_Hue -1      0  360000
    defvar  VCE_Sat -1      0  100000
    defvar  VCE_Vib -1 100000  100000

    defsvar VCE_RGB "1.0 1.0 1.0"
    defsvar VCE_HSV "1.0 1.0 1.0"

    defvar  VCE_WidthC 2 64 128
    defvar  VCE_WidthE 2 64 128

    defvar  VCE_LiveSet 0 0 1 [if (&& $VCE_LiveSet (ingamemp)) [VCE_LiveSet 0]]
]

VCE_Get = [ // (colorvectoivec LIST)
    VCE_Red = (*f (at (getvcolor $getseltex) 0) 100000)
    VCE_Grn = (*f (at (getvcolor $getseltex) 1) 100000)
    VCE_Blu = (*f (at (getvcolor $getseltex) 2) 100000)
    VCE_UpdateHSV
]
VCE_Set = [ // (colorivectovec LIST)
    vcolor (*f $VCE_Red 0.00001) (*f $VCE_Grn 0.00001) (*f $VCE_Blu 0.00001)
]
VCE_UpdateHSV = [
    VCE_HSV = (colorrgbtohsv (concat (*f $VCE_Red 0.00001) (*f $VCE_Grn 0.00001) (*f $VCE_Blu 0.00001)))
    VCE_Hue = (*f (at $VCE_HSV 0) 100000)
    VCE_Sat = (*f (at $VCE_HSV 1) 100000)
    VCE_Vib = (*f (at $VCE_HSV 2) 100000)
]
VCE_UpdateRGB = [
    VCE_RGB = (colorhsvtorgb (concat (*f $VCE_Hue 0.00001) (*f $VCE_Sat 0.00001) (*f $VCE_Vib 0.00001)))
    VCE_Red = (*f (at $VCE_RGB 0) 100000)
    VCE_Grn = (*f (at $VCE_RGB 1) 100000)
    VCE_Blu = (*f (at $VCE_RGB 2) 100000)
]

newgui "vcolor" [
    if (|| (! $editing) (! (ingame))) [cleargui 1] [
        guistayopen [
            guistrut 192 1
            guialign 0 [// H
                guilist [// V
                    guistrut 48 1
                ]
                guitext "^f2vcolor: " 0
                guilist [guistrut 8 1; guitext (concatword "^f3" (at (getvcolor $getseltex) 0)) 0]
                guilist [guistrut 8 1; guitext (concatword "^f0" (at (getvcolor $getseltex) 1)) 0]
                guilist [guistrut 8 1; guitext (concatword "^f1" (at (getvcolor $getseltex) 2)) 0]
                //guibar
                guibutton "^f2[GET]" [VCE_Get] 0
                guistrut 2.5
                guibutton "^f8[~~~~~~~SET~~~~~~~]" [VCE_Set] 0
                guistrut 2.5
                if (= (ingamemp) 0) [guicheckbox "LiveSet" VCE_LiveSet 1 0]
                guilist [// V
                    guistrut 48 1
                    if (&& $VCE_LiveSet (= (ingamemp) 0)) [
                        guitext "  Warning: LiveSet will spam vslot creation." 0
                    ]
                ]
            ]
            guialign 0 [// H
                guilist [// V
                    guistrut 0.5; guitext "^f8R: " 0
                    guistrut 0.5; guitext "^f8G: " 0
                    guistrut 0.5; guitext "^f8B: " 0
                ]
                guilist [// V
                    guistrut 64 1
                    guistrut 0.5; guislider VCE_Red 0 100000 [VCE_UpdateHSV; if (&& $VCE_LiveSet (= (ingamemp) 0)) [VCE_Set]]
                    guistrut 0.5; guislider VCE_Grn 0 100000 [VCE_UpdateHSV; if (&& $VCE_LiveSet (= (ingamemp) 0)) [VCE_Set]]
                    guistrut 0.5; guislider VCE_Blu 0 100000 [VCE_UpdateHSV; if (&& $VCE_LiveSet (= (ingamemp) 0)) [VCE_Set]]
                ]
                guilist [// V
                    guistrut 8 1
                    guistrut 0.5; guialign 1 [guitext "^f8H: " 0] // H
                    guistrut 0.5; guialign 1 [guitext "^f8S: " 0] // H
                    guistrut 0.5; guialign 1 [guitext "^f8V: " 0] // H
                ]
                guilist [// V
                    guistrut 64 1
                    guistrut 0.5; guislider VCE_Hue 0 360000 [VCE_UpdateRGB; if (&& $VCE_LiveSet (= (ingamemp) 0)) [VCE_Set]]
                    guistrut 0.5; guislider VCE_Sat 0 100000 [VCE_UpdateRGB; if (&& $VCE_LiveSet (= (ingamemp) 0)) [VCE_Set]]
                    guistrut 0.5; guislider VCE_Vib 0 100000 [VCE_UpdateRGB; if (&& $VCE_LiveSet (= (ingamemp) 0)) [VCE_Set]]
                ]
            ]
        ]
    ]
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
newgui "vshaderparam" [
    guitext "TODO" 0
] 0 []
TODO 0 "menus.cfg -- vShaderParam Editor..."

// vdelta [ ... ] to add multiple vshaderparam's

//  defuniformparam "envscale" 0.2 0.2 0.2 // reflectivity
//  defuniformparam "envscale" 0.2 0.2 0.2 // reflectivity
//  defuniformparam "envscale" 0.2 0.2 0.2 // reflectivity
//  defuniformparam "envscale" 0.2 0.2 0.2 // reflectivity
//  defuniformparam "envscale" 1 1 1 // reflectivity map multiplier
//
//  defuniformparam "glowcolor" 1 1 1  // glow color
//  defuniformparam "glowcolor" 1 1 1 // glow color
//  defuniformparam "glowcolor" 1 1 1 // glow color
//  defuniformparam "glowcolor" 1 1 1 // glow color
//
//  defuniformparam "parallaxscale" 0.06 -0.03 // parallax scaling
//
//  defuniformparam "pulseglowcolor" 0 0 0 // pulse glow color
//  defuniformparam "pulseglowcolor" 0 0 0 // pulse glow color
//
//  defuniformparam "pulseglowspeed" 1     // pulse frequency (Hz)
//  defuniformparam "pulseglowspeed" 1 // pulse frequency (Hz)
//
//  defuniformparam "pulsespeed" 1 // pulse frequency (Hz)
//
//  defuniformparam "specscale" 1 1 1 // spec multiplier
//  defuniformparam "specscale" 6 6 6 // spec map multiplier


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  Map Model Browser:

nodebug [
    defvarp mmguiwidth  2 7 12
    defvarp mmguiheight 2 4 12
    defvarp mmguiname   0 1  1

    defvar  MMB_ModelsPerTab 0 0 144
]

newgui "mapmodels" [
    if (|| (! $editing) (! (ingame))) [cleargui 1] [
        guikeeptab 1

        guinoautotab [
            loop iTab (div (+ $nummapmodels (- $MMB_ModelsPerTab 1)) $MMB_ModelsPerTab) [
                if $iTab [guitab (+ $iTab 1)]
                if (= $guitabnum (+ $iTab 1)) [
                    loop iY $mmguiheight [
                        guilist [
                            loop iX $mmguiwidth [
                                iMdl = (+ (* $iTab $MMB_ModelsPerTab) (+ (* $iY $mmguiwidth) $iX))
                                guimodelpreview (mapmodelname $iMdl) "mapmodel" [newent mapmodel @iMdl] 3 1 $iMdl 1
                            ]
                        ]
                    ]
                    if (!= $mmguiname 0) [
                        guistrut 0.2
                        //guititle (concatword "^f7" (strreplace (strreplace $guirolloverpreviewname "\" "/") "/" "^f~/^f7"))
                        guititle (concatword "^f~[^f2" $iMdl "^f~] ^f2" $guirolloverpreviewname)
                    ]
                ]
            ]
        ]
    ]
] "" [MMB_ModelsPerTab = (* $mmguiwidth $mmguiheight)]

//getmapmodels = [
//    loopconcat i $nummapmodels [
//        if $arg2 [ if (mapmodelloaded $i) [ $arg1 $i ] ] [ $arg1 $i ]
//    ]
//]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  Entity Manager:

nodebug [
    defvarp EM_EntsPerColumn 16 32    64
    defvarp EM_ColumnsPerTab  1  2     4
    defvar  EM_EntsPerTab    -1 -1   512
    defvar  EM_NumOf_Columns -1 -1   625
    defvar  EM_NumOf_Tabs    -1 -1   625

    defvar  EM_WidthColumn   24 48    96
    defvar  EM_Width         -1 -1   384
    defvar  EM_Height        -1 -1    64

    defvar  EM_NumOf_Ents    -1 -1 10000  //  MAXENTS = 10,000
]

EM_GenEntList = [
    EM_EntList = ""

    cancelsel
    entfind *
    entloop [
        EM_EntList = (concat $EM_EntList (escape $entget))
    ]
    cancelsel

    EM_NumOf_Ents (- $numoctaents 1)
    EM_Recalc
]
EM_Recalc = [
    EM_EntsPerTab    (* $EM_EntsPerColumn $EM_ColumnsPerTab)
    EM_NumOf_Columns (+ (ceil (divf (+ $EM_NumOf_Ents 1) $EM_EntsPerColumn)) 0) //  (+ n 0) <-- Float to Int hack.
    EM_NumOf_Tabs    (+ (ceil (divf    $EM_NumOf_Columns $EM_ColumnsPerTab)) 0)
    EM_Width         (* $EM_ColumnsPerTab $EM_WidthColumn)
    EM_Height           $EM_EntsPerColumn
]

newgui "entman" [
    if (|| (! $editing) (! (ingame))) [cleargui 1] [
        if (!= $EM_NumOf_Ents (- $numoctaents 1)) [EM_GenEntList]
        guistayopen [
            //#####################################################################################################################################
            guilist [// H
                guistrut $EM_Height 1
                guilist [// V
                    guistrut $EM_Width 1
                    //=====================================================================================================
                    // Is this the active Tab? If so, populate.
                    if (= $guitabnum 1) [
                        guititle "Entity Manager"
                        guibar
                        guialign 0 [// H
                            guilist [// V
                                guititle "Stats"
                                guistrut 1
                                guitext (concatword " NumOf Entities: " $EM_NumOf_Ents) 0
                                guitext " ^f4(Last index, count is +1.)" 0
                                guistrut 1
                                guibar
                                guititle "Options"
                                guistrut 1
                                guilist [
                                    guitext (format " EntsPerColumn:^t%1^t" $EM_EntsPerColumn) 0
                                    guibutton "" [EM_EntsPerColumn (+ $EM_EntsPerColumn -1); if (< $EM_EntsPerColumn 16) [EM_EntsPerColumn 16]; EM_Recalc] "val_dec"
                                    guibutton "" [EM_EntsPerColumn (+ $EM_EntsPerColumn  1); if (> $EM_EntsPerColumn 64) [EM_EntsPerColumn 64]; EM_Recalc] "val_inc"
                                ]
                                guilist [
                                    guitext (format " ColumnsPerTab: ^t%1^t" $EM_ColumnsPerTab) 0
                                    guibutton "" [EM_ColumnsPerTab (+ $EM_ColumnsPerTab -1); if (< $EM_ColumnsPerTab 1) [EM_ColumnsPerTab 1]; EM_Recalc] "val_dec"
                                    guibutton "" [EM_ColumnsPerTab (+ $EM_ColumnsPerTab  1); if (> $EM_ColumnsPerTab 8) [EM_ColumnsPerTab 8]; EM_Recalc] "val_inc"
                                ]
                                guistrut 1
                                guitext (concatword " NumOf Columns: ^t" $EM_NumOf_Columns) 0
                                guitext (concatword " NumOf Tabs:    ^t" $EM_NumOf_Tabs   ) 0
                                guistrut 1
                            ]
                            //guibar
                        ]
                    ]
                    //=====================================================================================================
                ]
            ]
            //#####################################################################################################################################
            loop iTab $EM_NumOf_Tabs [
                guitab (* $iTab $EM_EntsPerTab)
                guilist [// H
                    //=====================================================================================================
                    // Is this the active Tab? If so, populate.
                    if (= $iTab (- $guitabnum 2)) [
                        iMdl = (* $iTab $EM_EntsPerTab)
                        iCol = 1
                        while [<= $iCol $EM_ColumnsPerTab] [
                            guilist [// V
                                iEND = (+ (* $iTab $EM_EntsPerTab) (- (* $iCol $EM_EntsPerColumn) 1))
                                while [<= $iMdl $iEND] [
                                    if (> $iMdl $EM_NumOf_Ents) [
                                        iMdl = (+ $iEND 1) // exitloop/break
                                    ][
                                        This = (at $EM_EntList $iMdl)
                                        guilist [// H
                                            guilist [guistrut  7 1; guialign 1 [guitext (concatword "^f7[^f2" (iMdl) "^f7]") 0]]
                                            guilist [guistrut 11 1; guialign 1 [guitext (concatword "^f2" (at $This 0)) 0]]
                                            guilist [guistrut  6 1; guialign 1 [guitext (concatword "^f2" (at $This 1)) 0]]
                                            guilist [guistrut  6 1; guialign 1 [guitext (concatword "^f2" (at $This 2)) 0]]
                                            guilist [guistrut  6 1; guialign 1 [guitext (concatword "^f2" (at $This 3)) 0]]
                                            guilist [guistrut  6 1; guialign 1 [guitext (concatword "^f2" (at $This 4)) 0]]
                                            guilist [guistrut  6 1; guialign 1 [guitext (concatword "^f2" (at $This 5)) 0]]
                                        ]
                                        iMdl = (+ $iMdl 1)
                                    ]
                                ]
                                iCol = (+ $iCol 1)
                            ]
                        ]
                    ]
                    //=====================================================================================================
                ]
            ]
            //#####################################################################################################################################
        ]
    ]
] "Info" [EM_GenEntList]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  Texture Slot Manager:

nodebug [
    defvarp TSM_SlotsPerColumn 16 32    64
    defvarp TSM_ColumnsPerTab   1  2     4
  //defvarp TSM_ShowSideBar     0  0     1
    defvarp TSM_ShowLayerParams 0  0     1

    defvar  TSM_WidthColumn    24 48    96
    defvar  TSM_WidthSideBar    0 50    96

    defvar  TSM_Width          -1 -1   480
    defvar  TSM_Height         -1 -1    64

    defvar  TSM_NumOf_Columns  -1 -1  4096
    defvar  TSM_NumOf_Tabs     -1 -1  4096
    defvar  TSM_SlotsPerTab    -1 -1 16384

    defvar  TSM_NumOf_tSlots   -1 -1 65535 //  Texture slot index is an 'unsigned short'.
    defvar  TSM_NumOf_vSlots   -1 -1 65535
    defvar  TSM_NumOf_AllSlots -1 -1 65535
    defvar  TSM_SelectedSlot   -1 -1 65535
]

TSM_Recalc = [
    TSM_SlotsPerTab   (* $TSM_SlotsPerColumn $TSM_ColumnsPerTab)
    TSM_NumOf_Columns (+ (ceil (divf (+ $TSM_NumOf_AllSlots 1) $TSM_SlotsPerColumn)) 0) //  (+ n 0) <-- Float to Int hack.
    TSM_NumOf_Tabs    (+ (ceil (divf    $TSM_NumOf_Columns     $TSM_ColumnsPerTab )) 0) //  Why do round,floor,ceil return floats?
    TSM_Width         (+ (+ (* $TSM_ColumnsPerTab $TSM_WidthColumn) $TSM_WidthSideBar) 2) //<-- +2 to account for guibar.
    TSM_Height        $TSM_SlotsPerColumn
]
TSM_Update = [
    TSM_NumOf_tSlots   (- $numslots 1)
    TSM_NumOf_vSlots   (- $numvslots $numslots)
    TSM_NumOf_AllSlots (- $numvslots 1)
    TSM_SelectedSlot   -1
    TSM_Recalc
]


TODO 0 "menus.cfg -- TSM - 'history' section: Show last used & last replaced texture."
//    //  getcurtex       "Get Current Texture"  Index of last used texture.
//    //  getreptex       "Get Replace Texture"  Index of last texture replaced. (/replace uses this)
//    //result "^f~[^f2~^f~] ^f2^"~^"^n^n" //  None.
//    format "^f~[^f0%1^f~] ^f0^"%2^"^n^f~[^f0%3^f~] ^f0^"%4^"^n^n" $getcurtex (gettexname $getcurtex) $getreptex (gettexname $getreptex)

TODO 0 "menus.cfg -- TSM - 'replacetex' section."
// replacetex    $TSM_WithMe $TSM_ReplaceMe
// replacetexsel $TSM_WithMe $TSM_ReplaceMe

newgui "texslotman" [
    if (|| (! $editing) (! (ingame))) [cleargui 1] [
        if (!= $TSM_NumOf_AllSlots (- $numvslots 1)) [TSM_Update]
        guistayopen [
            //#####################################################################################################################################
            guilist [// H
                guistrut $TSM_Height 1
                guilist [// V
                    guistrut $TSM_Width 1
                    guititle "Texture Slot Manager"
                    guibar //-------------------------------------------------------------------------
                    guialign 0 [// H
                        guilist [// V
                            guititle "Stats"
                            guistrut 1
                            guitext (concatword " NumOf tSlots:  ^t"  $TSM_NumOf_tSlots  ) 0
                            guitext (concatword " NumOf vSlots:  ^t"  $TSM_NumOf_vSlots  ) 0
                            guitext (concatword " Total TexSlots: ^t" $TSM_NumOf_AllSlots) 0
                            guitext " ^f4(Last index, count is +1.)" 0
                            guistrut 1
                            guibar //-------------------------------------------------------------------------
                            guititle "Options"
                            guistrut 1
                            guilist [// H
                                guitext (format " ^f7SlotsPerColumn:^t%1^t"  $TSM_SlotsPerColumn) 0
                                guibutton "" [TSM_SlotsPerColumn (+ $TSM_SlotsPerColumn -1); if (< $TSM_SlotsPerColumn 16) [TSM_SlotsPerColumn 16]; TSM_Recalc] "val_dec"
                                guibutton "" [TSM_SlotsPerColumn (+ $TSM_SlotsPerColumn  1); if (> $TSM_SlotsPerColumn 64) [TSM_SlotsPerColumn 64]; TSM_Recalc] "val_inc"
                            ]
                            guilist [// H
                                guitext (format " ^f7ColumnsPerTab: ^t%1^t"  $TSM_ColumnsPerTab) 0
                                guibutton "" [TSM_ColumnsPerTab (+ $TSM_ColumnsPerTab -1); if (< $TSM_ColumnsPerTab 1) [TSM_ColumnsPerTab 1]; TSM_Recalc] "val_dec"
                                guibutton "" [TSM_ColumnsPerTab (+ $TSM_ColumnsPerTab  1); if (> $TSM_ColumnsPerTab 4) [TSM_ColumnsPerTab 4]; TSM_Recalc] "val_inc"
                            ]
                            guistrut 1
                            guitext (concatword " NumOf Columns: ^t" $TSM_NumOf_Columns) 0
                            guitext (concatword " NumOf Tabs:    ^t" $TSM_NumOf_Tabs   ) 0
                            guistrut 1
                            guilist [guistrut 1; guicheckbox " Show Layer Params" TSM_ShowLayerParams]
                        ]
                        guibar //-------------------------------------------------------------------------
                        guilist [// V
                            guititle "Info"
                            guistrut 1
                            guitext "   ^f7White^t = Texture is loaded." 0
                            guitext "   ^f4Grey^t = Texture is not loaded." 0
                            guitext "   ^f2Yellow^t = Texture is vSlot." 0
                            guistrut 1
                            guitext " A texture can be loaded and un-used." 0
                            guitext " Reload map if you would like to see if the map is using a texture. Or use ^f2/resetgl" 0
                            guistrut 1
                            guibar //-------------------------------------------------------------------------
                            guititle "CleanUp / Optimize"
                            guistrut 1
                            guitext " vSlots are automatically optimized upon ^f2/savemap^f~." 0
                            guistrut 1
                            guitext " vSlots will automatically optimze when this many new vslots have been created:" 0
                            guistrut 0.5
                            guilist [// H
                                guitext "   autocompactvslots: " 0
                                guilist [// V
                                    guistrut 32 1
                                    guislider autocompactvslots 0 1024 [autocompactvslots (round $autocompactvslots 32)]
                                ]
                                guitext "^f4 (default 256)" 0
                            ]
                            guistrut 1.5
                            guitext " Manually optimze vSlots:" 0
                            guistrut 0.5
                            guilist [guistrut 3; guibutton "^f2/compactvslots" [compactvslots]]
                            guistrut 1.5
                            guitext " This will set the texture of all hidden cube faces to slot[1] (default texture):" 0
                            guistrut 0.5
                            guilist [guistrut 3; guibutton "^f2/fixinsidefaces" [fixinsidefaces]]
                            guistrut 1
                        ]
                    ]
                ]
            ]
            //#####################################################################################################################################
            loop iTab $TSM_NumOf_Tabs [
                guitab (* $iTab $TSM_SlotsPerTab)
                guialign 0 [// H
                    guistrut $TSM_Height 1
                    //=========================================================================================================
                    // Is this the active Tab? If so, populate.
                    if (= $iTab (- $guitabnum 2)) [
                        iTex = (* $iTab $TSM_SlotsPerTab)
                        iCol = 1
                        while [<= $iCol $TSM_ColumnsPerTab] [
                            guilist [// V
                                guistrut $TSM_WidthColumn 1
                                iEND = (+ (* $iTab $TSM_SlotsPerTab) (- (* $iCol $TSM_SlotsPerColumn) 1))
                                while [<= $iTex $iEND] [
                                    if (> $iTex $TSM_NumOf_AllSlots) [
                                        iTex = (+ $iEND 1) // exitloop/break
                                    ][
                                        if (= (getslottex $iTex) -1) [
                                            // vSlot:
                                            guibutton (concatword " ^f7[^f2" $iTex "^f7] " (? (= $iTex $TSM_SelectedSlot) "^f8" "^f2") (gettexname $iTex)) (concat "TSM_SelectedSlot =" $iTex) 0
                                        ][
                                            if (texloaded $iTex) [
                                                // tSlot Loaded:
                                                guibutton (concatword " ^f7[" $iTex "] " (? (= $iTex $TSM_SelectedSlot) "^f8" "^f7") (gettexname $iTex)) (concat "TSM_SelectedSlot =" $iTex) 0
                                            ][
                                                // tSlot Not Loaded:
                                                guibutton (concatword " ^f7[" $iTex "] " (? (= $iTex $TSM_SelectedSlot) "^f8" "^f4") (gettexname $iTex)) (concat "TSM_SelectedSlot =" $iTex) 0
                                            ]
                                        ]
                                        iTex = (+ $iTex 1)
                                    ]
                                ]
                                iCol = (+ $iCol 1)
                            ]
                        ]
                        guibar //-------------------------------------------------------------------------
                        guilist [// V
                            guistrut $TSM_WidthSideBar 1
                            guialign 0 [// H
                                guiimage (concatword "packages/" (gettexname $TSM_SelectedSlot)) 0 7 0 "data/cube.png" // <thumbnail:256>  <-- Breaks non-square textures.
                                if (!= (getvlayer $TSM_SelectedSlot) 0) [
                                    guiimage (concatword "packages/" (gettexname (getvlayer $TSM_SelectedSlot)) ) () 4 0 "data/cube.png"
                                ]
                            ]
                            if (< $TSM_SelectedSlot 0) [
                                guitext " ^f~[^f2~^f~,^f2~^f~] ^f2^"~^"" 0
                            ][
                                                                     guitext (concatword " ^f~[^f2" $TSM_SelectedSlot "^f~,^f20^f~] ^f2^"" (gettexname $TSM_SelectedSlot 0) "^"" ) 0
                                if (gettexname $TSM_SelectedSlot 1) [guitext (concatword " ^f~[^f2" $TSM_SelectedSlot "^f~,^f21^f~] ^f2^"" (gettexname $TSM_SelectedSlot 1) "^"" ) 0]
                                if (gettexname $TSM_SelectedSlot 2) [guitext (concatword " ^f~[^f2" $TSM_SelectedSlot "^f~,^f22^f~] ^f2^"" (gettexname $TSM_SelectedSlot 2) "^"" ) 0]
                                if (gettexname $TSM_SelectedSlot 3) [guitext (concatword " ^f~[^f2" $TSM_SelectedSlot "^f~,^f23^f~] ^f2^"" (gettexname $TSM_SelectedSlot 3) "^"" ) 0]
                                if (gettexname $TSM_SelectedSlot 4) [guitext (concatword " ^f~[^f2" $TSM_SelectedSlot "^f~,^f24^f~] ^f2^"" (gettexname $TSM_SelectedSlot 4) "^"" ) 0]
                                if (gettexname $TSM_SelectedSlot 5) [guitext (concatword " ^f~[^f2" $TSM_SelectedSlot "^f~,^f25^f~] ^f2^"" (gettexname $TSM_SelectedSlot 5) "^"" ) 0]
                                if (gettexname $TSM_SelectedSlot 6) [guitext (concatword " ^f~[^f2" $TSM_SelectedSlot "^f~,^f26^f~] ^f2^"" (gettexname $TSM_SelectedSlot 6) "^"" ) 0]
                                if (gettexname $TSM_SelectedSlot 7) [guitext (concatword " ^f~[^f2" $TSM_SelectedSlot "^f~,^f27^f~] ^f2^"" (gettexname $TSM_SelectedSlot 7) "^"" ) 0]
                            ]
                            TSM_Parameters $TSM_SelectedSlot
                            guibar //-------------------------------------------------------------------------
                            if (!= (getvlayer $TSM_SelectedSlot) 0) [
                                guitext (format " ^f~layer: ^f~[^f6%1^f~] ^f6^"%2^""  (getvlayer $TSM_SelectedSlot) (gettexname (getvlayer $TSM_SelectedSlot)) ) 0
                                if (!= $TSM_ShowLayerParams 0) [
                                    TSM_Parameters (getvlayer $TSM_SelectedSlot)
                                ]
                            ][
                                guitext " ^f~layer: ^f~[^f6~^f~] ^f6^"none^"" 0
                            ]
                        ]
                    ]
                    //=========================================================================================================
                ]
            ]
            //#####################################################################################################################################
        ]
    ]
] "Info" [TSM_Recalc]

TSM_Parameters = [
    // ARGS  1: INDEX | Texture slot.
    guifreerow 12  1 [ guitext "color:" 0
             ]  8  1 [ guitext (concatword "^f3" (at (getvcolor @arg1) 0)) 0
             ]  8  1 [ guitext (concatword "^f0" (at (getvcolor @arg1) 1)) 0
             ]  8  1 [ guitext (concatword "^f1" (at (getvcolor @arg1) 2)) 0
    ]
    guistrut 0.08333 //0.09375
    guifreerow 12  1 [ guitext "alpha:" 0
             ]  8  1 [ guitext (concatword "^f5" (at (getvalpha @arg1) 0)) 0
             ]  8  1 [ guitext (concatword "^f5" (at (getvalpha @arg1) 1)) 0
    ]
    guistrut 0.08333
    guifreerow 12  1 [ guitext "rotate:" 0
             ]  8  1 [ guitext (concatword "^f2" (getvrotate @arg1)) 0
             ]  1 -1 [ guitext (concatword " ^f7^"" (at $_Labels_TexRotate (getvrotate @arg1)) "^"") 0
    ]
    guistrut 0.08333
    guifreerow 12  1 [ guitext "offset:" 0
             ]  8  1 [ guitext (concatword "^f3" (at (getvoffset @arg1) 0)) 0
             ]  8  1 [ guitext (concatword "^f0" (at (getvoffset @arg1) 1)) 0
    ]
    guistrut 0.08333
    guifreerow 12  1 [ guitext "scale:" 0
             ]  8  1 [ guitext (concatword "^f2" (getvscale @arg1)) 0
    ]
    guistrut 0.08333
    guifreerow 12  1 [ guitext "scroll:" 0
             ]  8  1 [ guitext (concatword "^f3" (at (getvscroll @arg1) 0)) 0
             ]  8  1 [ guitext (concatword "^f0" (at (getvscroll @arg1) 1)) 0
    ]
    guistrut 0.08333
    guifreerow 12  1 [ guitext "smooth:" 0
             ]  8  1 [ guitext (concatword "^f2group: " (getvsmooth @arg1)) 0
             ]  8  1 [ guitext (concatword "^f2threshold: " (getsmoothangle (getvsmooth @arg1))) 0
    ]
    guistrut 0.08333
    guifreerow 12  1 [ guitext "vshaderparam:" 0
             ]  1 -1 [ guitext (concatword " ^f2^"" (getvshaderparamnames @arg1) "^" " (getvshaderparam @arg1 (getvshaderparamnames @arg1)) ) 0
    ]
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  Tests:

newgui "test_font" [
    guinoautotab [
        guitext " A B C D E F G H          " 0
        guitext " I J K L M N O P          " 0
        guitext " Q R S T U V W X          " 0
        guitext " Y Z 0 1 2 3 4              " 0
        guitext " a b c d e f g h          " 0
        guitext " i j k l m n o p          " 0
        guitext " q r s t u v w x          " 0
        guitext " y z 5 6 7 8 9         ` ~ ^^ " 0
        guistrut 1
        guitext "                  " 0
        guitext "                  " 0
        guitext "                  " 0
        guitext "                  " 0
        guitext "                  " 0
        guitext "                    " 0
        guitext " , . ; : ! ? ' ^"   @ # $  % &   " 0
        guitext " < > ( ) [ ] { }  + - * = / \ _ | " 0
    guitab "2"
        guitext " ABCDEFGH   " 0
        guitext " IJKLMNOP   " 0
        guitext " QRSTUVWX   " 0
        guitext " YZ01234      " 0
        guitext " abcdefgh   " 0
        guitext " ijklmnop   " 0
        guitext " qrstuvwx   " 0
        guitext " yz56789   `~^^ " 0
        guistrut 1
        guitext "    " 0
        guitext "    " 0
        guitext "    " 0
        guitext "    " 0
        guitext "    " 0
        guitext "      " 0
        guitext " ,.;:!?'^"  @#$%&  " 0
        guitext " <>()[]{}  +-*=/\_| " 0
    guitab "3"
        guitext "^tThe quick brown fox jumps over the lazy dog." 0
        guistrut 1
        guitext "^tLorem ipsum dolor sit amet, consectetur adipiscing elit." 0
        guitext "Phasellus malesuada urna nec lectus scelerisque vehicula." 0
        guitext "In id lorem nec dui fringilla vestibulum. Integer" 0
        guitext "scelerisque consequat nibh. Integer feugiat tortor eget" 0
        guitext "tortor posuere, et consequat sem venenatis. Etiam mi purus," 0
        guitext "iaculis vel orci vehicula, efficitur facilisis sem. Nunc" 0
        guitext "bibendum nisi vel dignissim efficitur. Nulla in libero eget" 0
        guitext "neque scelerisque interdum. Nullam cursus metus sed libero" 0
        guitext "tincidunt aliquam. Integer aliquet felis elit, ut viverra" 0
        guitext "nibh mollis ut. Pellentesque congue, urna a lobortis" 0
        guitext "porttitor, ipsum justo pulvinar lorem, non vehicula magna" 0
        guitext "arcu in tellus. Fusce id odio velit. Nullam et massa dui." 0
        guitext "Suspendisse potenti. Aenean laoreet justo id aliquet tempus." 0
    //guitab "~"
    //    guitext "-|-----------------|-|-----------------|-" 0
    //    guitext "0| A B C D E F G H |0|         |0" 0
    //    guitext "1| I J K L M N O P |1|         |1" 0
    //    guitext "2| Q R S T U V W X |2|         |2" 0
    //    guitext "3| Y Z 0 1 2 3 4   |3|           |3" 0
    //    guitext "4| a b c d e f g h |4|         |4" 0
    //    guitext "5| i j k l m n o p |5|         |5" 0
    //    guitext "6| q r s t u v w x |6|         |6" 0
    //    guitext "7| y z 5 6 7 8 9   |7|      ` ~ ^^ |7" 0
    //    guitext "-|-----------------|-|-----------------|-" 0
    //    guitext "0|         |0|         |0" 0
    //    guitext "1|         |1|         |1" 0
    //    guitext "2|         |2|         |2" 0
    //    guitext "3|         |3|         |3" 0
    //    guitext "4|         |4|         |4" 0
    //    guitext "5|           |5|         |5" 0
    //    guitext "6| , . ; : ! ? ' ^" |6|  @ # $  % &   |6" 0
    //    guitext "7| < > ( ) [ ] { } |7| + - * = / \ _ | |7" 0
    //    guitext "-|-----------------|-|-----------------|-" 0
    ]
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
newgui "test_padding" [
    guinoautotab [
        guiimage "packages/textures/razgriz/black.png" [] 6
    guitab "IW"
        guiimage "packages/textures/razgriz/white.png" [] 6
    guitab "IP"
        guiimage "packages/textures/steep_fur_n.png" [] 6
    guitab "BH"
        guilist [// H
            guistrut  5 1
            guialign -1 [guistrut 24 1; guibar]// V  bar_H
        ]
        guilist [// H
            guistrut  2 1
            guialign  0 [guistrut 24 1; guibar]// V  bar_H
        ]
        guilist [// H
            guistrut  5 1
            guialign  1 [guistrut 24 1; guibar]// V  bar_H
        ]
    guitab "BV"
        guilist [// H
            guilist [// V
                guistrut 10 1
                guialign -1 [guistrut 12 1; guibar]// H  bar_V
            ]
            guilist [// V
                guistrut  4 1
                guialign  0 [guistrut 12 1; guibar]// H  bar_V
            ]
            guilist [// V
                guistrut 10 1
                guialign  1 [guistrut 12 1; guibar]// H  bar_V
            ]
        ]
    ]
] "IB"

