///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  Defines the editHUD, visible bottom-left while editing:
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
//  Built-in edithud stuff:  (/showeditstats)
//      wtr  =  OctreeData:  Number of Triangles    (% of total)
//      wvt  =  OctreeData:  Number of Vertices     (% of total)
//      evt  =               Number of "Extra" Vertices??           I see this a lot: "xtraverts += gle::end()"     "gle::end()" returns "numvertexes"
//      eva  =               Number of "Extra" VertexArrays??       I see this a lot: "xtravertsva += numverts"     These both appear to be measuring vertex counts of something(s)...
//
//      ond  =  OctreeData:  Number of Nodes
//       va  =  OpenGL:      Number of VertexArrays
//       gl  =  OpenGL:      Number of DrawCalls    ("Batches")
//       oq  =  OpenGL:      Number of "Queries"
//       lm  =  MapData:     Number of Lightmap Textures
//       rp  =  MapDataish:  Number of Render Planes currently visible (used for water refraction & reflection)
//      pvs  =  MapData:     Number of PVS ViewCells
//
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
edithud_showplayerposition     = 0
edithud_showplayerrotation     = 0
edithud_showtextureparams      = 1
edithud_showtexturelayerparams = 1 //0

edithud = [
    format "%1%2%3%4%5" (
    //#####################################################################################################################################
        if (!= $edithud_showplayerposition 0) [
            format "^f7position: ^f3%1  ^f0%2  ^f1%3%4" (at $getcampos 0) (at $getcampos 1) (at $getcampos 2) (? (!= $edithud_showplayerrotation 0) "^n" "^n^n")
        ]
    )(
    //#####################################################################################################################################
        if (!= $edithud_showplayerrotation 0) [
            format "^f7rotation: ^f3%1  ^f0%2  ^f1%3^n^n" $getcampitch $getcamroll $getcamyaw  //  Sauer's axes are weird. :P
        ]
    )(
    //#####################################################################################################################################
    //  %3: Edit_Heightmap / Edit_Blendmap / Cube Face-Texture Info
        if $hmapedit [
            format "heightmap^n*  brush: ^f~[^f2%1^f~] ^f2^"%2^"^n^n" $currentbrush $currentbrushname
        ][
            if $blendpaintmode [
                format "blendmap^n*  mode: ^f2^"%1^"^f~^n*  brush: ^f~[^f2%2^f~] ^f2^"%3^"^n^n" (at $_Labels_BlendPaintMode $blendpaintmode) (curblendbrush) (getblendbrushname $curblendbrush)
            ][
                if (! $enthavesel) [
                    if (! $getseltex) [
                        result "^f~[^f2~^f~] ^f2^"~^"^n^n" //  None or multiple selected.
                    ][
                        if (= $edithud_showtextureparams 0) [
                            format "^f~[^f2%1^f~] ^f2^"%2^"^n^n" $getseltex (gettexname $getseltex)
                        ][
                            format "%1%2^n" (
                                format "^f~[^f2%1^f~] ^f2^"%2^"^n" $getseltex (gettexname $getseltex)
                            )(
                                format "%1%2%3%4%5%6%7%8" ( /////////////////////////////////////////////////////////////////////////////////// %1
                                    if (|| (!=f (at (getvcolor $getseltex) 0) 1.0) (!=f (at (getvcolor $getseltex) 1) 1.0) (!=f (at (getvcolor $getseltex) 2) 1.0)) [
                                        format "^f~* color:^t^f3%1^t^f0%2^t^f1%3^n" (at (getvcolor $getseltex) 0) (at (getvcolor $getseltex) 1) (at (getvcolor $getseltex) 2)
                                    ]
                                )( //////////////////////////////////////////////////////////////////////////////////////////////////////////// %2
                                    if (|| (!=f (at (getvalpha $getseltex) 0) 0.5) (!=f (at (getvalpha $getseltex) 1) 0.0) ) [
                                        format "^f~* alpha:^t^f5%1^t%2^n" (at (getvalpha  $getseltex) 0) (at (getvalpha  $getseltex) 1)
                                    ]
                                )( //////////////////////////////////////////////////////////////////////////////////////////////////////////// %3
                                    if (getvrotate $getseltex) [
                                        format "^f~* rotate:^t^f2%1 ^f7^"%2^"^n" (getvrotate $getseltex) (at $_Labels_TexRotate (getvrotate $getseltex))
                                    ]
                                )( //////////////////////////////////////////////////////////////////////////////////////////////////////////// %4
                                    if (|| (!= (at (getvoffset $getseltex) 0) 0) (!= (at (getvoffset $getseltex) 1) 0) ) [
                                        format "^f~* offset:^t^f3%1^t^f0%2^n" (at (getvoffset $getseltex) 0) (at (getvoffset $getseltex) 1)
                                    ]
                                )( //////////////////////////////////////////////////////////////////////////////////////////////////////////// %5
                                    if (!=f (getvscale $getseltex) 1.0) [
                                        format "^f~* scale:^t^f2%1^n" (getvscale $getseltex)
                                    ]
                                )( //////////////////////////////////////////////////////////////////////////////////////////////////////////// %6
                                    if (|| (!=f (at (getvscroll $getseltex) 0) 0.0) (!=f (at (getvscroll $getseltex) 1) 0.0) ) [
                                        format "^f~* scroll:^t^f3%1^t^f0%2^n" (at (getvscroll $getseltex) 0) (at (getvscroll $getseltex) 1)
                                    ]
                                )( //////////////////////////////////////////////////////////////////////////////////////////////////////////// %7
                                    if (getvshaderparamnames $getseltex) [
                                        format "^f~* vshaderparam: ^f2^"%1^" %2^n" (getvshaderparamnames $getseltex) (getvshaderparam $getseltex (getvshaderparamnames $getseltex))

                                        //if ()

//(listlen getvshaderparamnames) <---- Not zero-inclusive
//
//(concatword (listlen getvshaderparamnames))

// vdelta [ ... ] to add multiple vshaderparam's


                                    ]
                                )( //////////////////////////////////////////////////////////////////////////////////////////////////////////// %8
                                    if (getvlayer $getseltex) [
                                        format "^f~* layer:^t^f~[^f6%1^f~] ^f6^"%2^"^n" (getvlayer $getseltex) (gettexname (getvlayer $getseltex))

                                        //if (!= edithud_showtexturelayerparams 0) [
                                        //    //@@  Show params for layer!
                                        //]

                                    ]
                                )
                            )
                        ]
                    ]
                ]
            ]
        ]
    )(
    //#####################################################################################################################################
    //  %4: Entity Selection
        //
        //      Selection behavior is a bit weird:
        //        * If you have a Clicked_Selection AND a Hover_Selection, "enthavesel" will still return 1
        //              Having "enthavesel" return a special case (or just "2") for Clicked/Hover combo would be useful.
        //
        //        * If you edit Attributes with a Multi_Selection,
        //          it will take the value of the first ent, alter it,
        //          then apply it to all ents selected
        //
        if (= $enthavesel 1) [
            if (strcmp (enttype) "particles") [
                _EH_ParticleTypeIsValid = (|| (&& (>= (entattr 0) 0) (!= (entattr 0) 8) (<= (entattr 0) 13)) (&& (>= (entattr 0) 32) (<= (entattr 0) 35)))
            ][
                _EH_ParticleTypeIsValid = 0
            ]

            format "^f~[^f6%1^f~] ^f6%2%3%4%5%6%7^n^n" (entindex) (enttype) @[_EditHud_EntAttr_@(enttype)]
        ][
            if (> $enthavesel 1) [
                format "^f~[^f6~^f~] ^f6%1 entities selected%2%3%4%5%6^n^n" (enthavesel) @_EditHud_EntAttr_
            ]
        ]
    )(
    //#####################################################################################################################################
    //  %5: Settings
        concatword                                             "^f~gridpower ^f2"       $gridpower        (
            ? $allfaces                                      "  ^f0allfaces"       "  ^f4allfaces"       )(
            ? $entediting                                    "  ^f0entediting"     "  ^f4entediting"     )(
            ? $entselsnap                                    "  ^f0entselsnap"     "  ^f4entselsnap"     )(
            ? $dropwaypoints (? (mod (div $getmillis 500) 2) "  ^f0dropwaypoints"  "  ^f3dropwaypoints") ) "^n"
    )
    //#####################################################################################################################################
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// NOTE: Entity Attributes are labeled 1-5, instead of 0-4, to correspond to default keybinds for editing values. ( NumRow 1-5 )

//#############################################################################################################################################
_EditHud_EntAttr_ = [(
    concatword "^n^f4* attr1:^t" (entattr 0) )(
    concatword "^n^f4* attr2:^t" (entattr 1) )(
    concatword "^n^f4* attr3:^t" (entattr 2) )(
    concatword "^n^f4* attr4:^t" (entattr 3) )(
    concatword "^n^f4* attr5:^t" (entattr 4) )
]


//#############################################################################################################################################
//  Miscellaneous:
_EditHud_EntAttr_none?  = $_EditHud_EntAttr_
_EditHud_EntAttr_carrot = $_EditHud_EntAttr_


//#############################################################################################################################################
//  Interact-able:
_EditHud_EntAttr_base = [(
    concatword "^n^f~* ammo:^t^f2"  (entattr 0) "^f~ ^"" (? (< (entattr 0) 0) "^f5random-group" (? (> (entattr 0) 6) "^f3INVALID" (unescape (at $_CaptureBaseAmmoTypes (entattr 0))) ) ) "^f~^"" )(
    concatword "^n^f~* index:^t^f2" (entattr 1) )(
    concatword "^n^f4* attr3:^t"    (entattr 2) )(
    concatword "^n^f4* attr4:^t"    (entattr 3) )(
    concatword "^n^f4* attr5:^t"    (entattr 4) )
]
_EditHud_EntAttr_flag = [(
    concatword "^n^f~* yaw:^t^f2"  (entattr 0) )(
    concatword "^n^f~* team:^t^f2" (entattr 1) )(
    concatword "^n^f4* attr3:^t"   (entattr 2) )(
    concatword "^n^f4* attr4:^t"   (entattr 3) )(
    concatword "^n^f4* attr5:^t"   (entattr 4) )
]
_EditHud_EntAttr_jumppad = [(
    concatword "^n^f~* forceZ:^t^f2" (entattr 0) )(
    concatword "^n^f~* forceY:^t^f2" (entattr 1) )(
    concatword "^n^f~* forceX:^t^f2" (entattr 2) )( // Sauer's axes are weird.
    concatword "^n^f4* attr4:^t"     (entattr 3) )(
    concatword "^n^f4* attr5:^t"     (entattr 4) )
]
_EditHud_EntAttr_playerstart = [(
    concatword "^n^f~* yaw:^t^f2"  (entattr 0) )(
    concatword "^n^f~* team:^t^f2" (entattr 1) )(
    concatword "^n^f4* attr3:^t"   (entattr 2) )(
    concatword "^n^f4* attr4:^t"   (entattr 3) )(
    concatword "^n^f4* attr5:^t"   (entattr 4) )
]
_EditHud_EntAttr_teleport = [(
    concatword "^n^f~* index:^t^f2"             (entattr 0) )(
    concatword "^n^f~* model:^t^f2"             (entattr 1) )(
    concatword "^n^f~* preserve velocity:^t^f2" (entattr 2) )( // Maybe?
    concatword "^n^f4* attr4:^t"                (entattr 3) )(
    concatword "^n^f4* attr5:^t"                (entattr 4) )
]
_EditHud_EntAttr_teledest = [(
    concatword "^n^f~* yaw:^t^f2"   (entattr 0) )(
    concatword "^n^f~* index:^t^f2" (entattr 1) )(
    concatword "^n^f4* attr3:^t"    (entattr 2) )(
    concatword "^n^f4* attr4:^t"    (entattr 3) )(
    concatword "^n^f4* attr5:^t"    (entattr 4) )
]


//#############################################################################################################################################
//  Things...:
_EditHud_EntAttr_envmap = [(
    concatword "^n^f~* radius:^t^f2" (entattr 0) )(
    concatword "^n^f4* attr2:^t"     (entattr 1) )(
    concatword "^n^f4* attr3:^t"     (entattr 2) )(
    concatword "^n^f4* attr4:^t"     (entattr 3) )(
    concatword "^n^f4* attr5:^t"     (entattr 4) )
]
_EditHud_EntAttr_light = [(
    concatword "^n^f~* radius:^t^f2" (entattr 0) (? (= (entattr 0) 0) "^f~ ^"infinite^"") )(
    concatword "^n^f~* ^f3red:^t"    (entattr 1)                                          )(
    concatword "^n^f~* ^f0green:^t"  (entattr 2)                                          )(
    concatword "^n^f~* ^f1blue:^t"   (entattr 3)                                          )(
    concatword "^n^f4* attr5:^t"     (entattr 4)                                          )
]
_EditHud_EntAttr_spotlight = [(
    concatword "^n^f~* angle:^t^f2" (entattr 0) )(
    concatword "^n^f4* attr2:^t"    (entattr 1) )(
    concatword "^n^f4* attr3:^t"    (entattr 2) )(
    concatword "^n^f4* attr4:^t"    (entattr 3) )(
    concatword "^n^f4* attr5:^t"    (entattr 4) )
]
_EditHud_EntAttr_mapmodel = [(
    concatword "^n^f~* yaw:^t^f2"   (entattr 0)                                          )(
    concatword "^n^f~* index:^t^f2" (entattr 1) " ^f~^"" (mapmodelname (entattr 1)) "^"" )(
    concatword "^n^f4* attr3:^t"    (entattr 2)                                          )(
    concatword "^n^f4* attr4:^t"    (entattr 3)                                          )(
    concatword "^n^f4* attr5:^t"    (entattr 4)                                          )
]
_EditHud_EntAttr_sound = [(
    concatword "^n^f~* index:^t^f2"         (entattr 0) )(
    concatword "^n^f~* radius stereo:^t^f2" (entattr 1) )(
    concatword "^n^f~* radius mono:^t^f2"   (entattr 2) )(
    concatword "^n^f4* attr4:^t"            (entattr 3) )(
    concatword "^n^f4* attr5:^t"            (entattr 4) )
]


//#############################################################################################################################################
//  Particles:
_EditHud_EntAttr_particles = [(
        format "^n^f~* type:^t^f2%1 ^f~^"%2^f~^"" (
            entattr 0
        )(  if (_EH_ParticleTypeIsValid) [unescape (at $(concatword "_ParticleLabels_" (entattr 0)) 0)] [result "???"]
        )
    )(
        _EH_AttrLabel = (_EH_GetParticleAttrLabel 1)
        format "^n^f~* %1:^t%2 %3" (
            unescape $_EH_AttrLabel
        )(  if (|| (! $_EH_ParticleTypeIsValid) (strcmp (unescape $_EH_AttrLabel) "^f4attr2")) [concatword "^f4" (entattr 1)] [concatword "^f2" (entattr 1)]
        )(  if (|| (strcmp (unescape $_EH_AttrLabel) "^f~dir") (&& (|| (= (entattr 0) 4) (= (entattr 0) 7) ) (< (entattr 1) 6)) ) [
                unescape (at $_ParticleDirections (entattr 1))
            ][
                if (&& (strcmp (unescape $_EH_AttrLabel) "^f~variety") (>= (entattr 1) 256)) [
                    unescape (at $_ParticleVarieties (mod (entattr 1) 64))
                ]
            ]
        )
    )(
        _EH_AttrLabel = (_EH_GetParticleAttrLabel 2)
        format "^n^f~* %1:^t%2%3" (
            unescape $_EH_AttrLabel
        )(  if (|| (! $_EH_ParticleTypeIsValid) (strcmp (unescape $_EH_AttrLabel) "^f4attr3")) [result "^f4"] [result "^f2"]
        )(  at $entget 3 //entattr 2 <-- This doesn't return colors in 16-bit hex format.
        )
    )(
        _EH_AttrLabel = (_EH_GetParticleAttrLabel 3)
        format "^n^f~* %1:^t%2%3" (
            unescape $_EH_AttrLabel
        )(  if (|| (! $_EH_ParticleTypeIsValid) (strcmp (unescape $_EH_AttrLabel) "^f4attr4")) [result "^f4"] [result "^f2"]
        )(  if (= (entattr 0) 5) [entattr 3] [at $entget 4] //entattr 3 <-- This doesn't return colors in 16-bit hex format.
        )                                                   //at $entget 4 <-- This shouldn't return a color for particle_type 5 "meter".
    )(
        _EH_AttrLabel = (_EH_GetParticleAttrLabel 4)
        format "^n^f~* %1:^t%2" (
            unescape $_EH_AttrLabel
        )(  if (|| (! $_EH_ParticleTypeIsValid) (strcmp (unescape $_EH_AttrLabel) "^f4attr5")) [concatword "^f4" (entattr 4)] [concatword "^f2" (entattr 4)]
        )
    )
]
_EH_GetParticleAttrLabel = [
    if (_EH_ParticleTypeIsValid) [
        result (at $(concatword "_ParticleLabels_" (entattr 0)) $arg1)
    ][
        result (at $_ParticleLabels_ $arg1)
    ]
]


//#############################################################################################################################################
//  Pickups:
_EditHud_EntAttr_bullets      = $_EditHud_EntAttr_
_EditHud_EntAttr_cartridges   = $_EditHud_EntAttr_
_EditHud_EntAttr_grenades     = $_EditHud_EntAttr_
_EditHud_EntAttr_riflerounds  = $_EditHud_EntAttr_
_EditHud_EntAttr_rockets      = $_EditHud_EntAttr_
_EditHud_EntAttr_shells       = $_EditHud_EntAttr_
_EditHud_EntAttr_health       = $_EditHud_EntAttr_
_EditHud_EntAttr_healthboost  = $_EditHud_EntAttr_
_EditHud_EntAttr_greenarmour  = $_EditHud_EntAttr_
_EditHud_EntAttr_yellowarmour = $_EditHud_EntAttr_
_EditHud_EntAttr_quaddamage   = $_EditHud_EntAttr_


//#############################################################################################################################################
//  SinglePlayer:
_EditHud_EntAttr_barrel = [(
    concatword "^n^f~* yaw:^t^f2" (entattr 0) )(
    concatword "^n^f4* attr2:^t"  (entattr 1) )(
    concatword "^n^f4* attr3:^t"  (entattr 2) )(
    concatword "^n^f4* attr4:^t"  (entattr 3) )(
    concatword "^n^f4* attr5:^t"  (entattr 4) )
]
_EditHud_EntAttr_box = [(
    concatword "^n^f~* yaw:^t^f2" (entattr 0) )(
    concatword "^n^f4* attr2:^t"  (entattr 1) )(
    concatword "^n^f4* attr3:^t"  (entattr 2) )(
    concatword "^n^f4* attr4:^t"  (entattr 3) )(
    concatword "^n^f4* attr5:^t"  (entattr 4) )
]
_EditHud_EntAttr_elevator = [(
    concatword "^n^f~* yaw:^t^f2" (entattr 0) )(
    concatword "^n^f4* attr2:^t"  (entattr 1) )(
    concatword "^n^f4* attr3:^t"  (entattr 2) )(
    concatword "^n^f4* attr4:^t"  (entattr 3) )(
    concatword "^n^f4* attr5:^t"  (entattr 4) )
]
_EditHud_EntAttr_platform = [(
    concatword "^n^f~* yaw:^t^f2" (entattr 0) )(
    concatword "^n^f4* attr2:^t"  (entattr 1) )(
    concatword "^n^f4* attr3:^t"  (entattr 2) )(
    concatword "^n^f4* attr4:^t"  (entattr 3) )(
    concatword "^n^f4* attr5:^t"  (entattr 4) )
]
_EditHud_EntAttr_monster = [(
    concatword "^n^f~* yaw:^t^f2" (entattr 0) )(
    concatword "^n^f4* attr2:^t"  (entattr 1) )(
    concatword "^n^f4* attr3:^t"  (entattr 2) )(
    concatword "^n^f4* attr4:^t"  (entattr 3) )(
    concatword "^n^f4* attr5:^t"  (entattr 4) )
]
_EditHud_EntAttr_respawnpoint = [(
    concatword "^n^f~* yaw:^t^f2" (entattr 0) )(
    concatword "^n^f4* attr2:^t"  (entattr 1) )(
    concatword "^n^f4* attr3:^t"  (entattr 2) )(
    concatword "^n^f4* attr4:^t"  (entattr 3) )(
    concatword "^n^f4* attr5:^t"  (entattr 4) )
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
_CaptureBaseAmmoTypes = [
    "^f7random"
    "^f3shells"
    "^f0bullets"
    "^f2rockets"
    "^f1riflerounds"
    "^f8grenades"
    "^f4cartridges"
    "^f5INVALID"
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Particle Crap:

//#############################################################################################################################################
//            iType       Attr0                               Attr1             Attr2           Attr3           Attr4
_ParticleLabels_   = ["???"                               "^f4attr2"        "^f4attr3"      "^f4attr4"      "^f4attr5"      ]
_ParticleLabels_0  = ["flame & smoke"                     "^f~width"        "^f~size"       "^f~color"      "^f4attr5"      ] // Additive & Alpha
_ParticleLabels_1  = ["steam"                             "^f~dir"          "^f4attr3"      "^f4attr4"      "^f4attr5"      ] // Additive
_ParticleLabels_2  = ["blob fountain"                     "^f~dir"          "^f~color"      "^f4attr4"      "^f4attr5"      ] // Alpha-ish?
_ParticleLabels_3  = ["fireball"                          "^f~size"         "^f~color"      "^f4attr4"      "^f4attr5"      ] // ~
_ParticleLabels_4  = ["tracer varieties ^f4(256-319)"     "^f~variety"      "^f~length"     "^f~color"      "^f4attr5"      ] // Additive       DIR_OR_VAR
_ParticleLabels_5  = ["meter"                             "^f~fill"         "^f~color"      "^f4attr4"      "^f4attr5"      ] // ~
_ParticleLabels_6  = ["meter 2"                           "^f~fill"         "^f~color"      "^f~color"      "^f4attr5"      ] // ~
_ParticleLabels_7  = ["lightning varieties ^f4(256-319)"  "^f~variety"      "^f~length"     "^f~color"      "^f4attr5"      ] // Alpha          DIR_OR_VAR
_ParticleLabels_8  = $_ParticleLabels_
_ParticleLabels_9  = ["smoke varieties ^f4(256-319)"      "^f~variety"      "^f~width"      "^f~color"      "^f~lifetime"   ] // Additive
_ParticleLabels_10 = ["blob varieties ^f4(256-319)"       "^f~variety"      "^f~width"      "^f~color"      "^f~lifetime"   ] // Alpha-ish?
_ParticleLabels_11 = ["flame"                             "^f~width"        "^f~size"       "^f~color"      "^f4attr5"      ] // Additive
_ParticleLabels_12 = ["smoke"                             "^f~width"        "^f~size"       "^f~color"      "^f4attr5"      ] // Alpha
_ParticleLabels_13 = ["snowflake varieties ^f4(256-319)"  "^f~variety"      "^f~width"      "^f~color"      "^f~lifetime"   ] // Additive
_ParticleLabels_14 = $_ParticleLabels_
_ParticleLabels_15 = $_ParticleLabels_
_ParticleLabels_16 = $_ParticleLabels_
_ParticleLabels_17 = $_ParticleLabels_
_ParticleLabels_18 = $_ParticleLabels_
_ParticleLabels_19 = $_ParticleLabels_
_ParticleLabels_20 = $_ParticleLabels_
_ParticleLabels_21 = $_ParticleLabels_
_ParticleLabels_22 = $_ParticleLabels_
_ParticleLabels_23 = $_ParticleLabels_
_ParticleLabels_24 = $_ParticleLabels_
_ParticleLabels_25 = $_ParticleLabels_
_ParticleLabels_26 = $_ParticleLabels_
_ParticleLabels_27 = $_ParticleLabels_
_ParticleLabels_28 = $_ParticleLabels_
_ParticleLabels_29 = $_ParticleLabels_
_ParticleLabels_30 = $_ParticleLabels_
_ParticleLabels_31 = $_ParticleLabels_
_ParticleLabels_32 = ["lens flare"                        "^f~colorR"      "^f~colorG"      "^f~colorB"      "^f4attr5"      ]
_ParticleLabels_33 = ["lens flare"                        "^f~colorR"      "^f~colorG"      "^f~colorB"      "^f4attr5"      ]
_ParticleLabels_34 = ["lens flare"                        "^f~colorR"      "^f~colorG"      "^f~colorB"      "^f4attr5"      ]
_ParticleLabels_35 = ["lens flare"                        "^f~colorR"      "^f~colorG"      "^f~colorB"      "^f4attr5"      ]

//#############################################################################################################################################
_ParticleDirections = [
    "^f~^"up^""
    "^f~^"left^""
    "^f~^"forward^""
    "^f~^"down^""
    "^f~^"right^""
    "^f~^"backward^""
]

//#############################################################################################################################################
_ParticleVarieties = [
    "^f~^"circle on planeZ^""           // 0 of 63
    "^f~^"circle on planeX^""
    "^f~^"circle on planeY^""
    "^f~^"cylinder shell +Z^""
    "^f~^"cylinder shell +X^""
    "^f~^"cylinder shell +Y^""
    "^f~^"cone shell +Z^""
    "^f~^"cone shell +X^""
    "^f~^"cone shell +Y^""
    "^f~^"cone shell -Z^""
    "^f~^"cone shell -X^""
    "^f~^"cone shell -Y^""
    "^f~^"plane volume +Z^""
    "^f~^"plane volume +X^""
    "^f~^"plane volume +Y^""
    "^f~^"line volume on planeY +Z^""
    "^f~^"line volume on planeZ +X^""
    "^f~^"line volume on planeX +Y^""
    "^f~^"line volume on planeX +Z^""
    "^f~^"line volume on planeY +X^""
    "^f~^"line volume on planeZ +Y^""
    "^f~^"sphere^""
    "^f~^"sphere^""
    "^f~^"sphere^""

    //  These only work with smoke,blob,snow variety types.
    "^f~^"flat on planeZ -Z^""
    "^f~^"flat on planeX -Z^""
    "^f~^"flat on planeY -Z^""
    "^f~^"drip^""
    "^f~^"drip^""
    "^f~^"drip^"" //  These have no initialized state and just slowly fall or rise.
    "^f~^"drip^""
    "^f~^"drip^""

    "^f~^"inverse circle on planeZ^""   // 32 of 63
    "^f~^"inverse circle on planeX^""
    "^f~^"inverse circle on planeY^""
    "^f~^"cylinder shell -Z^""
    "^f~^"cylinder shell -X^""
    "^f~^"cylinder shell -Y^""
    "^f~^"inverse cone shell +Z^""
    "^f~^"inverse cone shell +X^""
    "^f~^"inverse cone shell +Y^""
    "^f~^"inverse cone shell -Z^""
    "^f~^"inverse cone shell -X^""
    "^f~^"inverse cone shell -Y^""
    "^f~^"plane volume -Z^""
    "^f~^"plane volume -X^""
    "^f~^"plane volume -Y^""
    "^f~^"line volume on planeY -Z^""
    "^f~^"line volume on planeZ -X^""
    "^f~^"line volume on planeX -Y^""
    "^f~^"line volume on planeX -Z^""
    "^f~^"line volume on planeY -X^""
    "^f~^"line volume on planeZ -Y^""
    "^f~^"inverse sphere^""
    "^f~^"inverse sphere^""
    "^f~^"inverse sphere^""

    //  These only work with smoke,blob,snow variety types.
    "^f~^"flat on planeZ -Z^""
    "^f~^"flat on planeX -Z^""
    "^f~^"flat on planeY -Z^""
    "^f~^"drip^""
    "^f~^"drip^""
    "^f~^"drip^"" //  These have no initialized state and just slowly fall or rise.
    "^f~^"drip^""
    "^f~^"drip^""                       // 63 of 63
]

